@ADD_CHARACTER_EXPLORER, ARG
FLAG:8 = 0

SELECTCASE ARG
;ヴィレッジャは、たまに処女
CASE 8
	SIF RAND:10 == 0
		ARG = 9
;ボスを倒しちゃってる場合
CASE 303
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 304
CASE 313
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 215
CASE 34
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 412
CASE 35
	SIF PALAM_EXPLORER("中ボス撃破")
		ARG = 412
CASE 38
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 311
CASE 444
	IF PALAM_EXPLORER("ボス撃破")
		IF RAND:2
			ARG = 222
		ELSE
			ARG = 316
		ENDIF
	ENDIF
CASE 47
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 319
ENDSELECT

;100以上の値なら、上位種
LOCAL = ARG / 100
ARG %= 100

ADDCHARA ARG

;呼んだキャラをTARGETにする
TARGET = CHARANUM-1
;基本種で無いなら素質とかが変化
IF LOCAL
	CALL HIGH_RANKER, ARG, LOCAL
	SIF CFLAG:8 == 0
		CFLAG:8 = LOCAL
ENDIF

CALL SET_NEWNAME

;スキュラ短編だったり長編だったり
IF NO:TARGET == 15
	IF RAND:2
		CFLAG:209 = 2
		EXP:レズ経験 += 20
		ABL:レズっ気 += 1
		TALENT:素直 = 1
		TALENT:両刀 = 1
		TALENT:旧支配者の名残 = 1
	ELSE
		CFLAG:209 = 1
		TALENT:痛みに弱い = 1
		TALENT:旧支配者の名残 = 1
	ENDIF
ENDIF

;追加技能獲得
CALL RESET_EXP_GAIN, TARGET
CALL GET_EXTALENT, TARGET
CALL GET_EXABL, TARGET

;名前ありキャラは変動無し
SIF COND("ユニーク") == 0 && RAND:4 == 0
	CALL CHAR_SAMON_T, ARG
;LV
CALL SET_LV_MUMA, TARGET
;ボディミミック
SIF NO:TARGET == 33
	ABL:ＬＶ = ABL:PLAYER:ＬＶ
;精力セット
CALL SET_COUNTER_BASE
;MAXBASE:精力 = TALENT:上位夢魔*5 + ABL:欲望 + ABL:技巧*3 + ABL:ＬＶ + PALAM_EXPLORER("敵の強さ")
;BASE:精力 = MAXBASE:精力

FLAG:8 = 1

;相手の人数を設定
CALL SET_PALAM_EXPLORER, "敵キャラ数", 1

@WANDERING_MONSTER
CALL ADD_CHARACTER_EXPLORER, PALAM_EXPLORER("制限エンカウント")

;ARGが0だと全部消去。それ以外の数字だとARG人残す
@DELMONSTER, ARG
IF ARG == 0
	DELCHARA CHARANUM-1
	TARGET = 0
ENDIF
SIF PALAM_EXPLORER("敵キャラ数") == 3 && ARG <= 2
	DELCHARA CHARANUM-1
SIF PALAM_EXPLORER("敵キャラ数") >= 2 && ARG <= 1
	DELCHARA CHARANUM-1


@SEXBATTLE_EXPLORER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_BASE6
;remember character acquisition status to show the message later if something changed
VARSET nLocal
FOR LOCAL, 1, 100
	nLocal:LOCAL = nBossAcquired:LOCAL
NEXT
nLocal:303 = nBossAcquired:303
nLocal:403 = nBossAcquired:403
nLocal:124 = nBossAcquired:124
nLocal:133 = nBossAcquired:133
nLocal:152 = nBossAcquired:152
nLocal:604 = nBossAcquired:604
nLocal:163 = nBossAcquired:163
nLocal:222 = nBossAcquired:222

;エンカウント回数+1
TFLAG:13 += 1

;戦闘パラメーターの初期化
VARSET TFLAG, 0, 15, 30
VARSET LOCALS
VARSET LOCAL

;一人目決定
CALL ADD_CHARACTER_EXPLORER, PALAM_EXPLORER("ランダムエンカウント")
LOCALS:1 = %NAME:TARGET%(LV:{ABL:ＬＶ})

;複数登場判定（探索場所ごと）
SELECTCASE TEXTS("探索先")
CASE "草原"
	;上位種は単独
	IF RAND:3 == 0 && CFLAG:8 == 0
		CALL WANDERING_MONSTER
		SIF RAND:3 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "エルトゥム森林"
	;上位種は単独
	IF TALENT:上位夢魔 || CFLAG:8 >= 2
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:30
			CALL WANDERING_MONSTER
	ENDIF
CASE "リュードゥフ洞"
	;ストゥーナはメローナが倒されていないならメローナを伴って現れる
	IF NO:TARGET == 34 && PALAM_EXPLORER("中ボス撃破") == 0 && (CONFIG("探索ボス") || GETCHARA(35) < 0)
		CALL ADD_CHARACTER_EXPLORER, 35
	;ヴィーヴル、メローナ、上位種でないなら複数かも
	ELSEIF NO:TARGET == 34 || NO:TARGET == 35 || (NO:TARGET == 12 && CFLAG:8 == 4)
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20 && CFLAG:8 == 0
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
CASE "アンショス海"
	;レヴィアタンはマーメイド２体とともに現れる
	IF NO:TARGET == 13 && CFLAG:8 == 3
		CALL ADD_CHARACTER_EXPLORER, 13
		CALL ADD_CHARACTER_EXPLORER, 13
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:15
		CALL WANDERING_MONSTER
		SIF RAND:5 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "ベルナドットの大迷路"
	;ベルナドットは２匹のアルプとともに現れる
	IF NO:TARGET == 47
		CALL ADD_CHARACTER_EXPLORER, 202
		CALL ADD_CHARACTER_EXPLORER, 202
	;ミミック以外なら基本的に複数
	ELSEIF NO:TARGET == 33
	ELSE
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
CASE "ゴズバーン砂漠"
	;エスプラーナは妹二人とともに現れる
	IF NO:TARGET == 38
		SIF CONFIG("探索ボス") || GETCHARA(39) < 0
			CALL ADD_CHARACTER_EXPLORER, 39
		SIF CONFIG("探索ボス") || GETCHARA(40) < 0
			CALL ADD_CHARACTER_EXPLORER, 40
	ELSE
		CALL WANDERING_MONSTER
		SIF RAND:5 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "キアズマの遊郭"
	;リンはタマモとハツメを伴う
	IF NO:TARGET == 44
		SIF RAND:2
			CALL ADD_CHARACTER_EXPLORER, 222
		SIF RAND:2
			CALL ADD_CHARACTER_EXPLORER, 316
	;ハツメ
	ELSEIF NO:TARGET == 22 && CFLAG:8 == 2 && RAND:3 == 0
	;リン以外は複数率高い
	ELSE
		SIF RAND:3 || ABL:ＬＶ < ABL:PLAYER:ＬＶ
			CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
ENDSELECT

;この時点で、PALAM_EXPLORER("敵キャラ数")に相手の人数が記録され、TARGETは最後に呼ばれたキャラとなっている
;かつ、PALAM_EXPLORER("一人目or二人目or三人目")に、それぞれのキャラ登録番号が代入されている
;LOCALS:1〜3はそれぞれの名前。
SIF PALAM_EXPLORER("敵キャラ数") == 3
	LOCALS:3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
SIF PALAM_EXPLORER("敵キャラ数") >= 2
	LOCALS:2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})

SETCOLOR DEF_COLOR("イエロー")

;全員の名前一緒
IF LOCALS:1 == LOCALS:2 && LOCALS:1 == LOCALS:3
	PRINTFORM %LOCALS:1%×３
;二人の名前が一緒
ELSEIF LOCALS:1 == LOCALS:2
	PRINTFORM %LOCALS:1%×２\@ PALAM_EXPLORER("敵キャラ数") == 3 ? %" "%and %LOCALS:3% # \@
ELSEIF LOCALS:1 == LOCALS:3
	PRINTFORM %LOCALS:1%×２ and %LOCALS:2%
ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2 && LOCALS:2 == LOCALS:3
	PRINTFORM %LOCALS:1% and %LOCALS:2%×２
;全員別
ELSE
	PRINTFORM %LOCALS:1%
	SIF PALAM_EXPLORER("敵キャラ数") >= 2
		PRINTFORM  and %LOCALS:2%
	SIF PALAM_EXPLORER("敵キャラ数") >= 3
		PRINTFORM  and %LOCALS:3%
ENDIF
PRINTFORMW  \@PALAM_EXPLORER("敵キャラ数") > 1 ? have appeared # has appeared \@!

RESETCOLOR

;遭遇口上を喋るのは一人目が代表
SETCOLOR C_P_GREEN
TRYCALL KOJO_MESSAGE_SEXBATTLE_A, NO:PALAM_EXPLORER("一人目")
RESETCOLOR
;ハツメの分身の術
IF NO:TARGET == 22 && CFLAG:8 == 2 && PALAM_EXPLORER("敵キャラ数") == 1
	PRINTFORML %CALLNAME:TARGET% uses Mirror Image!
	CALL ADD_CHARACTER_EXPLORER, 222
	CALL ADD_CHARACTER_EXPLORER, 222
	LOCALS:2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})
	LOCALS:3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
	PRINTFORML %CALLNAME:TARGET% is now split into 3!
ENDIF

;ムード 0,1〜4,5〜8,9以上でランク分け。
CALL SET_PALAM_EXPLORER, "ムード", TALENT:PLAYER:魅惑+TALENT:PLAYER:謎の魅力+TALENT:PLAYER:魅力

;戦闘ターン初期化
TFLAG:12 = 0
$START_BATTLE
TFLAG:12 += 1

;プレイヤーが攻撃選択
CALL SELECTCOM_SEXBATTLE_EXPLORER

;相手がイってない
IF BASE:精力 > 0
	;相手の反撃
	CALL COUNTER_SEXBATTLE_EXPLORER

	;イかされてしまったなら、ストックの判定
	SIF BASE:PLAYER:精力 <= 0
		CALL TEXT_LOSE_EXPLORER

	;勝負がついていないなら続行
	SIF BASE:PLAYER:精力 > 0
		GOTO START_BATTLE

	;敗北確定
	CALL DELMONSTER
	RETURN 0
;相手をイかせた
ELSEIF BASE:精力 <= 0
	CALL TEXT_WIN_EXPLORER
	;勝負がついていないなら続行
	SIF BASE:精力 > 0
		GOTO START_BATTLE
ENDIF

;PRINTFORML "戦闘ターン数" = {PALAM_EXPLORER("戦闘ターン数")}
;戦闘ターン数に応じて時間制限が迫る
COUNT:99 += PALAM_EXPLORER("戦闘ターン数")/5

;経験値算出
LOCAL = (MAX(1, ABL:ＬＶ - ABL:PLAYER:ＬＶ) + ABL:ＬＶ/4 + TALENT:上位夢魔*5) * PALAM_EXPLORER("敵キャラ数")

;経験値Get
EXP:PLAYER:経験値 += MULTIPLY(LOCAL, 100 + TALENT:MASTER:取得経験値率)
;助手にもおこぼれ
IF PLAYER == MASTER && ASSI
	EXP:ASSI:助手経験 += MAX(LOCAL/2, 1)
	EXP:ASSI:経験値 += MAX(LOCAL/2, 1)
ENDIF
;PRINTFORM {LOCAL}Ｐの経験値を得た。

;IF PALAM_EXPLORER("ムード") > 10 && RAND:100 == 0
;	PRINTFORM %CALLNAME:TARGET%のパンツ獲得。
IF PALAM_EXPLORER("ムード") > 10 && RAND:20 == 0 && ITEM:ムーンドロップス + I:99 < 99
	PRINTFORM %CALLNAME:TARGET% drops some 
	CALL PRINT_COLORTEXT, "Moondrops", DEF_COLOR("黄色")
	PRINTFORM . 
	IF PLAYER != MASTER && COND("夢魔", PLAYER)
		PRINTFORM %CALLNAME:PLAYER% is pleased greatly, consuming it delightfully.
		CFLAG:PLAYER:2 += 50
		BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	ELSE
		I:99 += 1
	ENDIF
	PRINTL 
ELSEIF PALAM_EXPLORER("ムード") > RAND:20
	PRINTFORM %CALLNAME:TARGET% drops a Treasure Box. 
	CALL SET_PALAM_EXPLORER, "財宝期待値", PALAM_EXPLORER("ムード") * (4 + RAND:7)
	;ボーナス
	CALL SET_PALAM_EXPLORER, "アイテムレベル", 1
	CALL TREASURE_BOX_EXPLORER_B, TARGET
	CALL SET_PALAM_EXPLORER, "アイテムレベル", -1
ELSE
	IF PALAM_EXPLORER("敵キャラ数") > RAND:3
		IF ITEM:淫魔の雫 + I:78 < 999 && COND("夢魔") && RAND:5
			PRINTFORM %CALLNAME:PLAYER% found 
			CALL PRINT_COLORTEXT, "Demon Drops", DEF_COLOR("黄色")
			PRINTFORM . 
			I:78 += 1
		ENDIF
	ENDIF
	SELECTCASE TEXTS("探索先")
	CASE "キアズマの遊郭"
	CASEELSE
		MONEY:5 = (PALAM_EXPLORER("敵の強さ") + ABL:ＬＶ) * (5 + RAND:5) * PALAM_EXPLORER("敵キャラ数")
		CALL PRINT_COLORTEXT, MONEY_C(MONEY:5), DEF_COLOR("黄色")
		PRINTFORML  acquired.
		MONEY:6 += MONEY:5
	ENDSELECT
ENDIF

;ボス撃破フラグ
SELECTCASE NO:TARGET
CASE 3
	IF CFLAG:8 == 3
		CALL SET_PALAM_EXPLORER, "エルトゥム森林：ボス撃破"
		CALL SET_PALAM_EXPLORER, "ボス撃破"
	ENDIF
CASE 13
	IF CFLAG:8 == 3
		CALL SET_PALAM_EXPLORER, "アンショス海：ボス撃破"
		CALL SET_PALAM_EXPLORER, "ボス撃破"
	ENDIF
CASE 34
	CALL SET_PALAM_EXPLORER, "リュードゥフ洞：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
	IF PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
		CALL SET_PALAM_EXPLORER, "リュードゥフ洞：中ボス撃破"
		CALL SET_PALAM_EXPLORER, "中ボス撃破"
	ENDIF
CASE 35
	CALL SET_PALAM_EXPLORER, "リュードゥフ洞：中ボス撃破"
	CALL SET_PALAM_EXPLORER, "中ボス撃破"
CASE 38
	CALL SET_PALAM_EXPLORER, "ゴズバーン砂漠：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
CASE 44
	CALL SET_PALAM_EXPLORER, "キアズマの遊郭：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
CASE 47
	CALL SET_PALAM_EXPLORER, "ベルナドットの大迷路：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
ENDSELECT

;仲魔判定(名前ありのボスはいつでも仲魔に入りたがる)
SELECTCASE TEXTS("探索先")
;CASE "キアズマの遊郭"
CASEELSE
	IF FLAG:246 == 3 || NO:TARGET == 34 || NO:TARGET == 35 || NO:TARGET == 38 || TALENT:初めての人
		LOCAL = 3
		;ムードが高い
		SIF PALAM_EXPLORER("ムード") > 10
			LOCAL += PALAM_EXPLORER("ムード") - 9
		;謎の魅力
		SIF TALENT:PLAYER:謎の魅力
			LOCAL += 1
		;初めての人
		SIF TALENT:初めての人
			LOCAL = 10

		;ボス
		SELECTCASE NO:TARGET
		CASE 34
			;ストゥーナが仲間に居る？
			IF FIND_COND("Ｃストゥーナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		CASE 35
			;メローナが仲間に居る？
			IF FIND_COND("Ｃメローナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		CASE 38
			;エスプラーナが仲間に居る？
			IF FIND_COND("Ｃエスプラーナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		ENDSELECT

		;仲魔になる
		IF LOCAL > RAND:10
			PRINTL 
			PRINTFORM %CALLNAME:TARGET% 
			;ストメロ限定
			IF NO:TARGET == 34 && PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
				PRINTFORML and Merona get up, seemingly being ready to become %CALLNAME:PLAYER%'s friends...
			;ハーピー姉妹
			ELSEIF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
				PRINTFORML and her sisters get up, seemingly being ready to become %CALLNAME:PLAYER%'s friends...
			;強気かツンデレ（ストゥーナとリザードマン）限定
			ELSEIF NO:TARGET == 12 || NO:TARGET == 34
				IF RAND:3 == 0
					PRINTFORM seems to be somewhat %SPLIT_R("annoyed:bitter:frustrated:vexed")% 
				ELSEIF RAND:2 == 0
					PRINTFORM verbally %SPLIT_R("curses:abuses")% 
				ELSE
					PRINTFORM is %FSYN("pant:ing")% 
				ENDIF
				IF RAND:3 == 0
					PRINTFORML but keeps %SPLIT_R("staring:looking:glaring")% at %CALLNAME:PLAYER% with expectation...
				ELSEIF RAND:2 == 0
					PRINTFORML but blushes slightly while stealing glances at %CALLNAME:PLAYER%...
				ELSE
					PRINTFORML but keeps taunting %CALLNAME:PLAYER%, %SPLIT_R("tempting:seducing")% %HIM_HER(PLAYER)%...
				ENDIF
			;その他汎用
			ELSE
				IF RAND:4 == 0
					PRINTFORM lies still, only slightly raising %HIS_HER(TARGET)% head, 
				ELSEIF RAND:3 == 0
					PRINTFORM %FSYN("tremble:s")% with joy and pleasure, 
				ELSEIF RAND:2 == 0
					PRINTFORM %FSYN("start:s")% playing with %HIS_HER(TARGET)% %BODYPART("Body", TARGET)%, 
				ELSE
					PRINTFORM gets up slowly, 
				ENDIF
				IF RAND:5 == 0
					PRINTFORML while calling %CALLNAME:PLAYER%'s name over and over again...
				ELSEIF RAND:4 == 0
					PRINTFORML while %SPLIT_R("staring:looking:glaring")% at %CALLNAME:PLAYER% %SPLIT_R("seductively:passionately")%...
				ELSEIF RAND:3 == 0
					PRINTFORML while drawing closer to %CALLNAME:PLAYER%'s %BODYPART("Feet", PLAYER)%...
				ELSEIF RAND:2 == 0
					PRINTFORML while reaching for %CALLNAME:PLAYER%'s hand...
				ELSE
					PRINTFORML expecting something more out of this...
				ENDIF
			ENDIF
			
			;buy out the satisfied prostitute
			IF TEXTS("探索先") == "キアズマの遊郭"
				LOCAL = RAND(6000, 12000) + RAND:5000 - RAND:1000
				SETCOLOR C_P_GREEN
				TRYCALL KOJO_MESSAGE_SEXBATTLE_D, NO:TARGET, 0, LOCAL
				RESETCOLOR
				PRINTFORML [0] Agree (Money: %MONEY_C(MONEY)%) [1] Decline
			ELSE
				PRINTFORML [0] \@ GROUPMATCH(NO:TARGET, 34, 38) && PALAM_EXPLORER("敵キャラ数") >= 2 ? Let them in # Take %HIS_HER(TARGET)% hand\@ [1] Walk away
			ENDIF

			
			CALL INPUT_SELECT, 2

			IF RESULT == 0
				IF TEXTS("探索先") == "キアズマの遊郭"
					IF MONEY < LOCAL
						CALL PRINTL_COLORTEXT, @"Not enough money!", DEF_COLOR("黄色")
						SETCOLOR C_P_GREEN
						TRYCALL KOJO_MESSAGE_SEXBATTLE_D, NO:TARGET, 1, 0
						RESETCOLOR
						RETURN 0
					ENDIF
					CALL PRINTL_COLORTEXT, @"You pay %CALLNAME:TARGET% %MONEY_C(LOCAL)%!", DEF_COLOR("黄色")
					MONEY -= LOCAL
				ENDIF
				SETCOLOR C_P_GREEN
				TRYCALL KOJO_MESSAGE_SEXBATTLE_C, NO:TARGET
				RESETCOLOR
				PRINTFORM %CALLNAME:TARGET% 
				;再召喚不可フラグ(TARGETがボス以外なら SETFLAG, "アイテムフラグ調整" で元に戻る)
				FLAG:(NO:TARGET + 999) = 0
				;調教の初回は表示しない
				CFLAG:201 = 1
				;好感度ボーナス
				CFLAG:2 = RAND:100 + 100
				
				;acquisition flag for the rest of the bosses
				SIF ( NO:TARGET == 35 || NO:TARGET == 44 || NO:TARGET == 47 ) && nBossAcquired:(NO:TARGET) == 0
					nBossAcquired:(NO:TARGET) ++
				;acquisition flag for the rest of the bosses/uniques
				SIF ( COND("Ｃスクーグスロー") ) && nBossAcquired:303 == 0
					nBossAcquired:303 ++
				SIF ( COND("Ｃティターニア") ) && nBossAcquired:403 == 0
					nBossAcquired:403 ++
				SIF ( COND("Ｃヴィーヴル") ) && nBossAcquired:124 == 0
					nBossAcquired:124 ++
				SIF ( COND("Ｃレヴィアタン") ) && nBossAcquired:133 == 0
					nBossAcquired:133 ++
				SIF ( COND("Ｃカリュブディス") ) && nBossAcquired:152 == 0
					nBossAcquired:152 ++
				SIF ( COND("Ｃスフィンクス") ) && nBossAcquired:604 == 0
					nBossAcquired:604 ++
				SIF ( COND("Ｃタマモ") ) && nBossAcquired:163 == 0
					nBossAcquired:163 ++
				SIF ( COND("Ｃハツメ") ) && nBossAcquired:222 == 0
					nBossAcquired:222 ++
				IF NO:TARGET == 34 && PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
					PRINTFORM and Merona 
					FLAG:1034 = 0
					CFLAG:PALAM_EXPLORER("二人目"):201 = 1
					;好感度ボーナス
					CFLAG:PALAM_EXPLORER("二人目"):2 = 100
					;acquisition flag
					nBossAcquired:34 ++
					nBossAcquired:35 ++
					CALL DELMONSTER, 2
				ELSEIF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
					nBossAcquired:38 ++
					IF PALAM_EXPLORER("敵キャラ数") >= 3
						PRINTFORM and her sisters 
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
						CFLAG:PALAM_EXPLORER("三人目"):201 = 1
						CFLAG:PALAM_EXPLORER("三人目"):2 = 100
						;acquisition flag
						nBossAcquired:39 ++
						nBossAcquired:40 ++
					ELSEIF NO:PALAM_EXPLORER("二人目") == 39
						PRINTFORM and Riz-Raz
						FLAG:1038 = 0
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
						;acquisition flag
						nBossAcquired:39 ++
					ELSEIF NO:PALAM_EXPLORER("二人目") == 40
						PRINTFORM and Rocco-Barocco 
						FLAG:1039 = 0
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
						;acquisition flag
						nBossAcquired:40 ++
					ENDIF
				ELSE
					CALL DELMONSTER, 1
				ENDIF
				PRINTFORML will be brought back with you together.
				I:999 = 1
				COUNT:99 = 29
				CALL ACQUISITION_BOSS
				RETURN 0
			ELSE
				IF TEXTS("探索先") == "キアズマの遊郭"
					SETCOLOR C_P_GREEN
					TRYCALL KOJO_MESSAGE_SEXBATTLE_D, NO:TARGET, 1, 0
					RESETCOLOR
				ENDIF
				;acquisition flag for bosses if not taking them (uniques don't need them because they can be encountered anyway)
				IF NO:TARGET == 34 && PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
					nBossAcquired:34 ++
					nBossAcquired:35 ++
				ELSEIF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
					nBossAcquired:38 ++
					IF PALAM_EXPLORER("敵キャラ数") >= 3
						nBossAcquired:39 ++
						nBossAcquired:40 ++
					ELSEIF NO:PALAM_EXPLORER("二人目") == 39
						nBossAcquired:39 ++
					ELSEIF NO:PALAM_EXPLORER("二人目") == 40
						nBossAcquired:40 ++
					ENDIF
				ENDIF
				
				;acquisition flag for the rest of the bosses
				SIF ( NO:TARGET == 44 || NO:TARGET == 47 ) && nBossAcquired:(NO:TARGET) == 0
					nBossAcquired:(NO:TARGET) ++
				CALL ACQUISITION_BOSS
			ENDIF
		ENDIF
	ENDIF
ENDSELECT
PRINTL 
;精力とLINECOUNTを記録
MEMO_BASE6 = BASE:PLAYER:精力
MEMO_LINECOUNT = LINECOUNT

;エンカウント回数が3+4nでセリフを出してみる
SETCOLOR C_P_GREEN
SIF PLAYER == MASTER && ASSI && (PALAM_EXPLORER("エンカウント回数") + 1)%4 == 0
	TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "探索：ご休憩", ASSI
RESETCOLOR
;セリフが無いor精力の変動がなければ汎用地の文出力
IF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1
	CALL HEAL_EXPLORER, "Taking a breather..."
ELSEIF MEMO_BASE6 == BASE:PLAYER:精力
	PRINTL 
	CALL HEAL_EXPLORER, "Taking a breather..."
ENDIF

PRINTFORML The Exploration continues.
nExploringLines = 0
CALL DELMONSTER

WAIT

@HEAL_EXPLORER, ARGS, ARGS:1
PRINTFORM %ARGS% VIG Recovery {BASE:PLAYER:精力}→

;精力が33%回復。食事を追加してるなら、その分も加味。
BASE:PLAYER:精力 = MIN(BASE:PLAYER:精力 + MAXBASE:PLAYER:精力/3 + PALAM_EXPLORER("精力回復補正"), MAXBASE:PLAYER:精力)

PRINTFORM {BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}%ARGS:1%. 


@TEXT_LOSE_EXPLORER
SETCOLOR DEF_COLOR("イエロー")

PRINTFORM %CALLNAME:PLAYER% 
IF PALAM_EXPLORER("Ｖインサート")
	PRINTFORML %SPLIT_R("pumps:fills:pours")% %CALLNAME:TARGET%'s %BODYPART("Vagina", TARGET)% full of %SEMEN(PLAYER,1)%!
	CALL SETFLAG, "膣内精液経験", 1
ELSEIF PALAM_EXPLORER("Ａインサート")
	PRINTFORML %SPLIT_R("pumps:fills:pours")% %CALLNAME:TARGET%'s %BODYPART("Anus", TARGET)% full of %SEMEN(PLAYER,1)%!
	CALL SETFLAG, "肛内精液経験", 1
ELSEIF PALAM_EXPLORER("エースオブスペード")
	PRINTFORML %FSYN("orgasm:s")% from having her %BODYPART("Crotch")% rubbed against %CALLNAME:TARGET%'s!
ELSEIF PALAM_EXPLORER("キス") || PALAM_EXPLORER("フェラチオ")
	PRINTFORML %FSYN("orgasm:s")% from %CALLNAME:TARGET%'s skillful tongue technique!
	CALL SETFLAG, "精飲経験", 1
ELSEIF PALAM_EXPLORER("手コキ")
	PRINTFORML %FSYN("orgasm:s")% from %CALLNAME:TARGET%'s skillful hand technique!
ELSEIF PALAM_EXPLORER("足コキ")
	PRINTFORML %FSYN("orgasm:s")% from %CALLNAME:TARGET%'s skillful foot technique!
ELSEIF PALAM_EXPLORER("顔面騎乗") || PALAM_EXPLORER("シックスナイン")
	PRINTFORML %FSYN("orgasm:s")% from %FSYN("intense")% facesitting done by %CALLNAME:TARGET%!
ELSEIF PALAM_EXPLORER("パイズリ")
	IF !HAS_PENIS(PLAYER)
		PRINTFORML %FSYN("orgasm:s")% from %CALLNAME:TARGET%'s %FSYN("intense")% breast caress!
	ELSE
		PRINTFORML yields to the power of %CALLNAME:TARGET%'s %BODYPART("Chest", TARGET)%!
	ENDIF
ELSE
	PRINTFORML gets pleasured enough by %CALLNAME:TARGET% to %FSYN("orgasm")%!
ENDIF

RESETCOLOR

EXP:経験値 += MAX(2*(ABL:PLAYER:ＬＶ - ABL:ＬＶ), 0) + 3
EXP:イかせた回数 += 1
IF TALENT:PLAYER:オンナ
	EXP:PLAYER:絶頂経験 += 1
ELSEIF TALENT:PLAYER:ふたなり
	EXP:PLAYER:絶頂経験 += 1
	EXP:PLAYER:射精経験 += 1
	EXP:精液経験 += 2
ELSE
	EXP:PLAYER:射精経験 += 1
	EXP:精液経験 += 2
ENDIF

;決着台詞
SETCOLOR C_P_GREEN
SIF TALENT:初めての人
	CALL KOJO_MESSAGE_DOUTEI_SHOOT
TRYCALL KOJO_MESSAGE_SEXBATTLE_B, NO:TARGET
RESETCOLOR

IF BASE:PLAYER:ストック > 0
	BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	BASE:PLAYER:ストック -= 1
	PRINTFORML However %CALLNAME:PLAYER% %SPLIT_R(@"musters %HIS_HER(PLAYER)% willpower:focuses intently")% and rises once again...! (Stock:{BASE:PLAYER:ストック}/{MAXBASE:PLAYER:ストック})
ENDIF

WAIT

@TEXT_WIN_EXPLORER
PRINTFORM %CALLNAME:PLAYER% 

R = RAND:3

IF PALAM_EXPLORER("両穴征服")
	PRINTFORML %FSYN("stir:s")% both of %CALLNAME:TARGET%'s holes, satisfying her.
ELSEIF PALAM_EXPLORER("Ｖインサート")
	PRINTFORML %FSYN("intense:ly")% %SPLIT_R("fucks:rapes")% all of the resistance from %CALLNAME:TARGET%.
ELSEIF PALAM_EXPLORER("Ａインサート")
	PRINTFORML anally %SPLIT_R("fucks:rapes")% %CALLNAME:TARGET% into submission.
ELSEIF PALAM_EXPLORER("エースオブスペード")
	PRINTFORML keeps rubbing her %BODYPART("Crotch")% against %CALLNAME:TARGET%'s until satisfying her.
ELSEIF PALAM_EXPLORER("ヴァッジ")
	IF R == 2
		PRINTFORML uses %HIS_HER(PLAYER)% tongue caress to calm down %CALLNAME:TARGET%'s aching %FSYN("vagina")%.
	ELSEIF R == 1
		PRINTFORML slips %HIS_HER(PLAYER)% %NAMES("finger", PLAYER)%s inside %CALLNAME:TARGET%'s %BODYPART("Vagina", TARGET)%, skilfully %SPLIT_R("satisfying:pleasuring")% her.
	ELSE
		PRINTFORML pleasures %CALLNAME:TARGET%'s %BODYPART("Vagina", TARGET)% %FSYN("intense:ly")%, %SPLIT_R(@"satisfying %HIM_HER(TARGET)%:calming %HIM_HER(TARGET)% down:defeating %HIM_HER(TARGET)%:making %HIM_HER(TARGET)% happy:making %HIM_HER(TARGET)% lose all desire to resist")%.
	ENDIF
ELSEIF PALAM_EXPLORER("アナル")
	IF R == 2
		PRINTFORML uses %HIS_HER(PLAYER)% tongue caress to calm down %CALLNAME:TARGET%'s aching %FSYN("anus")%.
	ELSEIF R == 1
		PRINTFORML slips %HIS_HER(PLAYER)% %NAMES("finger", PLAYER)%s inside %CALLNAME:TARGET%'s %BODYPART("Anus", TARGET)%, skilfully %SPLIT_R("satisfying:pleasuring")% %HIM_HER(TARGET)%.
	ELSE
		PRINTFORML pleasures %CALLNAME:TARGET%'s %BODYPART("Anus", TARGET)% %FSYN("intense:ly")%, %SPLIT_R(@"satisfying %HIM_HER(TARGET)%:calming %HIM_HER(TARGET)% down:defeating %HIM_HER(TARGET)%:making %HIM_HER(TARGET)% happy:making %HIM_HER(TARGET)% lose all desire to resist")%.
	ENDIF
ELSEIF PALAM_EXPLORER("バスト")
	IF R == 2
		PRINTFORML uses %HIS_HER(PLAYER)% tongue caress to calm down %CALLNAME:TARGET%'s aching %BODYPART("Nipples", TARGET)%.
	ELSEIF R == 1
		PRINTFORML %FSYN("rub:s")% %CALLNAME:TARGET%'s %BODYPART("Chest", TARGET)% %FSYN("intense:ly")%, skilfully %SPLIT_R("satisfying:pleasuring")% %HIM_HER(TARGET)%.
	ELSE
		PRINTFORML gropes %CALLNAME:TARGET%'s %BODYPART("Chest", TARGET)% and gently massages them, %SPLIT_R(@"satisfying %HIM_HER(TARGET)%:calming %HIM_HER(TARGET)% down:defeating %HIM_HER(TARGET)%:making %HIM_HER(TARGET)% happy:making %HIM_HER(TARGET)% lose all desire to resist")%.
	ENDIF
ELSEIF PALAM_EXPLORER("キス")
	IF R == 2
		PRINTFORML makes a passionate kiss with %CALLNAME:TARGET%, calming %HIM_HER(TARGET)% down. 
	ELSEIF R == 1
		PRINTFORML joins %HIS_HER(PLAYER)% lips with %CALLNAME:TARGET%'s in a sloppy hot kiss, skilfully %SPLIT_R("satisfying:pleasuring")% %HIM_HER(TARGET)%.
	ELSE
		PRINTFORML exchanges a hot kiss with %CALLNAME:TARGET%, %SPLIT_R(@"satisfying %HIM_HER(TARGET)%:calming %HIM_HER(TARGET)% down:defeating %HIM_HER(TARGET)%:making %HIM_HER(TARGET)% happy:making %HIM_HER(TARGET)% lose all desire to resist")%.
	ENDIF
ELSE
	PRINTFORML %FSYN("stir:s")% %CALLNAME:TARGET% %FSYN("intense:ly")%, %SPLIT_R(@"satisfying %HIM_HER(TARGET)%:calming %HIM_HER(TARGET)% down:defeating %HIM_HER(TARGET)%:making %HIM_HER(TARGET)% happy:making %HIM_HER(TARGET)% lose all desire to resist")%.
ENDIF

EXP:絶頂経験 += 1
SETCOLOR C_P_GREEN
TRYCALL KOJO_MESSAGE_SEXBATTLE_B, NO:TARGET
RESETCOLOR
;後が控えてる？
IF TARGET != PALAM_EXPLORER("一人目")
	TARGET -= 1
	;ムード以外の脱衣、インサート、行動フラグリセット
	CALL SET_PALAM_EXPLORER, "戦闘フラグリセット"
ENDIF

@SELECTCOM_SEXBATTLE_EXPLORER
CALL SET_PALAM_EXPLORER, "行動", 0
SELECTCASE PALAM_EXPLORER("ムード")
;ムードLV0
CASE 0
	CALL SET_PALAM_EXPLORER, "テンダーキッス"
;ムードLV1
CASE IS <= 4
	IF RAND:(PALAM_EXPLORER("ムード") + 1) == 0
		CALL SET_PALAM_EXPLORER, "テンダーキッス"
	ELSEIF TALENT:PLAYER:甘い言葉 && RAND:(PALAM_EXPLORER("ムード") + 3) == 0
		CALL SET_PALAM_EXPLORER, "甘い言葉"
	ELSEIF PALAM_EXPLORER("脱衣") == 0 && RAND:PALAM_EXPLORER("ムード") > RAND:4
		CALL SET_PALAM_EXPLORER, "脱衣", 1
	ELSEIF RAND:4 == 0
		CALL SET_PALAM_EXPLORER, "アナル"
	ELSEIF RAND:3 == 0
		CALL SET_PALAM_EXPLORER, "ヴァッジ"
	ELSEIF RAND:2
		CALL SET_PALAM_EXPLORER, "バスト"
	ELSE
		CALL SET_PALAM_EXPLORER, "キス"
	ENDIF
;ムードLV2以上
CASEELSE
	IF PALAM_EXPLORER("ムード") < 10 && RAND:(PALAM_EXPLORER("ムード") + 1) == 0
		CALL SET_PALAM_EXPLORER, "テンダーキッス"
	ELSEIF PALAM_EXPLORER("ムード") < 10 && TALENT:PLAYER:甘い言葉 && RAND:PALAM_EXPLORER("ムード") <= 1
		CALL SET_PALAM_EXPLORER, "甘い言葉"
	ELSEIF PALAM_EXPLORER("Ａインサート") && RAND:PALAM_EXPLORER("ムード")
		SIF RAND:4 == 0
			CALL SET_PALAM_EXPLORER, "Ｖインサート"
	ELSEIF PALAM_EXPLORER("Ｖインサート") && RAND:PALAM_EXPLORER("ムード")
		SIF RAND:4 == 0 && (TALENT:Ａ敏感 || TALENT:PLAYER:サド || COND("♀に弱い", PLAYER)) && RAND:(1 + EXP:PLAYER:Ａ挿入経験) > RAND:(1 + EXP:PLAYER:Ｖ挿入経験)
			CALL SET_PALAM_EXPLORER, "Ａインサート"
	;インサート等
	ELSEIF PALAM_EXPLORER("脱衣") && PALAM_EXPLORER("体位") == 0 && RAND:PALAM_EXPLORER("ムード") >= 2 && TALENT:妖精 == 0
		;女ならエースオブスペード。ふたなりは相手が両刀ならたまにやる
		IF TALENT:PLAYER:オンナ || (TALENT:PLAYER:ふたなり && TALENT:両刀 && RAND:4 == 0)
			CALL SET_PALAM_EXPLORER, "エースオブスペード"
		;アナルセックス
		ELSEIF (TALENT:Ａ敏感 || TALENT:PLAYER:サド || COND("♀に弱い", PLAYER)) && RAND:(1 + EXP:PLAYER:Ａ挿入経験) > RAND:(1 + EXP:PLAYER:Ｖ挿入経験)
			CALL SET_PALAM_EXPLORER, "Ａインサート"
		ELSEIF (TALENT:PLAYER:童貞 && CONFIG("脱童貞フィルタ") ) || TALENT:処女
			IF RAND:4 == 0
				CALL SET_PALAM_EXPLORER, "アナル"
			ELSEIF RAND:3 == 0
				CALL SET_PALAM_EXPLORER, "ヴァッジ"
			ELSEIF RAND:2
				CALL SET_PALAM_EXPLORER, "バスト"
			ELSE
				CALL SET_PALAM_EXPLORER, "キス"
			ENDIF
		ELSE
			CALL SET_PALAM_EXPLORER, "Ｖインサート"
		ENDIF
	ELSEIF PALAM_EXPLORER("脱衣") == 0 && RAND:PALAM_EXPLORER("ムード") >= 1
		CALL SET_PALAM_EXPLORER, "脱衣", 1
	ELSEIF RAND:4 == 0
		CALL SET_PALAM_EXPLORER, "アナル"
	ELSEIF RAND:3 == 0
		CALL SET_PALAM_EXPLORER, "ヴァッジ"
	ELSEIF RAND:2
		CALL SET_PALAM_EXPLORER, "バスト"
	ELSE
		CALL SET_PALAM_EXPLORER, "キス"
	ENDIF
ENDSELECT

VARSET LOCAL
IF PALAM_EXPLORER("テンダーキッス")
	BASE:精力 -= 1
	CALL SET_PALAM_EXPLORER, "ムード", 2
	EXP:キス経験 += 1
	EXP:PLAYER:キス経験 += RAND:2
ELSEIF PALAM_EXPLORER("キス")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:舌使い
	LOCAL = TALENT:Ｍ敏感
	EXP:キス経験 += 1
	EXP:PLAYER:キス経験 += 1
ELSEIF PALAM_EXPLORER("バスト")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌使い + ABL:PLAYER:指使い)/2
	LOCAL = TALENT:Ｂ敏感
	EXP:PLAYER:手淫経験 += 1
ELSEIF PALAM_EXPLORER("アナル")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌使い + ABL:PLAYER:指使い)/2
	LOCAL = TALENT:Ａ敏感
	EXP:Ａ経験 += 1
	EXP:PLAYER:Ａ責め経験 += 1
ELSEIF PALAM_EXPLORER("アナル")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌使い + ABL:PLAYER:指使い)/2
	LOCAL = TALENT:Ｃ敏感 + TALENT:Ｖ敏感
	IF RAND:2 == 0
		EXP:PLAYER:クンニ経験 += 1
	ELSE
		EXP:PLAYER:手淫経験 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("Ｖインサート")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰使い + 1
	LOCAL = TALENT:Ｖ敏感

	IF TALENT:PLAYER:童貞
		SETCOLOR DEF_COLOR("イエロー")
		PRINTFORML %CALLNAME:PLAYER% has lost %HIS_HER(PLAYER)% Male Virginity!
		RESETCOLOR
		TALENT:PLAYER:童貞 = 0
		TALENT:初めての人 = 1
		EXP:童貞狩り経験 += 1
		EXP:経験値 += 50
		SETCOLOR C_P_GREEN
		CALL KOJO_MESSAGE_DOUTEI
		RESETCOLOR
	ENDIF
	IF PENIS(PLAYER)
		EXP:PLAYER:Ｖ挿入経験 += 1
		EXP:Ｖ性交経験 += 1
	ENDIF
	EXP:PLAYER:腰使い経験 += 1
	TALENT:処女 = 0
	EXP:Ｖ経験 += 2
ELSEIF PALAM_EXPLORER("Ａインサート")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰使い
	IF TALENT:Ａ敏感
		BASE:精力 -= 2
		CALL SET_PALAM_EXPLORER, "ムード", RAND:2
	ELSE
		CALL SET_PALAM_EXPLORER, "ムード", -2
	ENDIF
	IF PENIS(PLAYER)
		EXP:PLAYER:Ａ挿入経験 += 1
		EXP:Ａ性交経験 += 1
	ENDIF
	EXP:PLAYER:腰使い経験 += 1
	TALENT:Ａ処女 = 0
	EXP:Ａ経験 += 2
;エースオブスペード
ELSEIF PALAM_EXPLORER("エースオブスペード")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰使い + 1
	LOCAL = TALENT:Ｃ敏感
	EXP:PLAYER:腰使い経験 += 1
;甘い言葉
ELSEIF PALAM_EXPLORER("甘い言葉")
	CALL SET_PALAM_EXPLORER, "ムード", 3
ENDIF

;弱点突き
IF LOCAL
	BASE:精力 -= 1 + (1 + ABL:PLAYER:技巧)/2
	CALL SET_PALAM_EXPLORER, "ムード", 1
ENDIF

;イきそう
SIF BASE:精力 < MAXBASE:精力/3 && PALAM_EXPLORER("ムード") < 5
	CALL SET_PALAM_EXPLORER, "ムード", 1

;レズ、BL経験
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	EXP:PLAYER:レズ経験 += 2
	EXP:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	EXP:PLAYER:ＢＬ経験 += 2
	EXP:ＢＬ経験 += 2
ENDIF


@ATTACK_SEXBATTLE_EXPLORER
LOCAL = PALAM_EXPLORER("探索者攻撃力", PLAYER)
;アルカナの罠で不器用になっている
SIF PALAM_EXPLORER("クラムジ")
	LOCAL -= 1 + RAND:3
;LVの差
SIF ABL:PLAYER:ＬＶ - ABL:ＬＶ
	LOCAL += 1 + (ABL:PLAYER:ＬＶ - ABL:ＬＶ) / 3
;脱衣済み
SIF PALAM_EXPLORER("脱衣")
	LOCAL += 1

;MASTER専用補正
IF PLAYER == MASTER
	;助手
	IF ASSI
		LOCAL += PALAM_EXPLORER("助手攻撃力", ASSI) + PALAM_EXPLORER("アルカナ補助", ASSI)
		SIF ABL:ASSI:ＬＶ > ABL:ＬＶ
			LOCAL += 1
	ENDIF
ELSE
	;上位夢魔
	IF TALENT:PLAYER:上位夢魔
		LOCAL += TALENT:PLAYER:上位夢魔
		SIF TALENT:上位夢魔 == 0
			LOCAL += 1
	ENDIF
ENDIF

;ハーピー三姉妹
IF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
	IF NO:PALAM_EXPLORER("二人目") == 39 && NO:PALAM_EXPLORER("三人目") == 40
		LOCAL = LOCAL/4 + 1
	ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2
		LOCAL = LOCAL/2 + 1
	ENDIF
ENDIF

SIF LOCAL <= 0
	BASE:精力 = MAX(RAND:2, 0)
BASE:精力 -= LOCAL

@COUNTER_SEXBATTLE_EXPLORER
;甘い言葉の時には待ってくれるかも
SIF PALAM_EXPLORER("甘い言葉") && RAND:2 == 0
	RETURN 0

IF PALAM_EXPLORER("Ｖインサート")
	TALENT:処女 = 0
	EXP:Ｖ経験 += 2
	EXP:腰使い経験 += 1
	IF PENIS(PLAYER)
		EXP:PLAYER:Ｖ挿入経験 += 1
		EXP:Ｖ性交経験 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("Ａインサート")
	TALENT:Ａ処女 = 0
	EXP:Ａ経験 += 2
	EXP:腰使い経験 += 1
	IF PENIS(PLAYER)
		EXP:PLAYER:Ａ挿入経験 += 1
		EXP:Ａ性交経験 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("エースオブスペード")
	EXP:腰使い経験 += 1
;舌使い
ELSEIF ABL:舌使い >= MAX(ABL:指使い, ABL:足使い, ABL:腰使い, ABL:魔乳, 1) && RAND:3
	IF RAND:2 == 0 || TALENT:PLAYER:オンナ
		CALL SET_PALAM_EXPLORER, "キス"
		EXP:キス経験 += 1
	ELSE
		IF PALAM_EXPLORER("ヴァッジ")
			CALL SET_PALAM_EXPLORER, "シックスナイン"
		ELSE
			CALL SET_PALAM_EXPLORER, "フェラチオ"
		ENDIF
		IF PENIS(PLAYER)
			EXP:フェラ経験 += 1
		ELSE
			EXP:クンニ経験 += 1
		ENDIF
	ENDIF
;指使い
ELSEIF ABL:指使い >= MAX(ABL:舌使い, ABL:足使い, ABL:腰使い, ABL:魔乳, 1) && RAND:3
	IF TALENT:サド && RAND:2 == 0
		CALL SET_PALAM_EXPLORER, "アナル責め"
		EXP:Ａ責め経験 += 1
	ELSE
		CALL SET_PALAM_EXPLORER, "手コキ"
		EXP:手淫経験 += 1
	ENDIF
;足使い
ELSEIF COND("脚が無い") == 0 && ABL:足使い >= MAX(ABL:舌使い, ABL:指使い, ABL:腰使い, ABL:魔乳, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "足コキ"
	EXP:足コキ経験 += 1
;腰使い
ELSEIF ABL:腰使い >= MAX(ABL:舌使い, ABL:指使い, ABL:足使い, ABL:魔乳, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "顔面騎乗"
	EXP:腰使い経験 += 1
;魔乳
ELSEIF ABL:魔乳 >= MAX(ABL:舌使い, ABL:指使い, ABL:足使い, ABL:腰使い, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "パイズリ"
	IF HAS_PENIS(PLAYER)
		EXP:パイズリ経験 += 1
	ELSE
		EXP:手淫経験 ++
	ENDIF
ELSEIF RAND:7 == 0
	CALL SET_PALAM_EXPLORER, "足コキ"
ELSEIF RAND:6 == 0
	CALL SET_PALAM_EXPLORER, "フェラチオ"
ELSEIF RAND:5 == 0
	CALL SET_PALAM_EXPLORER, "手コキ"
ELSEIF RAND:4 == 0
	CALL SET_PALAM_EXPLORER, "顔面騎乗"
ELSEIF RAND:3 == 0
	CALL SET_PALAM_EXPLORER, "アナル責め"
ELSEIF RAND:2
	CALL SET_PALAM_EXPLORER, "パイズリ"
ELSE
	CALL SET_PALAM_EXPLORER, "キス"
ENDIF

LOCAL = PALAM_EXPLORER("攻撃力")

SIF RAND:(PALAM_EXPLORER("敵の強さ") + 1)
	LOCAL += 1 + RAND:(PALAM_EXPLORER("敵の強さ") + 1)/3

SELECTCASE PALAM_EXPLORER("敵キャラ数")
CASE IS >= 3
	LOCAL += 3
CASE 2
	LOCAL += 1
ENDSELECT

LOCAL = DIVIDE(LOCAL, 100 + TALENT:PLAYER:精神力強化)

SIF PALAM_EXPLORER("センシブ")
	LOCAL += 2
;行動
IF PALAM_EXPLORER("キス") || PALAM_EXPLORER("フェラチオ")
	LOCAL += RAND:(ABL:舌使い + 1) + MIN(COND("口に弱い", PLAYER) + COND("唇に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("シックスナイン")
	LOCAL += RAND:(ABL:舌使い + 1) + MIN(COND("口に弱い", PLAYER) + COND("唇に弱い", PLAYER), 1)
	LOCAL += RAND:(ABL:腰使い + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("アナル責め") || PALAM_EXPLORER("手コキ")
	LOCAL += RAND:(ABL:指使い + 1) + MIN(COND("手に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("足コキ")
	LOCAL += RAND:(ABL:足使い + 1) + MIN(COND("脚に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("顔面騎乗") || PALAM_EXPLORER("Ｖインサート") || PALAM_EXPLORER("エースオブスペード")
	LOCAL += RAND:(ABL:腰使い + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("Ａインサート")
	LOCAL += RAND:(ABL:腰使い + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("パイズリ")
	LOCAL += RAND:(ABL:魔乳 + 1) + MIN(COND("胸に弱い", PLAYER), 1)
ENDIF
;ハーピー三姉妹
IF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
	IF NO:PALAM_EXPLORER("二人目") == 39 && NO:PALAM_EXPLORER("三人目") == 40
		LOCAL += 10
	ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2
		LOCAL += 5
	ENDIF
ENDIF

;助手がいる
IF PLAYER == MASTER && ASSI
	SIF PALAM_EXPLORER("アルカナ補助", ASSI) >= 2
		LOCAL -= 1
	SIF ABL:ASSI:技巧 >= ABL:技巧
		LOCAL -= 1
	SIF ABL:ASSI:ＬＶ > ABL:ＬＶ
		LOCAL -= 1
ENDIF
IF LOCAL < 1
	IF RAND:(ABL:技巧 + 2)
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
ENDIF

BASE:PLAYER:精力 -= LOCAL

@ACQUISITION_BOSS
LOCALS =
				
FOR LOCAL, 1, 100
	IF nLocal:LOCAL != nBossAcquired:LOCAL
		LOCALS += @"%CSVNAME(LOCAL)%, "
	ENDIF
NEXT

SIF nLocal:303 != nBossAcquired:303
	LOCALS += @"Dryad's special version, Skogsfrun  "
SIF nLocal:403 != nBossAcquired:403
	LOCALS += @"Pixie's special version, Titania  "
SIF nLocal:124 != nBossAcquired:124
	LOCALS += @"Lizard Girl's special version, Wyvern  "
SIF nLocal:133 != nBossAcquired:133
	LOCALS += @"Mermaid's special version, Leviathan  "
SIF nLocal:152 != nBossAcquired:152
	LOCALS += @"Scylla's special version, Charybdis  "
SIF nLocal:604 != nBossAcquired:604
	LOCALS += @"Nekomata's special version, Sphinx  "
SIF nLocal:163 != nBossAcquired:163
	LOCALS += @"Kuzunoha's special version, Tamamo  "
SIF nLocal:222 != nBossAcquired:222
	LOCALS += @"Kunoichi's special version, Hatsume  "
	
SIF LOCALS == ""
	RETURN
CALL PRINT_COLORTEXT, @"%SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)% can now be summoned!", DEF_COLOR("イエロー")
PRINTL

@LIFE_LIST(ARG)
	;ARG = 0 ステータス表示
	;ARG = 1 調教対象選択
	;ARG = 2 助手選択
	;ARG = 3 キャラ売却
	#DIM LOOP_CHR
	#DIM LOOP_I
	#DIM LOOP_J
	#DIM 精子量
	DRAWLINE
	IF !ARG
		PRINTFORM 　[0]%NAME:MASTER%
		TRYCALL 簡易追加情報, MASTER
		PRINTL
		DRAWLINE
	ENDIF
	FOR LOOP_CHR,1, CHARANUM
		IF ARG == 2
			SIF !(CFLAG:LOOP_CHR:既成事実 & 2)
				CONTINUE
			IF COUNT == ASSI
				PRINT ☆
			ELSE
				PRINT 　
			ENDIF
		ENDIF
		IF ARG == 3
			SIF !(CFLAG:LOOP_CHR:既成事実 & 1)
				CONTINUE
		ENDIF
		PRINTFORM [{LOOP_CHR,2}]
		PRINTFORM %CALLNAME:LOOP_CHR,16,LEFT%

		SELECTCASE FLAG:1003
			;基礎表示選択
			CASE 2
				PRINTFORM \@(FLAG:1004) ? ENE#STA\@ 
				LOCAL = MAX(BASE:LOOP_CHR:(FLAG:1004), 0)
				;BAR LOCAL, MAXBASE:LOOP_CHR:(FLAG:1004),10
				IF FLAG:1004
					CALL PRINT_COLORBAR, BASE:LOOP_CHR:(FLAG:1004), MAXBASE:LOOP_CHR:(FLAG:1004), 10, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("青"), RESULT:1
				ELSE
					CALL PRINT_COLORBAR, BASE:LOOP_CHR:(FLAG:1004), MAXBASE:LOOP_CHR:(FLAG:1004), 10, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
				ENDIF
				PRINTFORM ({LOCAL,5}/{MAXBASE:LOOP_CHR:(FLAG:1004),5})
		
				SIF ARG == 0
					TRYCALL 簡易追加情報, LOOP_CHR
		
			;能力表示選択or性技表示選択
			CASE 3, 4
				IF FLAG:1003 == 4 && !(HAS_VAGINA(LOOP_CHR)) && (FLAG:1004 == 52 || FLAG:1004 == 54)
					PRINTFORM %ABLNAME:(FLAG:1004),16%: * 
				ELSE
					PRINTFORM %ABLNAME:(FLAG:1004),16%: {ABL:LOOP_CHR:(FLAG:1004),5,LEFT} 
				ENDIF
			;刻印表示選択
			CASE 5
				PRINTFORM %GET_MARK_LEVEL_OWNERNAME(LOOP_CHR,FLAG:1004),18% 
			;経験表示選択
			CASE 6
				IF (!(HAS_VAGINA(LOOP_CHR)) && FLAG:1004 == 51) || (HAS_VAGINA(LOOP_CHR) && FLAG:1004 == 52)
					PRINTFORM %EXPNAME:(FLAG:1004),30%: * 
				ELSE
				PRINTFORM %EXPNAME:(FLAG:1004),30%: {EXP:LOOP_CHR:(FLAG:1004),5,LEFT} 
				ENDIF
			;宝珠表示選択
			CASE 7
				LOCALS = {JUEL:LOOP_CHR:(FLAG:1004)}
				SIF JUEL:LOOP_CHR:(FLAG:1004) > 1000000
					LOCALS = {JUEL:LOOP_CHR:(FLAG:1004) / 1000}k
				SIF JUEL:LOOP_CHR:(FLAG:1004) > 1000000000
					LOCALS = {JUEL:LOOP_CHR:(FLAG:1004) / 1000000}m
				PRINTFORM %PALAMNAME:(FLAG:1004),6% Gems: %LOCALS,7,LEFT%
			;その他表示選択
			CASE 8
				SELECTCASE FLAG:1004
					;既成事実
					CASE 1
						VARSET LOCALS
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),0) 
							LOCALS += "Successfully confessed   "
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),1) 
							LOCALS += "Made out with each other "
						PRINTFORM %LOCALS%
					;好感度
					CASE 2
						VARSET LOCALS
						PRINTFORM Favorability: {CFLAG:LOOP_CHR:(FLAG:1004),5,LEFT}  Succumbing: {CFLAG:LOOP_CHR:屈服度,5,LEFT} 
						;恋慕/NTR状態
						IF TALENT:LOOP_CHR:NTR
							SETCOLOR 101,153,255
							PRINT NTR           
						ENDIF
						IF TALENT:LOOP_CHR:恋慕
							SETCOLOR 196,25,25
							PRINT Love          
						ENDIF
						IF TALENT:LOOP_CHR:人妻
							SETCOLOR 255,100,100
							PRINT Married       
						ENDIF
						IF TALENT:LOOP_CHR:公衆便所
							SETCOLOR 152,67,242
							PRINT Slut 
						ENDIF
						RESETCOLOR
					;浮気公認
					CASE 3
						PRINTFORM Seen Cheating: {TALENT:LOOP_CHR:浮気公認}
					;子宮内精液量
					CASE 4
						精子量 = 0
						FOR LOOP_I, 0, 301
							REPEAT 5
								IF TA:LOOP_CHR:LOOP_I:COUNT > 0
									精子量 += TA:LOOP_CHR:LOOP_I:COUNT
								ENDIF
							REND
						NEXT
						IF TALENT:LOOP_CHR:妊娠
							LOCALS:1 = PREG
						ELSEIF CFLAG:LOOP_CHR:卵子生存日数 == 1
							LOCALS:1 = OVUL
						ELSEIF TALENT:LOOP_CHR:危険日
							LOCALS:1 = DD　 
						ELSE
							LOCALS:1 =     
						ENDIF
						PRINTFORM Semen in womb: {精子量,5}/{CFLAG:LOOP_CHR:子宮内体積,5}ml
						PRINTFORM  %LOCALS:1%
					;弱味
					CASE 10
						VARSET LOCALS
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),0) 
							LOCALS += "Panties have been confiscated "
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),1) 
							LOCALS += "Was seen in a love affair     "
						PRINTFORM %LOCALS%
				ENDSELECT
		ENDSELECT

		IF ARG == 2
			IF LOOP_CHR == ASSI
				PRINT                    
			ELSEIF ISASSI:LOOP_CHR == 1
				PRINT [Former  Assistant] 
			ELSE
				PRINT [  Inexperienced  ] 
			ENDIF
		ENDIF
		PRINTL
	NEXT
	PRINTL 

;ステータス表示
@SHOW_CHARADATA
	CALL LIFE_LIST
	SIF FLAG:1003 == 2
		HTML_PRINT @"　　　　Legend (Hover to show): <nonbutton title='Assistant'>☆</nonbutton> <nonbutton title='Indicates a Hermaphrodite. %TALENT_INFO(2, 3)%'>HE</nonbutton> <nonbutton title='%TALENT_INFO(2, 2)%'>♂</nonbutton> <nonbutton title='Indicates a Female.'>♀</nonbutton> <nonbutton title='Indicates pregnancy. %TALENT_INFO(153)%'>PREG</nonbutton> <nonbutton title='Indicates ovulation.'>OVUL</nonbutton> <nonbutton title='Indicates that this character is on pill.'>PILL</nonbutton> <nonbutton title='Indicates Dangerous Day.'>DD</nonbutton> <nonbutton title='Female Virgin. %TALENT_INFO(0, 1)%\nMight be also a restored virgin.'>V</nonbutton> <nonbutton title='Male Virgin. %TALENT_INFO(1)%'>MV</nonbutton> <nonbutton title='Female and Male virgin.'>VM</nonbutton> <nonbutton title='%TALENT_INFO(6)%'>Ｎ</nonbutton> <nonbutton title='Indicates a Slut. %TALENT_INFO(7)%'>SL</nonbutton> <nonbutton title='%TALENT_INFO(160)%'>CL</nonbutton> <nonbutton title='%TALENT_INFO(161)%'>CV</nonbutton> <nonbutton title='%TALENT_INFO(162)%'>CA</nonbutton>"
	PRINTLC  [2000]Base Stats
	PRINTLC  [3000]Abilities
	PRINTLC  [4000]Sex Techniques
	PRINTLC  [5000]Marks
	PRINTLC  [6000]Experiences
	PRINTLC  [7000]Gems
	PRINTLC  [8000]Other
	PRINTFORML  [1000]Return

	$INPUT_LOOP
	INPUT
	IF RESULT == 1000
		RETURN 0
	ELSEIF RESULT == 2000 || RESULT == 3000 || RESULT == 4000 || RESULT == 5000 || RESULT == 6000 || RESULT == 7000 || RESULT == 8000
		CALL SHOW_CHARADATA2, RESULT
		RESTART
	ELSEIF RESULT < 0 || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ENDIF
	CALL PRINT_STATE(RESULT, 0, 1)
	RESTART

@SHOW_CHARADATA2, ARG
	VARSET LOCAL, 0
	LOCAL:999 = ARG/1000
	DRAWLINE
	DRAWLINE

	SELECTCASE LOCAL:999
		;基礎表示選択
		CASE 2
			PRINTLC  [   0] STA
			PRINTLC  [   1] ENE
			LOCAL:0 = 1
			LOCAL:1 = 1
			LOCAL:998 = 1
		;能力表示選択(性技は別）
		CASE 3
			FOR LOCAL:998,0,40
				SIF !STRLENS(ABLNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),16, LEFT%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;性技表示選択
		CASE 4
			FOR LOCAL:998,50,60
				SIF !STRLENS(ABLNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),16, LEFT%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;刻印表示選択
		CASE 5
			FOR LOCAL:998,0,5
				SIF !STRLENS(MARKNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %MARKNAME:(LOCAL:998),18, LEFT%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;経験表示選択
		CASE 6
			FOR LOCAL:998,0,100
				SIF !STRLENS(EXPNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %EXPNAME:(LOCAL:998),30, LEFT%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;宝珠表示選択
		CASE 7
			FOR LOCAL:998,0,22
				SIF !STRLENS(PALAMNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %PALAMNAME:(LOCAL:998),6, LEFT% Gems
				LOCAL:(LOCAL:998) = 1
			NEXT
			PRINTLC  [ 100] Denial Gems
			LOCAL:100 = 1
			LOCAL:998 = 100
		;その他表示選択
		CASE 8
			PRINTLC  [   1]Accomplishments
			PRINTLC  [   2]Favorability
			PRINTLC  [   3]Cheating
			PRINTLC  [   4]Semen in womb
			PRINTLC  [  10]Weakness
			LOCAL:1 = 1
			LOCAL:2 = 1
			LOCAL:3 = 1
			LOCAL:4 = 1
			LOCAL:10 = 1
			LOCAL:998 = 10
	ENDSELECT
	PRINTL  [1000] Return
	$INPUT_LOOP
	INPUT
	IF RESULT == 1000
		RETURN 0
	ELSEIF RESULT < 0 || RESULT > LOCAL:998 || LOCAL:RESULT <= 0
		GOTO INPUT_LOOP
	ENDIF
	FLAG:1003 = LOCAL:999
	FLAG:1004 = RESULT


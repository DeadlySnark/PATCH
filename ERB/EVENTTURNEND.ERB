;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@EVENTTURNEND
	#DIM LOOP_CHR

	FLAG:侵入者の現在位置 = 0
	FOR LOOP_CHR,1,CHARANUM
		;-------------------------------------------------------------------------------
		CALL 従者E_確率変動(LOOP_CHR)
		;-------------------------------------------------------------------------------
		CALL 体変化_１日経過(LOOP_CHR)
		;-------------------------------------------------------------------------------
		;管理人獲得判定
		CALL CHK_CARETAKER_GET(LOOP_CHR)
		;---------------------------------------------------------------------------
		;恋慕獲得判定
		CALL CHK_FALL_IN_LOVE(LOOP_CHR)
		;---------------------------------------------------------------------------
		;親愛獲得判定
		CALL CHK_LOVING_KINDNESS_GET(LOOP_CHR)
		;-------------------------------------------------------------------------------
		;性的魅力獲得判定
		CALL CHK_SEX_APPEAL_GET(LOOP_CHR)
		;---------------------------------------------------------------------------
		;肉体改造
		CALL CHK_BODY_STATE_CHANGE(LOOP_CHR)
		;---------------------------------------------------------------------------
		;寝取られ判定
		CALL CHK_NTR_CHANGE(LOOP_CHR)
		;---------------------------------------------------------------------------
		;公衆便所判定
		CALL CHK_PUBLIC_TOILET_CHANGE(LOOP_CHR)
		;---------------------------------------------------------------------------
		;淫乱・娼婦取得判定
		CALL CHK_LEWDNESS_GET(LOOP_CHR)
		;---------------------------------------------------------------------------
		;浮気素質変化判定
		CALL CHK_UNFAITHFUL_TALENT_CHANGE(LOOP_CHR)
		;---------------------------------------------------------------------------
		;不機嫌であれば反発
		CALL CHK_ILL_HUMORE_REBOUND(LOOP_CHR)
		;---------------------------------------------------------------------------
		;傷心消滅判定
		CALL CHK_HEART_BREAK_CLEAR(LOOP_CHR)
	NEXT
	FOR LOOP_CHR,1,CHARANUM
		;満足度の変動
		CALL CHK_SATISFACION_CHANGE(LOOP_CHR)
		;気力の回復
		CALL CHK_WILL_POWER_RECOVERY(LOOP_CHR)
		;体力の回復
		CALL CHK_BODY_POWER_RECOVERY(LOOP_CHR)
		;拡張経験による締まり具合変動
		CALL CHK_TIGHTNESS_CHANGE(LOOP_CHR)
		;精力増減
		CALL CHK_VITARITY_CHANGE(LOOP_CHR)
		CFLAG:LOOP_CHR:自慰中 = 0
		CFLAG:LOOP_CHR:桃霧性欲フラグ = 0
	NEXT
	FLAG:休憩フラグ = 0
	CALL RANK_POINT
	BEGIN SHOP

;-------------------------------------------------------------------------------
;管理人獲得判定
@CHK_CARETAKER_GET(奴隷)
	#DIM 奴隷
	;;あなたが[肉便器]の時の、管理人獲得条件
	;;・好感度は通常の恋慕並み（＝1000以上が必要）
	;;　淫乱や娼婦は影響せず
	;;・好感度順位が、１位
	;;・まだ管理人がいない
	;;・告白を受けている（CFLAG:既成事実,0）
	;;・チルノ、大妖精、魔理沙は管理人になれない
	;;・そもそもあなたが肉便器じゃないとだめ
	;;--------------------------------------------
	IF TALENT:MASTER:肉便器 && GETBIT(CFLAG:奴隷:既成事実,0) && WC_LOVEranking(奴隷) == 1 && CFLAG:奴隷:好感度 >= 1000 && CFLAG:PLAYER:WC_管理人は誰 == 0 && 奴隷 < 人物_チルノ
		;;管理人宣言
		TRYCALLFORM KOJO_WC_COUNTER_caretaker_K{奴隷}

		PRINTFORML %CALLNAME:奴隷%は、今後は自分が%CALLNAME:MASTER%のことを管理すると宣言した。
		IF 奴隷 == 人物_レミリア
			PRINTFORML 紅魔館の主である彼女にたいして、ことさら反対する者もいなかったらしい。
		ELSE
			PRINTFORML %CALLNAME:人物_レミリア%も、彼女が管理人となることに異論はないらしい。
		ENDIF
		PRINTL
		PRINTFORMW %CALLNAME:奴隷%は[管理人]を得ました
		CFLAG:PLAYER:WC_管理人は誰 = 奴隷
		TALENT:奴隷:管理人 = 1
		IF CFLAG:奴隷:幽閉
			PRINTL
			PRINTFORML %CALLNAME:奴隷%の精神は安定してきたようだ
			PRINTFORMW %CALLNAME:奴隷%の幽閉が解かれました
			CFLAG:奴隷:幽閉 = 0
		ENDIF
	ENDIF

RETURN 0

;-------------------------------------------------------------------------------
;恋慕獲得判定
@CHK_FALL_IN_LOVE(奴隷)
	#DIM 奴隷
	#DIM 閾値

	;恋慕獲得閾値
	;通常時は好感度1000以上が必要
	;[淫乱]であれば好感度は1500以上が必要
	;[娼婦]であれば好感度は2000以上が必要
	;;あなたが[肉便器]なら、さらに+2000
	;[浮気癖]があれば1/3で済む
	;NTR陥落経験１回につき5%上昇(寝取り返しは徐々に難しくなる)
;;		閾値 = (1000+(500*TALENT:奴隷:淫乱))*(100+EXP:奴隷:NTR陥落経験*5)/100
	閾値 = (1000+(500*TALENT:奴隷:淫乱)+(2000*TALENT:MASTER:肉便器))*(100+EXP:奴隷:NTR陥落経験*5)/100
	IF TALENT:奴隷:浮気癖
		閾値 /= 3
	ENDIF
	;---------------------------------------------------------------------------
	;恋慕獲得条件
	;好感度が閾値を超えている
	;経験:奉仕快楽経験が30以上
	;能力:従順が3以上
	IF CFLAG:奴隷:好感度 > 閾値 && EXP:奴隷:奉仕快楽経験 >= 30 && ABL:奴隷:従順 >= 3 && !TALENT:奴隷:恋慕
		PRINTFORML %CALLNAME:奴隷%は%CALLNAME:MASTER%と過ごす時間に悦びを感じているようだ…
		PRINTFORML %CALLNAME:奴隷%は[恋慕]を得た
		PRINTFORMW %CALLNAME:奴隷%の部屋の合鍵を貰った
		FLAG:陥落人数 ++
		TALENT:奴隷:恋慕 = 1
		CFLAG:奴隷:屈服度 = 0
		CFLAG:奴隷:NTR陥落イベントフラグ = 1
		IF TALENT:奴隷:NTR
			PRINTL
			IF EXP:奴隷:NTR陥落経験 < 10
				PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%への依存を断ち切った
				PRINTFORML %CALLNAME:奴隷%は涙を浮かべて微笑んだ
				PRINTFORMW %CALLNAME:奴隷%は[NTR]ではなくなった
			ELSE
				PRINTFORML %"もう何度目かも分からないが、"+CALLNAME:奴隷+"は再び"+CALLNAME:MASTER+"の元へ戻ってきた"%
				PRINTFORML %"どうやら"+CALLNAME:奴隷+"は優しくされれば、誰にでも気を許してしまうようだ……"%
				PRINTFORMW %CALLNAME:奴隷+"は[NTR]ではなくなったが、もうその[浮気癖]が治ることはないのだろう"%
				TALENT:奴隷:浮気癖 = 1
			ENDIF
			;浮気公認は消えます
			IF TALENT:奴隷:浮気公認 > 0
				PRINTFORML %CALLNAME:奴隷%は「もう二度と浮気して欲しいなんて言わないで」と泣きながら抱きついた。
				PRINTFORMW %CALLNAME:奴隷%との[浮気公認]を失った。
				TALENT:奴隷:浮気公認 = 0
			ENDIF
			TALENT:奴隷:NTR = 0
		ENDIF
		;主人の屈服度減算ボーナス
		CFLAG:MASTER:屈服度 = MAX(CFLAG:MASTER:屈服度 - 20 - RAND:20, 0)
		;恋慕獲得で公衆便所も消失。貞操帯の鍵を持っていたらそれもクリア。
		IF TALENT:奴隷:公衆便所
			PRINTL
			PRINTFORML %CALLNAME:奴隷%は肉便器から開放された
			IF TALENT:奴隷:淫乱 == 2
				PRINTFORML しかし今まで数多くの男達に買われその身体を抱かれてきた過去が消えるはずもなく
				PRINTFORMW %CALLNAME:奴隷%は娼婦の汚名をぬぐうことはできなかった
			ENDIF
			TALENT:奴隷:公衆便所 = 0
			IF 奴隷 == FLAG:貞操帯鍵購入フラグ
				FLAG:貞操帯鍵購入フラグ = 0
				FLAG:貞操帯鍵有効カウンタ = 0
			ENDIF
		ENDIF
		IF CFLAG:奴隷:幽閉
			PRINTL
			PRINTFORML %CALLNAME:奴隷%の精神は安定してきたようだ
			PRINTFORMW %CALLNAME:奴隷%の幽閉が解かれました
			CFLAG:奴隷:幽閉 = 0
		ENDIF
	ENDIF

RETURN 0

;-------------------------------------------------------------------------------
;親愛獲得判定
;好感度が閾値を越えている
;恋慕、愛情経験200以上
;浮気公認、浮気癖を持っていない
;寝取られなくなる
@CHK_LOVING_KINDNESS_GET(奴隷)
	#DIM 奴隷
	IF CFLAG:奴隷:好感度 > 10000 && EXP:奴隷:愛情経験 >= 200 && TALENT:奴隷:恋慕 && !TALENT:奴隷:親愛 && !TALENT:奴隷:浮気公認 && !TALENT:奴隷:浮気癖
		PRINTFORML %CALLNAME:奴隷%は%CALLNAME:MASTER%に心からの愛情を抱いているようだ…
		PRINTFORML %CALLNAME:奴隷%は［親愛］を得た
		TALENT:奴隷:親愛 = 1
		CFLAG:奴隷:屈服度 = 0
	ENDIF

RETURN 0

;-------------------------------------------------------------------------------
;性的魅力獲得判定
@CHK_SEX_APPEAL_GET(奴隷)
	#DIM 奴隷

	IF EXP:奴隷:ポルノ経験 >= 15 && TALENT:奴隷:性的魅力 == 0
		PRINTFORML %CALLNAME:奴隷%の下着や写真などが若い男たちの間で人気なようだ…
		PRINTFORML %CALLNAME:奴隷%は［性的魅力］を得た
		TALENT:奴隷:性的魅力 = 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;寝取られ判定
@CHK_NTR_CHANGE(奴隷)
	#DIM 奴隷
	#DIM GET_NTR	; 寝取られフラグ

	GET_NTR = 0
	;浮気癖があると寝取られやすくなる
	;浮気を公認している場合は寝取られにくくなる
	IF TALENT:奴隷:親愛
		;親愛があれば寝取られない
		GET_NTR = 0
	ELSEIF TALENT:奴隷:NTR
		;寝取られている場合はそれ以上寝取られない（今のところは）
		GET_NTR = 0
	ELSEIF TALENT:奴隷:浮気公認 > 0
		;浮気を公認している場合、屈服度がよほど高くなければ寝取られない
		;ここの式はNTR_CHK_FAVORABLY()と同一であること
		GET_NTR = CFLAG:奴隷:屈服度 > (CFLAG:奴隷:好感度 * TALENT:奴隷:浮気公認 * 10 )
	ELSEIF TALENT:奴隷:浮気癖
		;浮気癖があると逆条件（屈服度が2000/1500/1000以上かどうか）であっさり寝取られる（好感度を確認しない）
		;ここの式はNTR_CHK_FAVORABLY()と同一であること
		GET_NTR = CFLAG:奴隷:屈服度 > (2000-(500*TALENT:奴隷:淫乱))
	ELSE
		;通常は、好感度が1000未満で屈服度が2000以上、さらに屈服度が好感度より高くなければダメ
		GET_NTR = CFLAG:奴隷:好感度 < 1000 && CFLAG:奴隷:屈服度 > CFLAG:奴隷:好感度 && CFLAG:奴隷:屈服度 > 2000
	ENDIF
	
	IF GET_NTR
		;恋慕＋告白済みの状態でNTRを取得
		SIF CFLAG:奴隷:NTR陥落イベントフラグ == 1 && GETBIT(CFLAG:奴隷:既成事実 , 0)
			TRYCALL NTR陥落イベント, 奴隷
		;初回
		IF EXP:奴隷:NTR陥落経験 == 0
			;対象が訪問者のペニスを前の穴or後ろの穴から体内に受け入れた事が無い
			IF EXP:奴隷:浮気Ｖ性交経験 == 0 || EXP:奴隷:浮気Ａ性交経験 == 0
				PRINTFORMW %CALLNAME:奴隷%は%NTR_NAME(0)%が与える快楽に逆らえないようだ・・・
			ELSE
				PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%のペニスに逆らえないようだ・・・
			ENDIF
			IF TALENT:奴隷:恋慕
				PRINTFORMW %CALLNAME:奴隷%は[恋慕]を失った
			ENDIF
			; 2回目以降
		ELSE
			;対象が訪問者のペニスを前の穴or後ろの穴から体内に受け入れた事が無い
			IF EXP:奴隷:浮気Ｖ性交経験 == 0 || EXP:奴隷:浮気Ａ性交経験 == 0
				PRINTFORMW %CALLNAME:奴隷%は%NTR_NAME(0)%が与える快楽を忘れられなかったようだ・・・
			ELSE
				PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%のペニスの快楽を忘れられなかったようだ・・・
			ENDIF
			IF TALENT:奴隷:恋慕
				PRINTFORMW %CALLNAME:奴隷%は再び[恋慕]を失った
			ENDIF
		ENDIF
		IF TALENT:奴隷:人妻
			PRINTFORMW %CALLNAME:奴隷%は[人妻]を失った
		ENDIF
		TRYCALLLIST
			FUNC NTR_KOJO_K{奴隷}_12(奴隷)
			FUNC NTR_KOJO_K_12(奴隷)
		ENDFUNC
		PRINTFORML %CALLNAME:奴隷%は虚ろな眼で微笑みながら%NTR_NAME(0)%の雌奴隷になる事を伝えた
		PRINTFORMW %CALLNAME:奴隷%は寝取られてしまった
		PRINTFORMW %CALLNAME:奴隷%は[NTR]を得た
		PRINTFORML %NTR_NAME(0)%は隷属の証として%CALLNAME:奴隷%に細い黒革のチョーカーを与え、毎日それを着用することを義務付けた。
		TALENT:奴隷:恋慕 = 0
		TALENT:奴隷:人妻 = 0
		TALENT:奴隷:NTR = 1
		EXP:奴隷:NTR陥落経験 ++
		CFLAG:奴隷:好感度 = 0
		CFLAG:奴隷:既成事実 = 0
		CFLAG:奴隷:馴れ合い強度度 = 0
		IF ABL:奴隷:従順 > 20
			ABL:奴隷:従順 = 10
		ELSEIF ABL:奴隷:従順 > 10
			ABL:奴隷:従順 = 5
		ELSEIF ABL:奴隷:従順 > 5
			ABL:奴隷:従順 = 3
		ENDIF
		IF ABL:奴隷:親密 > 20
			ABL:奴隷:親密 = 10
		ELSEIF ABL:奴隷:親密 > 10
			ABL:奴隷:親密 = 5
		ELSEIF ABL:奴隷:親密 > 5
			ABL:奴隷:親密 = 3
		ENDIF
		;浮気公認時ペナルティ
		IF TALENT:奴隷:浮気公認 > 0
			ABL:奴隷:従順 /= 2
			ABL:奴隷:親密 /= 2
		ENDIF
	ENDIF

RETURN 0

;-------------------------------------------------------------------------------
;公衆便所判定
@CHK_PUBLIC_TOILET_CHANGE(奴隷)
	#DIM 奴隷

	IF CFLAG:奴隷:屈服度 > 5000 && EXP:奴隷:浮気人数 > 5 && !TALENT:奴隷:公衆便所
		; 初回
		IF EXP:奴隷:公衆便所陥落経験 == 0
			PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%から蹂躙される快楽をその身に刻まれた
			PRINTFORM %CALLNAME:奴隷%は
			IF TALENT:奴隷:恋慕
				PRINTFORM %MSG_LOVER(奴隷)%のいる身でありながら
			ENDIF
			PRINTFORML %NTR_NAME(0)%から肉便器として客を取らされることになった
			; 2回目以降
		ELSE
			PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%から蹂躙される快楽を忘れることができなかった
			PRINTFORM %CALLNAME:奴隷%は
			IF TALENT:奴隷:恋慕
				PRINTFORM %MSG_LOVER(奴隷)%のいる身でありながら
			ENDIF
			PRINTFORML 再び%NTR_NAME(0)%から肉便器として客を取らされることになった
		ENDIF
		TRYCALLLIST
			FUNC NTR_KOJO_K{奴隷}_13(奴隷)
			FUNC NTR_KOJO_K_13(奴隷)
		ENDFUNC
		IF TALENT:奴隷:恋慕
			PRINTFORML %CALLNAME:奴隷%は%MSG_LOVER(奴隷)%である%CALLNAME:MASTER%に肉便器になる事を宣言した
		ELSE
			PRINTFORML %CALLNAME:奴隷%は暗く濁った眼で肉便器になる事を宣言した
		ENDIF
		PRINTFORMW %CALLNAME:奴隷%は[公衆便所]を得た
		PRINTFORML %NTR_NAME(0)%は隷属の証として%CALLNAME:奴隷%に首輪を与え、毎日それを着用することを義務付けた。
		PRINTFORML 首輪のバックルには彼女の名前と、娼婦の証である淫らな紋様が彫られている。
		IF TALENT:奴隷:淫乱 == 2
			PRINTFORML %CALLNAME:奴隷%が娼婦として復帰するという噂は瞬く間にかつての客たちの間に広まった
			PRINTFORMW すでに多数の客が%CALLNAME:奴隷%の身体を買おうと予約をしているようだ
		ENDIF
		PRINTFORML %NTR_NAME(0)%は%CALLNAME:奴隷%を管理するため、彼女に貞操帯の着用を義務付けた。
		EQUIP:奴隷:下半身下着１ = 100
		EQUIP:奴隷:下半身下着２ = 0
		TALENT:奴隷:公衆便所 = 1
		EXP:奴隷:公衆便所陥落経験 ++
		IF !TALENT:奴隷:恋慕
			CFLAG:奴隷:好感度 = 0
			CFLAG:奴隷:既成事実 = 0
			CFLAG:奴隷:馴れ合い強度度 = 0
		ENDIF
		IF ABL:奴隷:従順 > 20
			ABL:奴隷:従順 = 10
		ELSEIF ABL:奴隷:従順 > 10
			ABL:奴隷:従順 = 5
		ELSEIF ABL:奴隷:従順 > 5
			ABL:奴隷:従順 = 3
		ENDIF
		IF ABL:奴隷:親密 > 20
			ABL:奴隷:親密 = 10
		ELSEIF ABL:奴隷:親密 > 10
			ABL:奴隷:親密 = 5
		ELSEIF ABL:奴隷:親密 > 5
			ABL:奴隷:親密 = 3
		ENDIF
		;浮気公認時ペナルティ
		IF TALENT:奴隷:浮気公認 > 0
			ABL:奴隷:従順 /= 2
			ABL:奴隷:親密 /= 2
		ENDIF
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;淫乱・娼婦取得判定
@CHK_LEWDNESS_GET(奴隷)
	#DIM 奴隷
	#DIM 閾値

	閾値 = EXP:奴隷:浮気絶頂経験 + EXP:奴隷:浮気Ｖ性交経験 + EXP:奴隷:浮気Ａ性交経験
	IF TALENT:奴隷:NTR && 閾値 >= 400 && TALENT:奴隷:淫乱 == 0
		PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%に与えられる快楽に溺れているようだ…
		PRINTFORMW %CALLNAME:奴隷%は[淫乱]を得た
		TALENT:奴隷:淫乱 = 1
		MAXBASE:奴隷:満足 = MAX(MAXBASE:奴隷:満足, 5000)
	ENDIF
	IF TALENT:奴隷:公衆便所 && EXP:奴隷:売春経験 >= 200 && TALENT:奴隷:淫乱 == 1
		PRINTFORML %CALLNAME:奴隷%は名も知らぬ男たちとの交わりの快楽に溺れているようだ…
		PRINTFORMW %CALLNAME:奴隷%は[娼婦]を得た
		TALENT:奴隷:淫乱 = 2
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;浮気素質変化
@CHK_UNFAITHFUL_TALENT_CHANGE(奴隷)
	#DIM 奴隷

	;浮気な唇取得判定
	IF TALENT:奴隷:NTR && EXP:奴隷:浮気キス経験 >= 200 && TALENT:奴隷:浮気な唇 == 0
		PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%の唇の虜になっているようだ…
		PRINTFORMW %CALLNAME:奴隷%は[浮気な唇]を得た
		TALENT:奴隷:浮気な唇 = 1
	ENDIF
	;---------------------------------------------------------------------------
	;浮気な蜜壷取得判定
	IF TALENT:奴隷:NTR && EXP:奴隷:浮気Ｖ性交経験 >= 200 && TALENT:奴隷:浮気な蜜壷 == 0
		PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%のペニスの虜になっているようだ…
		PRINTFORMW %CALLNAME:奴隷%は[浮気な蜜壷]を得た
		TALENT:奴隷:浮気な蜜壷 = 1
	ENDIF
	;---------------------------------------------------------------------------
	;浮気な尻穴取得判定
	IF TALENT:奴隷:NTR && EXP:奴隷:浮気Ａ性交経験 >= 200 && TALENT:奴隷:浮気な尻穴 == 0
		PRINTFORML %CALLNAME:奴隷%は%NTR_NAME(0)%にアヌスを抉られる快楽の虜になっているようだ…
		PRINTFORMW %CALLNAME:奴隷%は[浮気な尻穴]を得た
		TALENT:奴隷:浮気な尻穴 = 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;不機嫌であれば反発
@CHK_ILL_HUMORE_REBOUND(奴隷)
	#DIM 奴隷

	IF CFLAG:奴隷:不機嫌
		CFLAG:奴隷:不機嫌 --
		IF !CFLAG:奴隷:不機嫌
			SIF MARK:奴隷:反発刻印
				CFLAG:奴隷:不機嫌 = -- MARK:奴隷:反発刻印
		ENDIF
	ENDIF
RETURN 0

;---------------------------------------------------------------------------
;傷心消滅判定
@CHK_HEART_BREAK_CLEAR(奴隷)
	#DIM 奴隷

	IF TALENT:奴隷:傷心 && (TALENT:奴隷:親愛 || TALENT:奴隷:淫乱 || TALENT:奴隷:NTR || TALENT:奴隷:公衆便所)
		PRINTFORMW %CALLNAME:奴隷%は傷心状態から回復した
		TALENT:奴隷:傷心 = 0
	ENDIF

RETURN 0

;---------------------------------------------------------------------------
;満足度変動
@CHK_SATISFACION_CHANGE(奴隷)
	#DIM 奴隷

	IF CFLAG:奴隷:満足度上昇
		MAXBASE:奴隷:満足 += RAND(50, 100)
		CFLAG:奴隷:満足度上昇 = 0
	ELSE
		IF TALENT:奴隷:淫乱
			MAXBASE:奴隷:満足 = MAX(MAXBASE:奴隷:満足 - RAND(1, 10), 5000)
		ELSE
			MAXBASE:奴隷:満足 = MAX(MAXBASE:奴隷:満足 - RAND(1, 10), 1000)
		ENDIF
	ENDIF

RETURN 0

;-------------------------------------------------------------------------------
;気力回復
@CHK_WILL_POWER_RECOVERY(奴隷)
	#DIM 奴隷

	BASE:奴隷:気力 = MAXBASE:奴隷:気力
RETURN 0

;-------------------------------------------------------------------------------
;体力回復
@CHK_BODY_POWER_RECOVERY(奴隷)
	#DIM 奴隷
	#DIM 体力回復分

	体力回復分 = MAXBASE:奴隷:体力 / 3
	;回復早い、回復遅い
	体力回復分 = 体力回復分 * 3 / (3 - TALENT:奴隷:回復速度)
	BASE:奴隷:体力 = MIN(MAXBASE:奴隷:体力, BASE:奴隷:体力 + 体力回復分)
	;CFLAG:奴隷:衰弱 = 0
RETURN 0

;-------------------------------------------------------------------------------
;精力増減
@CHK_VITARITY_CHANGE(奴隷)
	#DIM 奴隷

	BASE:奴隷:精力 = MIN(MAXBASE:奴隷:精力, BASE:奴隷:精力 + MAXBASE:奴隷:精力 * BASE:奴隷:体力 / MAXBASE:奴隷:体力)
	;ついでに性欲も増減
	IF TALENT:奴隷:淫乱
		CFLAG:奴隷:性欲 += RAND:(((ABL:奴隷:欲望 + 1)*2) + 5)
	ELSEIF !TALENT:奴隷:妊娠
		CFLAG:奴隷:性欲 += RAND:(ABL:奴隷:欲望 + 1) + 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;肉体改造
@CHK_BODY_STATE_CHANGE(奴隷)
	#DIM 奴隷

	IF FLAG:56 == 1
		IF EXP:奴隷:Ｃ絶頂経験 >= 50 && ABL:奴隷:Ｃ感覚 >=3 && TALENT:奴隷:Ｃ感度 == -1
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けクリトリスが少し敏感になったようだ
			TALENT:奴隷:Ｃ感度 = 0
		ENDIF
		
		IF EXP:奴隷:Ｂ絶頂経験 >= 50 && ABL:奴隷:Ｂ感覚 >=3 && TALENT:奴隷:Ｂ感度 == -1
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果バストが少し敏感になったようだ
			TALENT:奴隷:Ｂ感度 = 1
		ENDIF
		
		IF EXP:奴隷:Ｖ絶頂経験 >= 50 && ABL:奴隷:Ｖ感覚 >=3 && TALENT:奴隷:Ｖ感度 == -1
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果ヴァギナが少し敏感になったようだ
			TALENT:奴隷:Ｖ感度 = 1
		ENDIF
		
		IF EXP:奴隷:Ａ絶頂経験 >= 50 && ABL:奴隷:Ａ感覚 >=3 && TALENT:奴隷:Ａ感度 == -1
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果アナルが少し敏感になったようだ
			TALENT:奴隷:Ａ感度 = 1
		ENDIF
		IF EXP:奴隷:Ｃ絶頂経験 >= 100 && ABL:奴隷:Ｃ感覚 >=5 && TALENT:奴隷:Ｃ感度 == 0
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果豆の部分が敏感になったようだ
			TALENT:奴隷:Ｃ感度 = 1
		ENDIF
		
		IF EXP:奴隷:Ｂ絶頂経験 >= 100 && ABL:奴隷:Ｂ感覚 >=5 && TALENT:奴隷:Ｂ感度 == 0
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果雌の象徴が敏感になったようだ
			TALENT:奴隷:Ｂ感度 = 1
		ENDIF
		
		IF EXP:奴隷:Ｖ絶頂経験 >= 100 && ABL:奴隷:Ｖ感覚 >=5 && TALENT:奴隷:Ｖ感度 == 0
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果おマンコが敏感になったようだ
			TALENT:奴隷:Ｖ感度 = 1
		ENDIF
		
		IF EXP:奴隷:Ａ絶頂経験 >= 100 && ABL:奴隷:Ａ感覚 >=5 && TALENT:奴隷:Ａ感度 == 0
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽を受けた結果ケツマンコが敏感になったようだ
			TALENT:奴隷:Ａ感度 = 1
		ENDIF
		IF TALENT:奴隷:Ｃ感度 == 1 && TALENT:奴隷:Ｂ感度 == 1 && TALENT:奴隷:Ｖ感度 == 1 && TALENT:奴隷:Ａ感度 == 1 && EXP:奴隷:Ｃ絶頂経験 >= 100 && EXP:奴隷:Ｂ絶頂経験 >= 100 && EXP:奴隷:Ｖ絶頂経験 >= 100 && EXP:奴隷:Ａ絶頂経験 >= 100 && TALENT:奴隷:快感応答 <= 0
			PRINTFORML %CALLNAME:奴隷%は度重なる快楽の虜となり快感に対して逆らえなくなったようだ
			TALENT:奴隷:快感応答 = 1
		ENDIF
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;緩んだ括約筋を少し締め直す処理
;基本的には一度緩んでしまうとなかなか戻らない。
@CHK_TIGHTNESS_CHANGE(奴隷)
	#DIM 奴隷
	#DIM 回復力
	
	;Ａ締め
	;回復力は拡張経験から決定(1%〜20%)。拡張経験400以上の場合は常に1%。
	回復力 = MAX(1,RAND(MAX(21-SQRT(EXP:奴隷:Ａ拡張経験),1))) * BASE:奴隷:Ａ緩さ / 100
	BASE:奴隷:Ａ緩さ = MAX(MAXBASE:奴隷:Ａ緩さ/2, BASE:奴隷:Ａ緩さ - 回復力)
	MAXBASE:奴隷:Ａ緩さ = MAX(MAXBASE:奴隷:Ａ緩さ,BASE:奴隷:Ａ緩さ)

	;Ｖ締め
	;回復力は拡張経験から決定(1%〜20%)。拡張経験400以上の場合は常に1%。
	回復力 = MAX(1,RAND(MAX(21-SQRT(EXP:奴隷:Ｖ拡張経験),1))) * BASE:奴隷:Ｖ緩さ / 100
	BASE:奴隷:Ｖ緩さ = MAX(MAXBASE:奴隷:Ｖ緩さ/2, BASE:奴隷:Ｖ緩さ - 回復力)
	MAXBASE:奴隷:Ｖ緩さ = MAX(MAXBASE:奴隷:Ｖ緩さ,BASE:奴隷:Ｖ緩さ)

RETURN 0

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@RANK_POINT
	VARSET LOCAL
	;清潔度不足による減算処理
	FOR LOCAL,0,21
		IF FLAG:(100 + LOCAL) >= 5000
			;清潔度:ゴミの山１室につき$400減算
			LOCAL:1 += 400
		ELSEIF FLAG:(100 + LOCAL) >= 2000
			;清潔度:やや低or低１室につき$200減算
			LOCAL:1 += 200
		ELSEIF FLAG:(100 + LOCAL) >= 500
			;清潔度:やや高or中１室につき$100減算
			LOCAL:1 += 100
		ENDIF
	NEXT
	;部屋数増えてるんで、多少増額しておきたい感
	LOCAL:1 = MAX(0,1400 - LOCAL:1)
	;主人公がお嬢様かふらんちゃんならお小遣い(紅主)
	IF NAME:MASTER == "Remilia Scarlet"
		LOCAL:2 = 100
	ELSEIF NAME:MASTER == "Flandre Scarlet"
		LOCAL:2 = 50
	ENDIF
	SIF NAME:MASTER == "Remilia Scarlet" || NAME:MASTER == "Flandre Scarlet"
		PRINTFORML Regular income: %TOSTR(LOCAL:2,"#,###")%$
	SIF LOCAL:1 > 0
		PRINTFORML House cleanness bonus: %TOSTR(LOCAL:1,"#,###")%$
	SIF FLAG:好感度上昇の累積値 > 0
		PRINTFORML Bonus for increasing favorability: %TOSTR(FLAG:好感度上昇の累積値 * 5,"#,###")%$
	LOCAL:2 += LOCAL:1 +  LOCAL:2 +FLAG:好感度上昇の累積値 * 5
	FLAG:好感度上昇の累積値 = 0
	MONEY += LOCAL:2

	WAIT
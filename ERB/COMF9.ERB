;-------------------------------------------------
;自慰
;愛撫系コマンド、レベル3
;-------------------------------------------------
@COM9
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
IF HAS_PENIS(TARGET)
	LOCALS = {TARGET}/
	CSTR:TARGET:10 += LOCALS
	TCVAR:TARGET:2 = 3
	;強度
	TCVAR:TARGET:4 = 500
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:快Ｃ = 80
SOURCE:快Ｂ = 80
SOURCE:性行動 = 100
SOURCE:恐怖 = 200
SOURCE:露出 = 400
SOURCE:屈従 = 500
SOURCE:逸脱 = 300
SOURCE:反感 = 250
;バイブ
IF TEQUIP:バイブ
	SOURCE:快Ｖ = 80
	SOURCE:屈従 += 100
	SOURCE:反感 += 100
	TIMES SOURCE:快Ｃ , 0.70
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;アナルバイブ
IF TEQUIP:アナルバイブ
	SOURCE:屈従 += 150
	SOURCE:反感 += 150
	TIMES SOURCE:快Ｃ , 0.70
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;シャワー
IF TEQUIP:32
	SOURCE:快Ｃ += 50
	SOURCE:露出 += 100
	SOURCE:屈従 += 50
	SOURCE:反感 += 80
ENDIF
;ビデオ
IF TEQUIP:28
	SOURCE:液体 += 50
	SOURCE:欲情 += 100
	SOURCE:反感 += 200
ENDIF
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 400
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の指⇔奴隷のＢの汚れが移動
STAIN:手 |= STAIN:Ｂ
STAIN:Ｂ |= STAIN:手

;奴隷の指⇔奴隷のＶの汚れが移動
STAIN:手 |= STAIN:Ｖ
STAIN:Ｖ |= STAIN:手

;シャワーオナニーの場合汚れをリセット、潤滑更に半分。
IF TEQUIP:32 == 1
	STAIN:手 = 汚れ_なし
	STAIN:Ｐ = 汚れ_ペニス
	STAIN:Ｖ = 汚れ_愛液
	STAIN:Ａ = 汚れ_アナル
	STAIN:膣内 = 汚れ_なし
	PALAM:4 /= 2
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオ撮影
	EXP:自慰経験 += 2
	EXP:調教自慰経験 += 2
ELSE
	EXP:自慰経験 += 1
	EXP:調教自慰経験 += 1
ENDIF
;公開オナニー時は異常経験付加
IF TEQUIP:ビデオ撮影 && !GETBIT(CFLAG:3,0)
	EXP:異常経験 ++
	SETBIT CFLAG:異常経験,0
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
RETURN 1


@CAN_COM9(ARG)
SKIPDISP ARG
SIF TEQUIP:ビデオ撮影
	PRINT Public 
SIF TEQUIP:シャワー
	PRINT Shower 
IF TEQUIP:バイブ && TEQUIP:アナルバイブ
	PRINT Double Vibrator 
ELSEIF TEQUIP:バイブ
	PRINT Vibrator 
ELSEIF TEQUIP:アナルバイブ
	PRINT Anal Vibrator 
ENDIF

IF TEQUIP:ビデオ撮影 || TEQUIP:シャワー || TEQUIP:バイブ || TEQUIP:アナルバイブ
	PRINTL Masturbation
ELSE
	PRINTL Onanism
ENDIF
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;VARSET TFLAG:実行値
TFLAG:実行値 = 0
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += ABL:欲望 * 3
	PRINT Lust
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;ABL:露出癖
IF ABL:露出癖
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += ABL:露出癖 * 4
	PRINT Exposure
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 4,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += ABL:自慰中毒 * 3
	PRINT Masturbation Addict
	PRINTV 'LV,ABL:自慰中毒,'(,ABL:自慰中毒 * 3,')
	TFLAG:実行値表示用フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += MARK:快楽刻印 * 3
	PRINT Pleasure Mark
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	TFLAG:実行値表示用フラグ = 1
ENDIF

;自制心
IF TALENT:自制心
	PRINT  - 
	TFLAG:実行値 -= 5
	PRINT Self Control
	PRINTV '(,5,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	TFLAG:実行値 -= 5
	PRINT Shy
	PRINTV '(,5,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;恥薄い
IF TALENT:羞恥心 < 0
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += 2
	PRINT Shameless
	PRINTV '(,2,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += 5
	PRINT Masturbating
	PRINTV '(,5,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;快感に素直
IF TALENT:快感応答 > 0
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += 5
	PRINT Enjoys Pleasure
	PRINTV '(,5,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	TFLAG:実行値 -= 5
	PRINT Denies Pleasure
	PRINTV '(,5,')
	TFLAG:実行値表示用フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += 8
	PRINT Aphrodisiac
	PRINTV '(,8,')
	TFLAG:実行値表示用フラグ = 1
ENDIF
;鍵を持っていると実行可能
IF FLAG:貞操帯鍵購入フラグ == TARGET
	SIF TFLAG:実行値表示用フラグ
		PRINT  + 
	TFLAG:実行値 += 100
	PRINT Chastity Belt Key
	PRINTV '(,100,')
	TFLAG:実行値表示用フラグ = 100
ENDIF	

;合計を表示(33以上で実行)
PRINT  = 
PRINTV TFLAG:実行値

;難易度上昇
;公開で+10、バイブで+5、アナルバイブで+5、シャワーで＋3
LOCAL = 33
SIF TEQUIP:ビデオ撮影
	LOCAL += 10
SIF TEQUIP:シャワー
	LOCAL += 3
SIF TEQUIP:バイブ
	LOCAL += 5
SIF TEQUIP:アナルバイブ
	LOCAL += 5

SIF TFLAG:実行値 < LOCAL
	PRINT  < 
SIF TFLAG:実行値 == LOCAL
	PRINT  = 
SIF TFLAG:実行値 > LOCAL
	PRINT  > 
PRINT Required Value 
PRINTV LOCAL

WAIT
SKIPDISP 0
;実行できない
SIF TFLAG:実行値 < LOCAL
	RETURN 0
RETURN 1


;--------------------------------------
;子供の行動範囲
;--------------------------------------
;CFLAG:ARG:75(1 = 乳児, 2 = 幼児, 3 = 思春期もとい成長期, 4 = 成人)
@CHARA_MOVEMENT_ACTIVETIME_CHILD(ARG)
IF CFLAG:ARG:成長度 == 成長度_乳児
	;常に寝てる
	RETURN (0 <= TIME && TIME <= 1440) || CFLAG:ARG:衰弱
ELSEIF CFLAG:ARG:成長度 == 成長度_幼児
	;20時〜8時は寝てる
	RETURN TIME < 480 || 1200 < TIME || CFLAG:ARG:衰弱
ELSEIF CFLAG:ARG:成長度 == 成長度_成長期
	;23時〜7時は寝てる
	RETURN TIME < 420 || 1380 < TIME || CFLAG:ARG:衰弱
ELSE
	;24時〜6時は寝てる
	RETURN TIME < 360 || CFLAG:ARG:衰弱
ENDIF

@CHARA_MOVEMENT_MOVERATE_CHILD(ARG)
IF CFLAG:ARG:成長度 == 成長度_乳児
	;常に母親と一緒
	RETURN 1
ELSEIF CFLAG:ARG:成長度 == 成長度_幼児
	;大人しいとあまり動かない
	SIF TALENT:ARG:応答 == -1
		RETURN 120
	;それ以外は動き回る
	RETURN 50
ELSEIF CFLAG:ARG:成長度 >= 成長度_成長期
	;大人しいとあまり動かない
	SIF TALENT:ARG:応答 == -1
		RETURN 120
	;それ以外は動き回る
	RETURN 80
ELSE
	RETURN 1440
ENDIF

@CHARA_MOVEMENT_CHILD(ARG,ARG:1)
;書かれていない場所へは移動しない
;常に母の元へ
IF CFLAG:(ARG:1):成長度 == 成長度_乳児
	RESULT:1 = CFLAG:(CFLAG:(ARG:1):母親):現在位置 * 倍_万 + 1000
	
;性格次第でどこにでも行く(臆病だと暗い部屋(納屋と地下室)には近寄らない(例外有))
ELSEIF CFLAG:(ARG:1):成長度 == 成長度_幼児
	LOCAL:3 = 1
	FOR LOCAL:4, 1, 場所番号最大値+1
		SELECTCASE LOCAL:4
			CASE 場所_納屋
				IF TALENT:(ARG:1):度胸 == -1 && !(GET_NOW_POS(CFLAG:(ARG:1):父親) == 6 || GET_NOW_POS(CFLAG:(ARG:1):母親) == 6)
					RESULT:(LOCAL:3) = 場所_納屋 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASE 場所_地下室
				IF TALENT:(ARG:1):度胸 == -1 && !(CFLAG:(ARG:1):父親 == 6 || CFLAG:(ARG:1):母親 == 6)
					RESULT:(LOCAL:3) = 場所_地下室 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASE 場所_大浴場
				IF CFLAG:(ARG:1):風呂 >= 1000
					RESULT:(LOCAL:3) = 場所_大浴場 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASEELSE
				RESULT:(LOCAL:3) = LOCAL:4 * 倍_万 + (FLAG:(100 + LOCAL:4) + 100)
				LOCAL:3 ++
		ENDSELECT
	NEXT
;性格次第でどこにでも行く(臆病だと暗い部屋(納屋と地下室)には近寄らない(例外有))
ELSEIF CFLAG:(ARG:1):成長度 >= 成長度_成長期
	LOCAL:3 = 1
	FOR LOCAL:4, 1, 場所番号最大値+1
		SELECTCASE LOCAL:4
			CASE 場所_納屋
				IF TALENT:(ARG:1):度胸 == -1 && !(GET_NOW_POS(CFLAG:(ARG:1):父親) == 6 || GET_NOW_POS(CFLAG:(ARG:1):母親) == 6)
					RESULT:(LOCAL:3) = 場所_納屋 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASE 場所_地下室
				IF TALENT:(ARG:1):度胸 == -1 && !(CFLAG:(ARG:1):父親 == 6 || CFLAG:(ARG:1):母親 == 6)
					RESULT:(LOCAL:3) = 場所_地下室 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASE 場所_大浴場
				IF CFLAG:(ARG:1):風呂 >= 1000
					RESULT:(LOCAL:3) = 場所_大浴場 * 倍_万 + 200
					LOCAL:3 ++
				ENDIF
			CASEELSE
				RESULT:(LOCAL:3) = LOCAL:4 * 倍_万 + (FLAG:(100 + LOCAL:4) + 100)
				LOCAL:3 ++
		ENDSELECT
	NEXT
ELSE
	RETURN 0
ENDIF

;引数で指定されたキャラの現在位置取得
;ARG … チェック対象キャラ
;戻り値 … 場所コード, -1:不明（キャラが「誰か」など)
@GET_NOW_POS(ARG)
#FUNCTION
;「誰か」（客, 村人）
SIF ARG == 人物_客
	RETURNF -1
SIF ARG == 人物_村人
	RETURNF -1
;訪問者
SIF ARG == 人物_訪問者
	RETURNF FLAG:訪問者の現在位置
;それ以外
RETURNF CFLAG:ARG:現在位置

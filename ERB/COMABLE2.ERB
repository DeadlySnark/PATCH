;-------------------------------------------------------------------------------
;COMABLE一括管理
; RETURN 0 コマンドの実行可能
; RETURN 1 コマンドの実行不可
@GLOBAL_COMABLE(コマンド番号)
	#FUNCTION
	#DIM コマンド番号
	;コマンドフィルタ
	LOCALS = /{コマンド番号}/
	IF STRCOUNT(SAVESTR:0,LOCALS)
		RESULT = 0
		RETURNF 1
	ENDIF
	;一括管理
	;TFLAG:COMABLE管理,(1=日常ON 2=ウフフON)
	SELECTCASE コマンド番号
		CASE 000 TO 199
			;0〜100類(通常調教コマンド)、500類(多人数プレイ)は、うふふOFFの場合不可、自慰の場合も不可
			IF TFLAG:COMABLE管理 != 2 || CFLAG:うふふ == 3
				RESULT = 0
				RETURNF 1
			ENDIF
		CASE 200 TO 299
			;200系（脱衣）はうふふONでなければ不可
			IF TFLAG:COMABLE管理 != 2
				RESULT = 0
				RETURNF 1
			ENDIF
		CASE 300 TO 309
			;会話(300)、お茶を入れる(301)、スキンシップ(302)などは日常モード限定
			IF TFLAG:COMABLE管理 != 1
				RESULT = 0
				RETURNF 1
			ENDIF
		CASE 310 TO 399
			;尻を撫でる(310)〜指挿入(316)などは日常モード限定
			IF TFLAG:COMABLE管理 == 2 && コマンド番号 != 349
				RESULT = 0
				RETURNF 1
			ENDIF
			;お風呂場ではガチエロになるので、押し倒され状態でない場合、連れ出す(351)と押し倒す(350)と邪魔しない(354)以外は不可
			IF BATHROOM(CFLAG:MASTER:現在位置) && CFLAG:うふふ != 2
				IF コマンド番号 != 350 && コマンド番号 != 351 && コマンド番号 != 354
					RESULT = 0
					RETURNF 1
				ENDIF
			ENDIF
		CASE 349
			;ソフトなウフフ
		CASE 400 TO 499
			;400系(休息、コレクションなどあなた特殊系コマンド)はうふふONの場合不可
			IF TFLAG:COMABLE管理 == 2
				RESULT = 0
				RETURNF 1
			ENDIF
		CASE 500 TO 599
			;0〜100類(通常調教コマンド)、500類(多人数プレイ)は、うふふOFFの場合不可、自慰の場合も不可
			IF TFLAG:COMABLE管理 != 2 || CFLAG:うふふ == 3
				RESULT = 0
				RETURNF 1
			ENDIF
		CASE 600 TO 699
			;600系（自慰専用）は自慰状態じゃない場合不可
			IF CFLAG:うふふ != 3
				RESULT = 0
				RETURNF 1
			ENDIF
		CASEELSE
	ENDSELECT
	IF TARGET:1 > 0 && CFLAG:睡眠 == 1 && (コマンド番号 < 400 || コマンド番号 >= 500)
		;寝ている相手には大半のコマンドは不可
		RESULT = 0
		RETURNF 1
	ENDIF
	;ビデオ処理（固定カメラならば制限なし）
	IF (TFLAG:ビデオカメラ == 1 && !ASSIPLAY) || (TFLAG:ビデオカメラ == 2 && ASSIPLAY)
		;撮影者が調教中、ビデオ片手ではいろいろ制限される
		SELECTCASE TRAINNAME:コマンド番号
			CASE "愛撫"
			CASE "指挿れ"
			CASE "アナル愛撫"
			CASE "胸愛撫"
			CASE "乳首責め"
			CASE "秘貝開帳"
			CASE "自慰"
			CASE "何もしない"
			CASE "ローター"
			CASE "Ｅマッサージャ"
			CASE "クリキャップ"
			CASE "バイブ"
			CASE "アナルバイブ"
			CASE "アナルビーズ"
			CASE "ニプルキャップ"
			CASE "搾乳機"
			CASE "騎乗位"
			CASE "騎乗位アナル"
			CASE "手淫"
			CASE "フェラチオ"
			CASE "パイズリ"
			CASE "素股"
			CASE "泡踊り"
			CASE "足コキ"
			CASE "スパンキング"
			CASE "鞭"
			CASE "蝋燭"
			CASE "針"
			CASE "アイマスク"
			CASE "縄"
			CASE "助手を犯させる"
			CASE "足コキする"
			CASE "イラマチオ"
			CASE "フィストファック"
			CASE "アナルフィスト"
			CASE "放尿"
			CASE "アナル奉仕"
			CASE "浣腸"
			CASE "拡張バルーン"
			CASE "アナル電極"
			CASE "Gスポット刺激"
			CASE "乱れ牡丹"
			CASE "ダブル素股"
			CASE "ダブルパイズリ"
			CASEELSE
				RESULT = 0
				RETURNF 1
		ENDSELECT
		RETURNF 0
	ELSEIF TFLAG:ビデオカメラ == 1 && ASSIPLAY
		;MASTERが撮影中、助手が調教中
		SELECTCASE TRAINNAME:コマンド番号
			CASE "二穴挿し"
			CASE "助手を犯す"
			CASEELSE
				RESULT = 0
				RETURNF 1
		ENDSELECT
		RETURNF 0
	ELSEIF TFLAG:ビデオカメラ == 2 && !ASSIPLAY
		;助手が撮影中、MASTERが調教中
		SELECTCASE TRAINNAME:コマンド番号
			CASE "二穴挿し"
			CASE "助手を犯す"
			CASE "ダブル素股"
			CASE "ダブルパイズリ"
			CASEELSE
				RESULT = 0
				RETURNF 1
		ENDSELECT
		RETURNF 0
	ENDIF
RETURNF 0

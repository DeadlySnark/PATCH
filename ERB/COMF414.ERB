;食事
@COM414

IF TARGET > 0
	CALL DISH_COM_SUCCESS_RATE
	TFLAG:193 = RESULT
	
	IF GETDISHMENU(FLAG:料理 % 1000) == "軽食"
		DOWNBASE:MASTER:気力 += 50
		DOWNBASE:MASTER:体力 += 25
	ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "主食"
		DOWNBASE:MASTER:気力 += 100
		DOWNBASE:MASTER:体力 += 50
	ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "デザート"
		DOWNBASE:MASTER:気力 += 50
		DOWNBASE:MASTER:体力 += 50
	ENDIF

	;その場に居るTARGET全員で回す
	LOCAL:1 = TARGET
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):睡眠
			CONTINUE
		TARGET = TARGET:LOCAL
		CALL COM414_1
	NEXT
	TARGET = LOCAL:1
ENDIF

IF GETDISHMENU(FLAG:料理 % 1000) == "軽食"
	BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + MAXBASE:MASTER:体力 / 15 , MAXBASE:MASTER:体力)
	BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + MAXBASE:MASTER:気力 / 5 , MAXBASE:MASTER:気力)
	;食事をしたフラグ
	TCVAR:PLAYER:304 |= 1
	TIME += 30
ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "主食"
	BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + MAXBASE:MASTER:体力 / 10 , MAXBASE:MASTER:体力)
	BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + MAXBASE:MASTER:気力 / 3 , MAXBASE:MASTER:気力)
	;食事をしたフラグ
	TCVAR:PLAYER:304 |= 2
	TIME += 60
ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "デザート"
	BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + MAXBASE:MASTER:体力 / 15 , MAXBASE:MASTER:体力)
	BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + MAXBASE:MASTER:気力 / 2 , MAXBASE:MASTER:気力)
	;食事をしたフラグ
	TCVAR:PLAYER:304 |= 4
	TIME += 60
ENDIF


TCVAR:PLAYER:305 = 0
TCVAR:PLAYER:306 = 0
RETURN 1

@COM414_1

DOWNBASE:気力 += 10

;固定で獲得するソース
SELECTCASE TCVAR:PLAYER:305
	CASE 0
		SOURCE:歓楽 = 800
	CASE 1
		SOURCE:歓楽 = 400
	CASE 2
		SOURCE:歓楽 = 100
		SOURCE:不潔 = 400
		SOURCE:反感 = 400
ENDSELECT

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 60)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 300 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 600 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 900 + (ABL:従順 * 75)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1500 + (ABL:従順 * 75)
ELSE
	SOURCE:歓楽 += 2500 + (ABL:従順 * 30)
ENDIF

;好感度をみる
IF CFLAG:好感度 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:好感度 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


SOURCE:受動 = 200 + 100 * ABL:従順
SOURCE:征服 = 200 + 100 * ABL:サドっ気

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:反感 , 2.00
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:反感 , 0.50
ENDIF

IF GETDISHMENU(FLAG:料理 % 1000) == "軽食"
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 20 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 5 , MAXBASE:気力)
	TCVAR:304 |= 1
ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "主食"
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 10 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 3 , MAXBASE:気力)
	TCVAR:304 |= 2
ELSEIF GETDISHMENU(FLAG:料理 % 1000) == "デザート"
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 15 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 2 , MAXBASE:気力)
	TCVAR:304 |= 4
ENDIF

;混ぜ物の効果
CALL DISHMIX_RESULT, TARGET


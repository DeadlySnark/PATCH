;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@EVENTCOM
	TFLAG:現在覗いている場所 = 0

;-------------------------------------------------------------------------------
;ターン終了の処理
@EVENTCOMEND
	#DIM 奴隷
	#DIM 屈服度上昇値
	CALL TURN_RESET
	;気温・天候変更
	CALL 天候によるステータス増減処理
	CALL 現在気温設定
	CALL 天候状態
	;特殊起床処理
	CALL 特殊起床
	TFLAG:行為騒音 = 0
	;全員分ループして処理
	FOR 奴隷, 0, CHARANUM
		CALL 桃霧処理(奴隷)
		;満足度最大値上昇処理
		SIF !CFLAG:奴隷:満足度上昇 && BASE:奴隷:満足 >= MAXBASE:奴隷:満足
			CFLAG:奴隷:満足度上昇 = 1
		;満足後の絶頂で性欲減少
		IF 奴隷 == MASTER && EXP:奴隷:絶頂経験 > CFLAG:奴隷:前回絶頂経験数
			CFLAG:奴隷:性欲 /= 2
		ELSEIF CFLAG:奴隷:満足度上昇 && EXP:奴隷:絶頂経験 > CFLAG:奴隷:前回絶頂経験数
			CFLAG:奴隷:性欲 /= 2
		ENDIF
		;自慰処理
		IF CFLAG:奴隷:自慰中 > 0
			CALL キャラ自慰中処理, 奴隷
		ENDIF
		;娼婦紋による発情処理
		CALL COM417_add_sexual_desire(奴隷)
		;傷心時の屈服度増加
		IF TALENT:奴隷:傷心 && FLAG:訪問者の現在位置 == CFLAG:奴隷:現在位置
			屈服度上昇値 = TIME_PROGRESS(TFLAG:300)
			IF TALENT:奴隷:傷心 >= 1000
				TIMES 屈服度上昇値, 1.30
			ELSEIF TALENT:奴隷:傷心 >= 600
				TIMES 屈服度上昇値, 1.15
			ELSEIF TALENT:奴隷:傷心 >= 300
				TIMES 屈服度上昇値, 0.90
			ELSEIF TALENT:奴隷:傷心 >= 150
				TIMES 屈服度上昇値, 0.70
			ELSE
				TIMES 屈服度上昇値, 0.50
			ENDIF
			CALL NTR_ADD_SURRENDER(奴隷, 屈服度上昇値)
		ENDIF
		CFLAG:奴隷:前回絶頂経験数 = EXP:奴隷:絶頂経験
	NEXT
	;パンツ没収、収集
	IF TFLAG:COMABLE管理 < 2
		IF TARGET > 0 && !CFLAG:睡眠
			IF STRLENS(CSTR:MASTER:12) && 収集品死守戦発生(TARGET)
				PRINTL 
				CALL 収集品死守戦開始, TARGET, 1
				IF RESULT == 1
					PRINTFORMW %CALLNAME:TARGET% thinks you're suspicious and searched you out...
					PRINTFORMW 
					PRINTFORMW 
					PRINTFORMW 
					PRINTFORMW Collectibles were confiscated.
					SIF !GETBIT (CFLAG:10 ,0)
						PRINTFORMW %CALLNAME:TARGET% will remember this as your weakness!
					SETBIT CFLAG:10 ,0
					CALL KOJO_EVENT(11, TARGET, 0)
					CSTR:MASTER:12 =
				ENDIF
			ELSEIF FLAG:貞操帯鍵購入フラグ && RAND:2 && CFLAG:TARGET:NTR貞操帯反応
				;NTR貞操帯鍵
				PRINTL 
				PRINTFORMW %CALLNAME:TARGET% thinks you're suspicious and searched you out...
				PRINTFORMW 
				PRINTFORMW 
				PRINTFORMW 
				PRINTFORMW The key to the chastity belt has been discovered.
				CALL NTR_LOCK_REACTION(TARGET)
			ENDIF
		ENDIF
	ENDIF
	IF BASE:MASTER:体力 <= 500
		PRINTW （Stamina neared the limit.　Day will be over.）
		FLAG:1840 = 0
		CFLAG:MASTER:睡眠 = 1
		CFLAG:MASTER:就寝時間 = DATETIME()
		IF TARGET > 0 && TALENT:恋慕 && CFLAG:現在位置 != 場所_訪問者宅 && BASE:体力 >= 500 && !CFLAG:睡眠
			PRINTFORMW %CALLNAME% brought %CALLNAME:MASTER% to %HIS_HER(MASTER)% private room...
			CFLAG:MASTER:現在位置 = CFLAG:MASTER:開始位置
			CFLAG:現在位置 = CFLAG:MASTER:開始位置
			CALL KOJO_EVENT(7, TARGET, 5)
		ENDIF
		FOR 奴隷,0,CHARANUM
			CFLAG:奴隷:うふふ = 0
		NEXT
		BEGIN AFTERTRAIN
	ENDIF
	IF TARGET && CFLAG:不機嫌
		PRINTFORMW %CALLNAME:TARGET% is angry...
		CFLAG:TARGET:同行 = 0
		FOR 奴隷,0,CHARANUM
			CFLAG:奴隷:うふふ = 0
		NEXT
	ENDIF
	IF TARGET && BASE:体力 < 500 && CFLAG:TARGET:現在位置 != 場所_訪問者宅
		PRINTFORMW （%CALLNAME:TARGET%'s stamina neared the limit.）
		CFLAG:TARGET:衰弱 = 360
		CFLAG:TARGET:睡眠深度 = 10000
		CFLAG:TARGET:自慰中 = 0
		CALL CHARA_SLEEP, TARGET
		FOR 奴隷,0,CHARANUM
			CFLAG:奴隷:うふふ = 0
		NEXT
	ENDIF
	;睡眠薬などによる睡眠
	IF TARGET && CFLAG:TARGET:睡眠深度 < 0 && CFLAG:TARGET:現在位置 != 場所_訪問者宅
		PRINTFORMW %CALLNAME:TARGET% suddenly became very drowsy and unable to resist it fell asleep.
		CFLAG:TARGET:衰弱 = 360
		CFLAG:TARGET:睡眠深度 = 20000
		CFLAG:TARGET:自慰中 = 0
		CALL CHARA_SLEEP, TARGET
		FOR 奴隷,0,CHARANUM
			CFLAG:奴隷:うふふ = 0
		NEXT
	ENDIF
	CALL ChastityBelt_Check()

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@TURN_RESET
	#DIM LOOP_CHR
	#DIM LOOP_I			; TCVARリセット用, 他ループ用
	#DIMS EJ_POS		; 射精箇所
	;処女喪失(念のため、程度。実際に破った時はLOST_VIRGINを呼ぶ事)
	FOR LOOP_CHR,0,CHARANUM
		IF TCVAR:LOOP_CHR:破瓜
			TALENT:LOOP_CHR:処女 = 0
		ENDIF
	NEXT
	;童貞喪失
	SELECTCASE SELECTCOM
		CASE 60,61,65,67,68
			TALENT:MASTER:童貞 |= 1
			TRYCALL memorial_mv_append(MASTER,TARGET,,6)
		CASE 62,63,66,69,70
			TALENT:MASTER:童貞 |= 2
			TRYCALL memorial_mv_append(MASTER,TARGET,,7)
	ENDSELECT
	SELECTCASE SELECTCOM
		CASE 64,97,98,99
			TALENT:童貞 |= 1
			TRYCALL memorial_mv_append(TARGET,MASTER,,6)
		CASE 92,93,94
			TALENT:童貞 |= 2
			TRYCALL memorial_mv_append(TARGET,MASTER,,7)
		CASE 71		; 二穴挿し
			IF MASTER_POSE(6,1,1)
				TALENT:童貞 |= 2
				TRYCALL memorial_mv_append(TARGET,MASTER,,7)
			ELSE
				TALENT:童貞 |= 1
				TRYCALL memorial_mv_append(TARGET,MASTER,,6)
			ENDIF
	ENDSELECT
	;既成事実
	FOR LOOP_CHR,1,CHARANUM
		SIF CFLAG:LOOP_CHR:うふふ
			SETBIT CFLAG:LOOP_CHR:既成事実 , 1
	NEXT
	;告白成功
	SIF SELECTCOM == 352 && TFLAG:193 > 0
		SETBIT CFLAG:1 , 0
	;食事
	SIF SELECTCOM == 414 || SELECTCOM == 415
		FLAG:料理 = 0
	;脱衣コマンド
	SELECTCASE SELECTCOM
			;上半身脱衣
		CASE 200
			EQUIP:上半身上着１ = 0
			EQUIP:上半身上着２ = 0
			EQUIP:ボディースーツ = 0
			EQUIP:ワンピース = 0
			EQUIP:着物 = 0
			EQUIP:レオタード = 0
			CALL CLOTHES_SETTING_TRAIN(TARGET)
			;下半身脱衣
		CASE 201
			EQUIP:下半身上着１ = 0
			EQUIP:下半身上着２ = 0
			EQUIP:スカート = 0
			CALL CLOTHES_SETTING_TRAIN(TARGET)
			;上半身下着脱衣
		CASE 202
			EQUIP:上半身下着１ = 0
			EQUIP:上半身下着２ = 0
			CALL CLOTHES_SETTING_TRAIN(TARGET)
			;下半身下着脱衣
		CASE 203
			EQUIP:下半身下着１ = 0
			EQUIP:下半身下着２ = 0
			CALL CLOTHES_SETTING_TRAIN(TARGET)
	ENDSELECT
	;調教履歴を1ターン保存
	FOR LOOP_CHR,0,CHARANUM
		TCVAR:LOOP_CHR:110 = TCVAR:LOOP_CHR:10
		TCVAR:LOOP_CHR:112 = TCVAR:LOOP_CHR:12
		SIF TCVAR:LOOP_CHR:102
			TCVAR:LOOP_CHR:102 --
		SIF !TCVAR:LOOP_CHR:102
			TCVAR:LOOP_CHR:103 = -1
		SIF TCVAR:LOOP_CHR:104
			TCVAR:LOOP_CHR:104 --
		SIF !TCVAR:LOOP_CHR:104
			TCVAR:LOOP_CHR:105 = -1
		;体位の保存
		FOR LOOP_I,1,11
			TEQUIP:LOOP_CHR:(110 + LOOP_I) = TEQUIP:LOOP_CHR:(100 + LOOP_I)
		NEXT
		;体勢制御
		IF TCVAR:LOOP_CHR:体勢制御
			TCVAR:LOOP_CHR:体勢制御 --
			SIF TCVAR:LOOP_CHR:体勢制御 == 0
				TCVAR:LOOP_CHR:体勢 = 0
		ENDIF
		;位置関係制御
		IF TCVAR:LOOP_CHR:位置関係制御
			TCVAR:LOOP_CHR:位置関係制御 --
			SIF TCVAR:LOOP_CHR:位置関係制御 == 0
				TCVAR:LOOP_CHR:位置関係 = 0
		ENDIF
		;ついでに射精処理用文字列変数の初期化
		CSTR:LOOP_CHR:10 = /
		CSTR:LOOP_CHR:11 = /
		;服装フラグ
		CALL CLOTHES_SETTING_TRAIN(LOOP_CHR)
	NEXT
	;BASEの変動
	FOR LOOP_CHR,0,CHARANUM
		;勃起度
		;加算後
		CFLAG:LOOP_CHR:勃起度２ = BASE:LOOP_CHR:勃起
		BASE:LOOP_CHR:勃起 = MIN(BASE:LOOP_CHR:勃起,1500)
		;DEBUGPRINTFORML %CALLNAME:LOOP_CHR+"の勃起＝"+TOSTR(BASE:LOOP_CHR:勃起)%
		IF NOWEX:LOOP_CHR:射精
			;射精後
			IF !MASTER_POSE(6,1) && !MASTER_POSE(7,1)
				;挿入中じゃなければ萎える
				BASE:LOOP_CHR:勃起 = (BASE:LOOP_CHR:勃起*(ABL:LOOP_CHR:欲望+1)*(BASE:LOOP_CHR:精力+200)/((MAXBASE:LOOP_CHR:精力+200)*(ABL:LOOP_CHR:欲望+10)))
			ELSE
				;挿入中なら1000を維持
				BASE:LOOP_CHR:勃起 = 1000
			ENDIF
		ELSE
			;射精時以外、精力が無いと萎えやすい。（精力100以下になると顕著）
			BASE:LOOP_CHR:勃起 = MIN(BASE:LOOP_CHR:勃起, ( BASE:LOOP_CHR:勃起 * (BASE:LOOP_CHR:精力+2000*(BASE:LOOP_CHR:精力>100))/(MAXBASE:LOOP_CHR:精力+2000)))
		ENDIF
		;勃起ペナルティ判定
		CALL SOJI_SexHara_ErectPunishment(LOOP_CHR)
	NEXT
	;フラグのリセット
	TFLAG:193 = 0
	TFLAG:194 = 0
	TSTR:2 = 
	;TCVARのリセット
	FOR LOOP_I,0,100
		FOR LOOP_CHR,0,CHARANUM
			TCVAR:LOOP_CHR:LOOP_I = 0
		NEXT
	NEXT
	;挿入履歴
	FOR LOOP_CHR,0,CHARANUM
		TCVAR:LOOP_CHR:100 = TEQUIP:LOOP_CHR:Ｖセックス
		TCVAR:LOOP_CHR:101 = TEQUIP:LOOP_CHR:Ａセックス
	NEXT
	TFLAG:110 = TCVAR:MASTER:130
	;ビデオテープの残量
	SIF TEQUIP:ビデオ撮影
		TEQUIP:ビデオ撮影 --
	;FIRSTTIMEにSELECTCOMをぶっこむ
	CALLF FIRSTTIME(TOSTR(SELECTCOM))
	FOR LOOP_CHR,1,CHARANUM
		SIF EXP_UP(15,LOOP_CHR)
			CALLF FIRSTTIME("精飲",0,LOOP_CHR)
	NEXT
	;射精箇所
	FOR LOOP_I,0,60
		IF GETBIT(TFLAG:1,LOOP_I)
			EJ_POS = "射精"{LOOP_I}
			CALLF FIRSTTIME(EJ_POS,0,TARGET)
		ENDIF
	NEXT
	PREVCOM = SELECTCOM
	;派生コマンドの履歴
	TFLAG:151 = TFLAG:50
	;前回のTARGET
	TFLAG:103 = TARGET
	TFLAG:150 = ASSIPLAY
	;経験値取表示用
	FOR LOOP_CHR,0,CHARANUM
		;現在位置の保存
		CFLAG:LOOP_CHR:前ターン位置 = CFLAG:LOOP_CHR:現在位置
		FOR LOOP_I,0,100
			TCVAR:LOOP_CHR:(400 + LOOP_I) = EXP:LOOP_CHR:LOOP_I
		NEXT
	NEXT
	;TFLAGのリセット
	REPEAT 100
		TFLAG:COUNT = 0
	REND
	;眠いと気力が減少していく
	IF TIME_PROGRESS(CFLAG:MASTER:起床時間) >= 1260 && !CFLAG:MASTER:睡眠
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 3 * TIME_PROGRESS(TFLAG:300) , 0)
		BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 5 * TIME_PROGRESS(TFLAG:300) , 0)
	ENDIF
	;行為者をリセット
	FOR LOOP_CHR,0,CHARANUM
		TCVAR:LOOP_CHR:行為者 = -1
	NEXT

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@桃霧処理(奴隷)
	#DIM 奴隷
	#DIM 性欲上昇値
	IF 天候(天候値) == "Peach Fog"
		CFLAG:奴隷:桃霧性欲フラグ += TIME_PROGRESS(TFLAG:300)
		IF OPENPLACE(CFLAG:奴隷:現在位置) == 2
			;野外
			IF CFLAG:奴隷:桃霧性欲フラグ >= 5
				性欲上昇値 = CFLAG:奴隷:桃霧性欲フラグ / 5
				CFLAG:奴隷:性欲 += 性欲上昇値
				CFLAG:奴隷:桃霧性欲フラグ -= (性欲上昇値*5)
			ENDIF
		ELSE
			;野外でない
			IF CFLAG:奴隷:桃霧性欲フラグ >= 20
				性欲上昇値 = CFLAG:奴隷:桃霧性欲フラグ / 20
				CFLAG:奴隷:性欲 += 性欲上昇値
				CFLAG:奴隷:桃霧性欲フラグ -= (性欲上昇値*20)
			ENDIF
		ENDIF
	ENDIF
RETURN 0

;------------------------------------------------
@キャラ自慰中処理(奴隷)
	#DIM 奴隷
	#DIM 自慰箇所
	#DIM 自慰箇所仮
	#DIM Ｃ自慰
	#DIM Ｖ自慰
	#DIM Ａ自慰
	#DIM Ｂ自慰
	#DIM 最大感覚
	#DIM 自慰回数
	#DIM 貞操帯着用中
	;前条件 奴隷が自室にいる＋部屋に誰もいない
	;部屋に到着していない場合は行わない
	IF !IS_PRIVATE_ROOM(奴隷, 0)
		CFLAG:奴隷:自慰中 = -ABS(CFLAG:奴隷:自慰中)
		RETURN
	ENDIF
	;部屋に誰か居れば中断(値をマイナスにして保存)
	IF GET_N_IN_ROOM(CFLAG:奴隷:現在位置,1,1,1) > 1
		CFLAG:奴隷:自慰中 = -ABS(CFLAG:奴隷:自慰中)
		RETURN
	ENDIF
	;睡眠中は自慰しない
	IF CFLAG:奴隷:睡眠
		CFLAG:奴隷:自慰中 = -ABS(CFLAG:奴隷:自慰中)
		RETURN
	ENDIF

	;LOCAL   前:誰かが来たか 後:弄る箇所数
	;LOCAL:1 弄る場所１
	;LOCAL:2 弄る場所２
	;LOCAL:3~6 弄る場所判定
	;弄る箇所数
	;	自慰中毒Lv0~1 １ヶ所
	;	          2~3 １〜２ヶ所
	;	          4~5 ２ヶ所
	;自慰行動保存 Bit 0=行為C 1=行為V 2=行為A 3=行為B 4=2点行為 5=絶頂
	CFLAG:奴隷:自慰行動保存 = 0

	貞操帯着用中 = (下着名１_２(奴隷)=="貞操帯")

	SELECTCASE ABL:奴隷:自慰中毒
		CASE 0, 1
			自慰箇所 = 1
		CASE 2, 3
			自慰箇所 = RAND:2 + 1
		CASEELSE
			自慰箇所 = 2
	ENDSELECT

	SIF 自慰箇所 == 2
		SETBIT CFLAG:奴隷:自慰行動保存, 4

	BASE:奴隷:体力 -= (15 * 自慰箇所)
	BASE:奴隷:気力 -= (10 * 自慰箇所)

	;弄る場所設定、感覚の高い順、貞操帯着用時はＣＶＡは半減
	最大感覚 = MAX(ABL:奴隷:Ｃ感覚, ABL:奴隷:Ｖ感覚, ABL:奴隷:Ａ感覚, ABL:奴隷:Ｂ感覚)
	Ｃ自慰 = 0
	Ｖ自慰 = 0
	Ａ自慰 = 0
	Ｂ自慰 = 0
	自慰箇所仮 = 0
	WHILE (最大感覚>-1)
		IF !Ｃ自慰 && 最大感覚 == (ABL:奴隷:Ｃ感覚 / (貞操帯着用中+1))
			Ｃ自慰 = 1
			自慰箇所仮 += 1
			SETBIT CFLAG:奴隷:自慰行動保存, 0
		ENDIF
		SIF 自慰箇所 == 自慰箇所仮
			BREAK
		IF !Ｂ自慰 && 最大感覚 == ABL:奴隷:Ｂ感覚
			Ｂ自慰 = 1
			自慰箇所仮 += 1
			SETBIT CFLAG:奴隷:自慰行動保存, 3
		ENDIF
		SIF 自慰箇所 == 自慰箇所仮
			BREAK
		IF !Ｖ自慰 && 最大感覚 == (ABL:奴隷:Ｖ感覚 / (貞操帯着用中+1))
			Ｖ自慰 = 1
			自慰箇所仮 += 1
			SETBIT CFLAG:奴隷:自慰行動保存, 1
		ENDIF
		SIF 自慰箇所 == 自慰箇所仮
			BREAK
		IF !Ａ自慰 && 最大感覚 == (ABL:奴隷:Ａ感覚 / (貞操帯着用中+1))
			Ａ自慰 = 1
			自慰箇所仮 += 1
			SETBIT CFLAG:奴隷:自慰行動保存, 2
		ENDIF
		SIF 自慰箇所 == 自慰箇所仮
			BREAK
		最大感覚 -= 1
	WEND

	自慰回数 = MAX(TIME_PROGRESS(TFLAG:300) / 5, 1)

	IF Ｃ自慰
		IF !貞操帯着用中
			PALAM:奴隷:快Ｃ += 自慰中変動値_快(奴隷, ABL:奴隷:Ｃ感覚, ABL:奴隷:指, ABL:奴隷:技巧, 自慰回数)
			PALAM:奴隷:欲情 += 自慰中変動値_欲情(奴隷, ABL:奴隷:Ｃ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			PALAM:奴隷:潤滑 += 自慰中変動値_潤滑(奴隷, ABL:奴隷:Ｃ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			EXP:奴隷:Ｃ経験 += 自慰回数
			IF PALAM:奴隷:快Ｃ >= 10000
				PALAM:奴隷:快Ｃ -= (10000 * (PALAM:奴隷:快Ｃ / 10000))
				EXP:奴隷:Ｃ絶頂経験 += 1
				EXP:奴隷:絶頂経験 += 1
				CFLAG:奴隷:自慰中 = MAX(CFLAG:奴隷:自慰中 - 1, 0)
				SETBIT CFLAG:奴隷:自慰行動保存, 5
			ENDIF
		ELSE
			CFLAG:奴隷:性欲 += 自慰回数
		ENDIF
	ENDIF
	IF Ｂ自慰
		PALAM:奴隷:快Ｂ += 自慰中変動値_快(奴隷, ABL:奴隷:Ｂ感覚, ABL:奴隷:指, ABL:奴隷:技巧, 自慰回数)
		PALAM:奴隷:欲情 += 自慰中変動値_欲情(奴隷, ABL:奴隷:Ｂ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
		PALAM:奴隷:潤滑 += 自慰中変動値_潤滑(奴隷, ABL:奴隷:Ｂ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
		EXP:奴隷:Ｂ経験 += 自慰回数
		IF PALAM:奴隷:快Ｂ >= 10000
			PALAM:奴隷:快Ｂ -= (10000 * (PALAM:奴隷:快Ｂ / 10000))
			EXP:奴隷:Ｂ絶頂経験 += 1
			EXP:奴隷:絶頂経験 += 1
			CFLAG:奴隷:自慰中 = MAX(CFLAG:奴隷:自慰中 - 1, 0)
			SETBIT CFLAG:奴隷:自慰行動保存, 5
		ENDIF
	ENDIF
	IF Ｖ自慰
		IF !貞操帯着用中
			PALAM:奴隷:快Ｖ += 自慰中変動値_快(奴隷, ABL:奴隷:Ｖ感覚, ABL:奴隷:指, ABL:奴隷:技巧, 自慰回数)
			PALAM:奴隷:欲情 += 自慰中変動値_欲情(奴隷, ABL:奴隷:Ｖ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			PALAM:奴隷:潤滑 += 自慰中変動値_潤滑(奴隷, ABL:奴隷:Ｖ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			EXP:奴隷:Ｖ経験 += 自慰回数
			IF PALAM:奴隷:快Ｖ >= 10000
				PALAM:奴隷:快Ｖ -= (10000 * (PALAM:奴隷:快Ｖ / 10000))
				EXP:奴隷:Ｖ絶頂経験 += 1
				EXP:奴隷:絶頂経験 += 1
				CFLAG:奴隷:自慰中 = MAX(CFLAG:奴隷:自慰中 - 1, 0)
				SETBIT CFLAG:奴隷:自慰行動保存, 5
			ENDIF
			CALL 締り具合変動, 奴隷, 1, 0
		ELSE
			CFLAG:奴隷:性欲 += 自慰回数
		ENDIF
	ENDIF
	IF Ａ自慰
		IF !貞操帯着用中
			PALAM:奴隷:快Ａ += 自慰中変動値_快(奴隷, ABL:奴隷:Ａ感覚, ABL:奴隷:指, ABL:奴隷:技巧, 自慰回数)
			PALAM:奴隷:欲情 += 自慰中変動値_欲情(奴隷, ABL:奴隷:Ａ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			PALAM:奴隷:潤滑 += 自慰中変動値_潤滑(奴隷, ABL:奴隷:Ａ感覚, ABL:奴隷:指, ABL:奴隷:欲望, 自慰回数)
			EXP:奴隷:Ａ経験 += 自慰回数
			IF PALAM:奴隷:快Ａ >= 10000
				PALAM:奴隷:快Ａ -= (10000 * (PALAM:奴隷:快Ａ / 10000))
				EXP:奴隷:Ａ絶頂経験 += 1
				EXP:奴隷:絶頂経験 += 1
				CFLAG:奴隷:自慰中 = MAX(CFLAG:奴隷:自慰中 - 1, 0)
				SETBIT CFLAG:奴隷:自慰行動保存, 5
			ENDIF
			CALL 締り具合変動, 奴隷, 1, 1
		ELSE
			CFLAG:奴隷:性欲 += 自慰回数
		ENDIF
	ENDIF
	EXP:奴隷:自慰経験 += 1
	IF !CFLAG:奴隷:自慰中
		CFLAG:奴隷:性欲 /= 10
	ENDIF

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@自慰中変動値_快(ARG, ARG:1, ARG:2, ARG:3, ARG:4)
	#FUNCTION
	;ARG   キャラ
	;ARG:1 感覚
	;ARG:2 性技・指
	;ARG:3 技巧
	;ARG:4 経過時間による倍率
	IF ARG:1 >= 5
		LOCAL = 5000
	ELSEIF ARG:1 == 4
		LOCAL = 4700
	ELSEIF ARG:1 == 3
		LOCAL = 4400
	ELSEIF ARG:1 == 2
		LOCAL = 4100
	ELSEIF ARG:1 == 1
		LOCAL = 3800
	ELSE
		LOCAL = 3500
	ENDIF
	IF RAND:2
		LOCAL += RAND:500
	ELSE
		LOCAL -= RAND:500
	ENDIF
	IF (ARG:2 + ARG:3) >= 7
		TIMES LOCAL, 2.15
	ELSEIF (ARG:2 + ARG:3) >= 5
		TIMES LOCAL, 1.85
	ELSEIF (ARG:2 + ARG:3) >= 3
		TIMES LOCAL, 1.55
	ELSEIF (ARG:2 + ARG:3) >= 1
		TIMES LOCAL, 1.25
	ENDIF
RETURNF LOCAL * ARG:4

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@自慰中変動値_欲情(ARG, ARG:1, ARG:2, ARG:3, ARG:4)
	#FUNCTION
	;ARG   キャラ
	;ARG:1 感覚
	;ARG:2 性技・指
	;ARG:3 欲望
	;ARG:4 経過時間による倍率
	IF ARG:1 >= 5
		LOCAL = 1800
	ELSEIF ARG:1 == 4
		LOCAL = 1500
	ELSEIF ARG:1 == 3
		LOCAL = 1200
	ELSEIF ARG:1 == 2
		LOCAL = 900
	ELSEIF ARG:1 == 1
		LOCAL = 600
	ELSE
		LOCAL = 300
	ENDIF
	IF RAND:2
		LOCAL += RAND:200
	ELSE
		LOCAL -= RAND:200
	ENDIF
	IF (ARG:2 + ARG:3) >= 8
		TIMES LOCAL, 2.15
	ELSEIF (ARG:2 + ARG:3) >= 6
		TIMES LOCAL, 1.85
	ELSEIF (ARG:2 + ARG:3) >= 4
		TIMES LOCAL, 1.55
	ELSEIF (ARG:2 + ARG:3) >= 2
		TIMES LOCAL, 1.25
	ENDIF
RETURNF LOCAL * ARG:4

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@自慰中変動値_潤滑(ARG, ARG:1, ARG:2, ARG:3, ARG:4)
	#FUNCTION
	;ARG   キャラ
	;ARG:1 感覚
	;ARG:2 性技・指
	;ARG:3 欲望
	;ARG:4 経過時間による倍率
	IF ARG:1 >= 5
		LOCAL = 1800
	ELSEIF ARG:1 == 4
		LOCAL = 1500
	ELSEIF ARG:1 == 3
		LOCAL = 1200
	ELSEIF ARG:1 == 2
		LOCAL = 900
	ELSEIF ARG:1 == 1
		LOCAL = 600
	ELSE
		LOCAL = 300
	ENDIF
	IF RAND:2
		LOCAL += RAND:200
	ELSE
		LOCAL -= RAND:200
	ENDIF
	IF (ARG:2 + ARG:3) >= 8
		TIMES LOCAL, 2.15
	ELSEIF (ARG:2 + ARG:3) >= 6
		TIMES LOCAL, 1.85
	ELSEIF (ARG:2 + ARG:3) >= 4
		TIMES LOCAL, 1.55
	ELSEIF (ARG:2 + ARG:3) >= 2
		TIMES LOCAL, 1.25
	ENDIF
RETURNF LOCAL * ARG:4

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@自慰中断, ARG
	SIF CFLAG:ARG:自慰中 <= 0
		RETURN
	;誰かが来た
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		IF ARG != LOCAL && CFLAG:ARG:現在位置 == CFLAG:LOCAL:現在位置
			LOCAL:1 = 1
			GOTO GGT_33443
		ENDIF
	NEXT
	$GGT_33443
	IF (CFLAG:ARG:現在位置 == FLAG:訪問者の現在位置 || LOCAL:1 == 1)
		SIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW When you knocked %CALLNAME:LOCAL%'s door, you heard strange noises and sounds from the inside.
		CFLAG:ARG:自慰中 = -ABS(CFLAG:ARG:自慰中)
		RETURN
	ENDIF

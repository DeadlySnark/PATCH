;==============================================================================
;TARGETによるカウンター行動を実行する
;@return 1:xxx,0:xxx
@EVENT_WC_COUNTER(加害者,行動順)
	#DIM 加害者
	#DIM 行動順
	#DIM セクハラ内容_ソフト
	#DIM セクハラ内容_着衣
	#DIM セクハラ内容_脱衣愛撫
	#DIM セクハラ内容_脱衣強要
	#DIM セクハラ内容_セックス
	#DIM セクハラ成分_恥じらい
	#DIM セクハラ成分_誘惑
	#DIM セクハラ成分_積極性
	#DIM セクハラ成分_タチ
	#DIM DYNAMIC LOOP_CNT
	#DIM DYNAMIC LOOP_CHR
	#DIM セクハラ成分_愛情
	#DIM セクハラ成分_興奮
	#DIM セクハラ内容_ハード
	;----------------------------------------------------
	;セクハラしてこない場合を先にチェック
	;直前のコマンドが連れ出す(351)か移動(400)か外出(490)なら発生しない
	SELECTCASE SELECTCOM
		CASE 351,400,490
			RETURN 0
	ENDSELECT
	;あなたはセクハラしない
	SIF 加害者 <= 0
		RETURN 0
	;寝てるキャラはセクハラしない
	SIF CFLAG:加害者:睡眠
		RETURN 0
	;同じ場所に訪問者がいる場合
	IF CFLAG:加害者:現在位置 == FLAG:訪問者の現在位置
		;NTRだとカウンター抑制
		SIF TALENT:加害者:公衆便所			;;NTRた人から見ても、便器は便器。でも公衆便所なら同類系なので抑制
			RETURN 0
		;主人が空気だと抑制
		SIF IS_AIR_MASTER(加害者)
			RETURN 0
		;訪問者の方が影響が大きいとある程度抑制
		SIF CFLAG:加害者:屈服度 > CFLAG:加害者:好感度 && RAND:2
			RETURN 0
	ENDIF
	;２重実行はさせない
	TCVAR:加害者:カウンター行動決定フラグ ++
	SIF TCVAR:加害者:カウンター行動決定フラグ > 1
		RETURN 0
	;----------------------------------------------------
	;特殊な処理
	VARSET LOCAL
	IF CFLAG:加害者:うふふ == 1
		;うふふ中のリアクション(うふふ=1、うふふ)
		CALL EVENT_WC_COUNTER_REACTION(加害者,行動順)
		RETURN 0
	ELSEIF CFLAG:加害者:うふふ == 2
		;押し倒し中のリアクション(うふふ=2、押し倒され)
		;押し倒し中に満足した場合のリアクション
		IF 行動順 == 1 && CFLAG:加害者:MASTERの弱味 && BASE:加害者:満足 == MAXBASE:加害者:満足
			CALL EVENT_WC_COUNTER_PUNISHMENT(加害者,行動順)
			;全員をリセットする必要はあるのか……？
			FOR LOOP_CHR,0,CHARANUM
				CFLAG:LOOP_CHR:うふふ = 0
				CALL CLOTHES_RESET(LOOP_CHR)
				CALL CLOTHES_SETTING_TRAIN(LOOP_CHR)
			NEXT
			RETURN 0
		ENDIF
	ENDIF
	;----------------------------------------------------
	;セクハラ内容の選択
	セクハラ内容_ソフト = 0
	セクハラ内容_ハード = 0
	セクハラ内容_着衣 = 0
	セクハラ内容_脱衣愛撫 = 0
	セクハラ内容_脱衣強要 = 0
	セクハラ内容_セックス = 0
	セクハラ成分_愛情 = 0
	セクハラ成分_興奮 = 0
	SELECTCASE TFLAG:COMABLE管理
		CASE 1
			;日常
			IF TALENT:加害者:管理人								;;加害者が管理人での、加点
				セクハラ内容_ソフト += 50
				セクハラ内容_ハード += 50
				セクハラ内容_着衣 += 50
			ENDIF
			IF TCVAR:馴れ合い強度 >= 3							;;馴れ合い強度での、加点
				セクハラ内容_ソフト += 10
				セクハラ内容_ハード += 25
				セクハラ内容_着衣 += 15
			ELSEIF TCVAR:馴れ合い強度 >= 2
				セクハラ内容_ソフト += 15
				セクハラ内容_ハード += 20
				セクハラ内容_着衣 += 10
			ELSEIF TCVAR:馴れ合い強度 >= 1
				セクハラ内容_ソフト += 20
				セクハラ内容_ハード += 15
				セクハラ内容_着衣 += 5
			ELSE
				セクハラ内容_ソフト += 25
				セクハラ内容_ハード += 10
			ENDIF
			SELECTCASE BASE:加害者:ムード						;;ムードが良ければ、加点
				CASE IS > 1000
					セクハラ内容_ハード += 10
					セクハラ内容_着衣 += 15
				CASE IS > 500
					セクハラ内容_ハード += 15
					セクハラ内容_着衣 += 10
			ENDSELECT
			SELECTCASE BASE:加害者:理性							;;理性が低ければ、加点
				CASE IS <= 0
					セクハラ内容_ハード += 10
					セクハラ内容_着衣 += 15
				CASE IS <= 500
					セクハラ内容_ハード += 15
					セクハラ内容_着衣 += 10
			ENDSELECT
			IF TALENT:MASTER:NTR								;;あなたがＮＴＲた場合、減点
				セクハラ内容_ソフト -= 20
				セクハラ内容_ハード -= 20
				セクハラ内容_着衣 -= 20
			ENDIF
			IF TALENT:MASTER:公衆便所							;;あなたが公衆便所の場合、減点
				セクハラ内容_ソフト -= 30
				セクハラ内容_ハード -= 30
				セクハラ内容_着衣 -= 30
			ENDIF
			;;一応、マイナスだったら０にしておく
			SIF セクハラ内容_ソフト < 0
				セクハラ内容_ソフト = 0
			SIF セクハラ内容_着衣 < 0
				セクハラ内容_着衣 = 0
		CASE 2
			;うふふ中
			;脱衣により着衣セクハラ率が決定
			SELECTCASE TEQUIP:MASTER:上半身着衣状況
				CASE IS < 1
					セクハラ内容_着衣 = 80
				CASE IS < 2
					セクハラ内容_着衣 = 90
				CASEELSE
					セクハラ内容_着衣 = 100
			ENDSELECT
			セクハラ内容_脱衣愛撫 = 80
			;ムードが良ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:ムード
				CASE IS > 1000
					セクハラ内容_脱衣愛撫 += 15
				CASE IS > 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
			;理性が低ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:理性
				CASE IS <= 0
					セクハラ内容_脱衣愛撫 += 15
				CASE IS <= 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
		CASE 3
			;押し倒し中
			;脱衣により着衣セクハラ率が決定
			SELECTCASE TEQUIP:MASTER:上半身着衣状況
				CASE IS < 1
					セクハラ内容_着衣 = 80
				CASE IS < 2
					セクハラ内容_着衣 = 90
				CASEELSE
					セクハラ内容_着衣 = 100
			ENDSELECT
			セクハラ内容_脱衣愛撫 = 80
			;ムードが良ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:ムード
				CASE IS > 1000
					セクハラ内容_脱衣愛撫 += 15
				CASE IS > 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
			;理性が低ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:理性
				CASE IS <= 0
					セクハラ内容_脱衣愛撫 += 15
				CASE IS <= 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
		CASEELSE
	ENDSELECT
	セクハラ内容_脱衣強要 = セクハラ内容_脱衣愛撫
	セクハラ内容_セックス = セクハラ内容_脱衣愛撫
	セクハラ成分_恥じらい = TALENT:加害者:目立ちたがり + TALENT:加害者:自己愛 - TALENT:加害者:自制心 - TALENT:加害者:貞操 - 2 * TALENT:加害者:羞恥心
	セクハラ成分_誘惑 = TALENT:加害者:魅力 + TALENT:加害者:魅惑 + TALENT:加害者:謎の魅力 + TALENT:加害者:バストサイズ + TALENT:加害者:体型 - TALENT:加害者:貞操 + TALENT:加害者:小悪魔 - TALENT:加害者:幼稚
	セクハラ成分_積極性 = TALENT:加害者:度胸 + TALENT:加害者:陽気／陰気 - TALENT:加害者:無関心 - TALENT:加害者:感情乏しい + TALENT:加害者:性的興味 - TALENT:加害者:一線越えない + TALENT:加害者:献身的
	セクハラ成分_タチ = TALENT:加害者:応答 + TALENT:加害者:プライド + TALENT:加害者:献身的 + 2 * (TALENT:加害者:サド - TALENT:加害者:マゾ)
	;50〜69 脱衣セクハラ(愛撫)
	SIF セクハラ内容_脱衣愛撫
		セクハラ内容_脱衣愛撫 += 2 * (セクハラ成分_タチ + セクハラ成分_積極性)
	;70〜79 脱衣セクハラ(強要)
	SIF セクハラ内容_脱衣強要
		セクハラ内容_脱衣強要 += 2 * (セクハラ成分_誘惑 - セクハラ成分_積極性)
	;80〜99 性交
	SIF セクハラ内容_セックス
		セクハラ内容_セックス += 2 * (セクハラ成分_タチ + セクハラ成分_積極性)

	セクハラ成分_興奮 = GETPALAMLV(PALAM:加害者:欲情,10) + TALENT:加害者:目立ちたがり + TALENT:加害者:性的興味 - TALENT:加害者:一線越えない - TALENT:加害者:自制心 - TALENT:加害者:羞恥心

	IF CFLAG:加害者:好感度 < 100
		セクハラ成分_愛情 = 0
	ELSEIF CFLAG:加害者:好感度 < 300
		セクハラ成分_愛情 += 1
	ELSEIF CFLAG:加害者:好感度 < 600
		セクハラ成分_愛情 += 2
	ELSEIF CFLAG:加害者:好感度 < 900
		セクハラ成分_愛情 += 3
	ELSEIF CFLAG:加害者:好感度 < 1500
		セクハラ成分_愛情 += 4
	ELSEIF CFLAG:加害者:好感度 < 2400
		セクハラ成分_愛情 += 5
	ELSEIF CFLAG:加害者:好感度 < 3900
		セクハラ成分_愛情 += 6
	ELSEIF CFLAG:加害者:好感度 < 6300
		セクハラ成分_愛情 += 7
	ELSEIF CFLAG:加害者:好感度 < 10200
		セクハラ成分_愛情 += 8
	ELSEIF CFLAG:加害者:好感度 < 16500
		セクハラ成分_愛情 += 9
	ELSE
		セクハラ成分_愛情 += 10
	ENDIF

	;------------------------------------------
	;10〜19 ソフト
	;------------------------------------------
	LOCAL:CNT_色っぽい仕草 = 10 + セクハラ成分_誘惑 - セクハラ成分_恥じらい
	LOCAL:CNT_見つめる = 10 + セクハラ成分_恥じらい
	LOCAL:CNT_体を摺り寄せる = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	LOCAL:CNT_スカートを捲る = 10 + セクハラ成分_積極性 + セクハラ成分_タチ - セクハラ成分_恥じらい
	LOCAL:CNT_耳元に息を吹きかける = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	LOCAL:CNT_尻を撫でる = 10 + セクハラ成分_積極性 + セクハラ成分_タチ

;;LOCAL:15までが、元々あるソフト
	LOCAL:CNT_囁く = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性

	FOR LOOP_CNT,10,20
		LOCAL:LOOP_CNT *= セクハラ内容_ソフト
	NEXT
	;------------------------------------------
	;20〜29 ハード／若干底上げ
	;------------------------------------------
	LOCAL:CNT_お尻いじめ = 10 + セクハラ成分_積極性 + セクハラ成分_タチ
	LOCAL:CNT_パンツを脱がす = 10 + セクハラ成分_誘惑 + セクハラ成分_タチ + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_パンツ選択 = 10 + セクハラ成分_誘惑 + セクハラ成分_タチ + ABL:加害者:親密 + ABL:加害者:欲望													
	LOCAL:CNT_アイテム装着される = 10 + セクハラ成分_タチ + ABL:加害者:欲望 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_スカートたくし上げ命令 = 10 + セクハラ成分_タチ + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_貞操帯関連 = 10 + ABL:加害者:親密 + ABL:加害者:欲望 + TALENT:加害者:サド + ABL:加害者:サドっ気 + セクハラ成分_愛情
	LOCAL:CNT_飲尿命令 = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性 + セクハラ成分_タチ + ABL:加害者:親密 - セクハラ成分_恥じらい + REST_GetUrge(加害者,1)	;;おしっこしたいほど確率増大

	;乳搾り手コキ(男性のみ、相手の浮気を公認してるか、相手にサドっ気3LV以上が必要、レベルの高さで発生率上昇)
	;ふたなり非対応なのは、前立腺の事を考えるとなんだか不自然っぽい感じがしたからです。わかりますね？
	IF TALENT:MASTER:性別 == 2
		;搾り発生率は２段階判定。
		;まずはサドっ気が2LVを超える毎に5%、浮気公認は１段階毎に10%（累積しない）
		IF ( MAX(TALENT:加害者:浮気公認*2,(ABL:加害者:サドっ気-2) ) > RAND:10)
			;指、器用な指、サド、サドっ気、精力などから算出。
			LOCAL:CNT_乳搾り手コキ = MIN(BASE:MASTER:精力, (ABL:加害者:指+5)*(TALENT:加害者:器用な指+1)+(TALENT:加害者:サド+1)*MIN(ABL:加害者:サドっ気,5)+BASE:MASTER:精力/50)
		ENDIF
		;ただし勃起しない時はしてこない
		IF MAXBASE:MASTER:勃起 < 300	; ED状態
			LOCAL:CNT_乳搾り手コキ = 0
		ENDIF
	ENDIF
	FOR LOOP_CNT,20,30
		LOCAL:LOOP_CNT *= セクハラ内容_ハード
	NEXT
	;------------------------------------------
	;30〜49 着衣セクハラ
	;------------------------------------------
	LOCAL:CNT_キス = 10 + セクハラ成分_積極性
	LOCAL:CNT_背面抱擁 = 10 + セクハラ成分_恥じらい - セクハラ成分_積極性
	LOCAL:CNT_対面抱擁 = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	LOCAL:CNT_胸を揉ませる = 10 + セクハラ成分_誘惑 - セクハラ成分_積極性
	LOCAL:CNT_股間をまさぐる = 10 + セクハラ成分_積極性 + セクハラ成分_タチ
	LOCAL:CNT_スカートの中を見せる = 10 + セクハラ成分_恥じらい + セクハラ成分_誘惑
	LOCAL:CNT_胸を揉む = 10 + セクハラ成分_積極性 + セクハラ成分_タチ

	LOCAL:CNT_処女献上 = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + セクハラ成分_愛情
	LOCAL:CNT_強制絶頂・Ｐ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_強制絶頂・Ｖ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_強制絶頂・Ａ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_強制絶頂・Ｃ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_強制絶頂・Ｂ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + TALENT:加害者:サド + ABL:加害者:サドっ気
	LOCAL:CNT_性欲処理・Ｖ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + セクハラ成分_愛情 + BASE:加害者:勃起 / 100
	LOCAL:CNT_性欲処理・Ａ = セクハラ成分_積極性 + セクハラ成分_タチ + セクハラ成分_興奮 + セクハラ成分_愛情 + BASE:加害者:勃起 / 100

	FOR LOOP_CNT,30,50
		LOCAL:LOOP_CNT *= セクハラ内容_着衣
	NEXT
	;------------------------------------------
	;50〜69 脱衣セクハラ(愛撫)
	;------------------------------------------
	LOCAL:CNT_手淫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指 + BASE:MASTER:勃起 / 500
	LOCAL:CNT_フェラチオ = 10 + TALENT:加害者:汚臭耐性 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + BASE:MASTER:勃起 / 500
	LOCAL:CNT_素股 = 10 + ABL:加害者:腰 + 2 * TALENT:加害者:濡れやすさ + BASE:MASTER:勃起 / 500
	LOCAL:CNT_パイズリ = 10 + TALENT:加害者:バストサイズ + 2 * TALENT:加害者:淫乳 + ABL:加害者:胸 + BASE:MASTER:勃起 / 500
	LOCAL:CNT_足コキ = 10 + 2 * TALENT:加害者:サド + MIN(ABL:加害者:サドっ気 , 5) + BASE:MASTER:勃起 / 500
	LOCAL:CNT_アナル愛撫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	LOCAL:CNT_アナル舐め = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + MIN(TALENT:加害者:汚臭耐性, 0)
	LOCAL:CNT_指挿入 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	LOCAL:CNT_胸愛撫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	LOCAL:CNT_乳首吸い = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + 2 * TALENT:加害者:幼稚
	LOCAL:CNT_ディープキス = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌

	FOR LOOP_CNT,50,70
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_脱衣愛撫 * (90 + RAND:21) / 100
	NEXT
	;------------------------------------------
	;70〜79 脱衣セクハラ(強要)
	;------------------------------------------
	LOCAL:CNT_クンニ強制 = 10 + ABL:加害者:Ｃ感覚
	LOCAL:CNT_フェラチオ強制 = 10 + ABL:加害者:Ｃ感覚
	LOCAL:CNT_アナル奉仕 = 10 + ABL:加害者:Ａ感覚
	LOCAL:CNT_ぱふぱふ = 10 + ABL:加害者:Ｂ感覚
	LOCAL:CNT_乳合わせ = 10 + ABL:加害者:Ｂ感覚
	LOCAL:CNT_タワシ洗い = 10 + ABL:加害者:Ｃ感覚

	FOR LOOP_CNT,70,80
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_脱衣強要 * (90 + RAND:21) / 100
	NEXT
	;------------------------------------------
	;80〜99 性交
	;------------------------------------------
	セクハラ内容_セックス = 0
	LOCAL:CNT_正常位挿入される = 10 + ABL:加害者:Ｃ感覚
	LOCAL:CNT_騎乗位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	LOCAL:CNT_後背位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	LOCAL:CNT_背面座位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	FOR LOOP_CNT,80,99
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_セックス * (90 + RAND:21) / 100
	NEXT
	;------------------------------------------
	;ランダマイズ
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT = RAND:(MAX(LOCAL:LOOP_CNT,1))
	NEXT
	;実行判定
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT *= WC_ACTABLE(加害者,LOOP_CNT)
	NEXT

	;------------------------------------------
	;実行判定
	;------------------------------------------
	LOCAL:300 = 0
	IF セクハラ内容_ソフト > セクハラ内容_ハード
		FOR LOOP_CNT,1,100
			IF LOCAL:LOOP_CNT > LOCAL:300
				LOCAL:300 = LOCAL:LOOP_CNT
				セクハラ内容_ソフト = LOOP_CNT
			ENDIF
		NEXT
		SIF LOCAL:300 > 0
			TCVAR:加害者:20 = セクハラ内容_ソフト
	ELSE
		FOR LOOP_CNT,1,100
			IF LOCAL:LOOP_CNT > LOCAL:300
				LOCAL:300 = LOCAL:LOOP_CNT
				セクハラ内容_ハード = LOOP_CNT
			ENDIF
		NEXT
		SIF LOCAL:300 > 0
			TCVAR:加害者:20 = セクハラ内容_ハード
	ENDIF

	;------------------------------------------
	;実行判定
	;------------------------------------------
	IF TCVAR:加害者:20 != 24 && TCVAR:加害者:20 != 25
		SIF (CFLAG:MASTER:WC_道具Ｂ装着者 == 加害者 && CFLAG:MASTER:WC_ニプルキャップ装着 == 1) || (CFLAG:MASTER:WC_道具Ｃ装着者 == 加害者 && CFLAG:MASTER:WC_クリキャップ装着 == 1) || (CFLAG:MASTER:WC_道具Ｖ装着者 == 加害者 && CFLAG:MASTER:WC_バイブ装着 == 1) || (CFLAG:MASTER:WC_道具Ａ装着者 == 加害者 && CFLAG:MASTER:WC_アナルバイブ装着 == 1) || (CFLAG:MASTER:WC_道具Ｃ装着者 == 加害者 && CFLAG:MASTER:WC_ローターＣ装着 == 1) || (CFLAG:MASTER:WC_道具Ｖ装着者 == 加害者 && CFLAG:MASTER:WC_ローターＶ装着 == 1) || (CFLAG:MASTER:WC_道具Ａ装着者 == 加害者 && CFLAG:MASTER:WC_ローターＡ装着 == 1)
			TCVAR:加害者:20 = 24
	ENDIF

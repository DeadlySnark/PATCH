;-------------------------------------------------
;対面座位アナル
;セックス系コマンド、レベル3
;-------------------------------------------------
@COM69

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_左尻)
SIF RESULT
	RETURN 0

;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 2
;強度
TCVAR:PLAYER:4 = 1700

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 70
DOWNBASE:気力 += 150

SOURCE:快Ｃ = 250
SOURCE:快Ａ = 350
SOURCE:情愛 = 350
SOURCE:苦痛 = 800
SOURCE:恭順 = 300
SOURCE:逸脱 = 800
SOURCE:反感 = 1200

;上のほうで計算した汚れデータ
SOURCE:不潔 = GET_STAINCOUNT(PLAYER, 部位_Ｐ, TARGET) * 10 + 60

CALL 締り具合変動, TARGET, 3, 1
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 900

;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:Ａ
STAIN:PLAYER:Ｐ |= STAIN:腸内
STAIN:Ａ |= STAIN:PLAYER:Ｐ
STAIN:腸内 |= STAIN:PLAYER:Ｐ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 ++
EXP:Ａ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF


;愛情経験
SIF !ASSIPLAY
EXP:愛情経験 += TALENT:恋慕

RETURN 1

@CAN_COM69(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	SKIPDISP ARG
	PRINTL Anal Lotus
	TFLAG:実行値 = 0
	TFLAG:実行値表示用フラグ = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += ABL:欲望 * 3
		PRINT Lust
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += ABL:Ａ感覚 * 2
		PRINT A Sense
		PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += MARK:快楽刻印 * 3
		PRINT Pleasure Mark
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		TFLAG:実行値 -= 5
		PRINT Not wet enough
		PRINTV '(,5,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT Desire
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		TFLAG:実行値 -= 2
		PRINT Shy
		PRINTV '(,2,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;快感の否定
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		TFLAG:実行値 -= 5
		PRINT Denies Pleasure
		PRINTV '(,5,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:性別嗜好 & 1 && !(HAS_VAGINA(PLAYER)) && (!TALENT:恋慕 || PLAYER != MASTER)
		PRINT  - 
		TFLAG:実行値 -= 12
		PRINT Man Hater
		PRINTV '(,12,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;淫乱
	IF TALENT:淫乱 && ASSIPLAY == 0
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += 15
		PRINT Lewd
		PRINTV '(,15,')
		TFLAG:実行値表示用フラグ = 15
	ENDIF
	;娼婦
	IF TALENT:淫乱 > 1 && ASSIPLAY == 0
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += 5
		PRINT Whore
		PRINTV '(,5,')
		TFLAG:実行値表示用フラグ = 5
	ENDIF
	;A経験
	IF EXP:Ａ経験 < EXPLV:2
		PRINT  - 
		TFLAG:実行値 -= 30
		PRINT Lack of 
		PRINT A Exp
		PRINTV '(,30,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF

	;調教者がふたなり
	IF GET_SEXNAME(PLAYER) == "ふたなり"
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += 4
		PRINT Hermaphrodite
		PRINTV '(,4,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += 6
		PRINT Aphrodisiac
		PRINTV '(,6,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF

	LOCAL = GET_STAINCOUNT(PLAYER, 部位_Ｐ, TARGET)
	;汚れあり
	IF LOCAL
		PRINT  - 
		TFLAG:実行値 -= LOCAL
		PRINT Dirty
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT , Resistant To Smell
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT , Smell Sensitive
		ENDIF
		PRINTV '(,LOCAL,')
		TFLAG:実行値表示用フラグ = 1
	ENDIF
	;鍵を持っていると実行可能
	IF FLAG:貞操帯鍵購入フラグ == TARGET
		SIF TFLAG:実行値表示用フラグ
			PRINT  + 
		TFLAG:実行値 += 100
		PRINT Chastity Belt Key
		PRINTV '(,100,')
		TFLAG:実行値表示用フラグ = 100
	ENDIF	

	;合計を表示(50以上で実行)
	PRINT  = 
	PRINTV TFLAG:実行値

	LOCAL = 50
	SIF TFLAG:実行値 < LOCAL
		PRINT  < 
	SIF TFLAG:実行値 == LOCAL
		PRINT  = 
	SIF TFLAG:実行値 > LOCAL
		PRINT  > 
	PRINT Required Value 
	PRINTV LOCAL

	WAIT
	SKIPDISP 0
	;実行できない
	SIF TFLAG:実行値 < LOCAL
		RETURN 0
ELSE
	SKIPDISP ARG
	PRINTL Restrained Anal Lotus
ENDIF
RETURN 1


;;-------------------------------------------------------------------------------
;;「@SOJI_SexHara」をベースにさせていただき、ちょこちょこと手を加えました。
@WC_SexHara
#DIM DYNAMIC LOOP_CNT
#DIM セクハラ内容
#DIM パンツ種類
#DIM オトコペナ
#DIM 服装_接触ボーナス
#DIM 服装_接触ペナ
#DIM 服装_観賞ボーナス
#DIM 服装_観賞ペナ
	セクハラ内容 = 0
	パンツ種類 = 0
	オトコペナ = 0
	服装_接触ボーナス = 0
	服装_接触ペナ = 0
	服装_観賞ボーナス = 0
	服装_観賞ペナ = 0
;;------------------------------------------
;;そもそも発生するかどうか判定
	;;------------------------------------------
	;;屈服レイプイベントに入ったら、強制継続
	SIF CFLAG:MASTER:WC_屈服レイプ継続中
		RETURN
	;;------------------------------------------
	;;人里プレイを開始してたら、継続判定
	SIF CFLAG:MASTER:WC_人里プレイ開始 > 0
		RETURN
	;;------------------------------------------
	IF CFLAG:MASTER:WC_訪問者の今回セクハラ > 0 && CFLAG:MASTER:WC_訪問者の前回セクハラ != CFLAG:MASTER:WC_訪問者の今回セクハラ
		CFLAG:MASTER:WC_訪問者の前々回セクハラ = CFLAG:MASTER:WC_訪問者の前回セクハラ
		CFLAG:MASTER:WC_訪問者の前回セクハラ = CFLAG:MASTER:WC_訪問者の今回セクハラ
	ENDIF
	CFLAG:MASTER:WC_訪問者の今回セクハラ = 0

	;;同じ場所にいないなら、当然セクハラしない
	SIF CFLAG:MASTER:現在位置 != FLAG:訪問者の現在位置
		RETURN 0
;;------------------------------------------
;;直前のコマンドが連れ出す(351)か移動(400)か覗く(404)か訪問者を誘う(463)か訪問者を案内する(464)か訪問者と別行動する(465)なら発生しない
	SELECTCASE SELECTCOM
		CASE 351,400,404,463,464,465
			RETURN 0
	ENDSELECT
	;;------------------------------------------
	;;うふふ中と押し倒し中は、とりあえず発生させない
	SELECTCASE TFLAG:COMABLE管理
		CASE 2,3
			RETURN 0
	ENDSELECT
	;;------------------------------------------
;;同席している女の子
CFLAG:MASTER:WC_同席者 = 0
	;;------------------------------------------
	FOR LOCAL:1,1,CHARANUM
		IF CFLAG:(LOCAL:1):現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:(LOCAL:1):睡眠
			SIF LOCAL:1 == 1
				SETBIT CFLAG:MASTER:WC_同席者,1
			SIF LOCAL:1 == 2
				SETBIT CFLAG:MASTER:WC_同席者,2
			SIF LOCAL:1 == 3
				SETBIT CFLAG:MASTER:WC_同席者,3
			SIF LOCAL:1 == 4
				SETBIT CFLAG:MASTER:WC_同席者,4
			SIF LOCAL:1 == 5
				SETBIT CFLAG:MASTER:WC_同席者,5
			SIF LOCAL:1 == 6
				SETBIT CFLAG:MASTER:WC_同席者,6
			SIF LOCAL:1 == 7
				SETBIT CFLAG:MASTER:WC_同席者,7
			SIF LOCAL:1 == 8
				SETBIT CFLAG:MASTER:WC_同席者,8
			SIF LOCAL:1 == 9
				SETBIT CFLAG:MASTER:WC_同席者,9
			SIF LOCAL:1 == 10
				SETBIT CFLAG:MASTER:WC_同席者,10
		ENDIF
	NEXT
	SIF GETBIT(CFLAG:MASTER:WC_同席者,1)	;;美鈴が同席
		CALL WC_DescriptiveStyle_K1
	SIF GETBIT(CFLAG:MASTER:WC_同席者,2)	;;小悪魔が同席
		CALL WC_DescriptiveStyle_K2
	SIF GETBIT(CFLAG:MASTER:WC_同席者,3)	;;パチュリーが同席
		CALL WC_DescriptiveStyle_K3
	SIF GETBIT(CFLAG:MASTER:WC_同席者,4)	;;咲夜が同席
		CALL WC_DescriptiveStyle_K4
	SIF GETBIT(CFLAG:MASTER:WC_同席者,5)	;;レミリアが同席
		CALL WC_DescriptiveStyle_K5
	SIF GETBIT(CFLAG:MASTER:WC_同席者,6)	;;フランが同席
		CALL WC_DescriptiveStyle_K6
	SIF GETBIT(CFLAG:MASTER:WC_同席者,7)	;;子悪魔が同席
		CALL WC_DescriptiveStyle_K7
	SIF GETBIT(CFLAG:MASTER:WC_同席者,8)	;;チルノが同席
		CALL WC_DescriptiveStyle_K8
	SIF GETBIT(CFLAG:MASTER:WC_同席者,9)	;;大妖精が同席
		CALL WC_DescriptiveStyle_K9
	SIF GETBIT(CFLAG:MASTER:WC_同席者,10)	;;魔理沙が同席
		CALL WC_DescriptiveStyle_K10
	;;------------------------------------------
;;エロから遠いパンツほど、脱がそうとする
	;;------------------------------------------
	IF EQUIP:MASTER:下半身下着１ && !EQUIP:MASTER:下半身下着２
		パンツ種類 = 0
	ELSE
		SELECTCASE EQUIP:MASTER:下半身下着２
			CASE 0,2,100				;;ノーパン／ばんそうこう／貞操帯
				パンツ種類 = 0
			CASE 3,4,5					;;ドロワーズ
				パンツ種類 = 15
			CASE 6,7					;;くまさんパンツ／ねこさんパンツ
				パンツ種類 = 10
			CASE 8,9					;;水玉ぱんつ
				パンツ種類 = 10
			CASE 10 TO 15				;;しましまぱんつ
				パンツ種類 = 10
			CASE 16,17,18				;;フリルぱんつ
				パンツ種類 = 10
			CASE 19,20,21				;;ローレグパンツ
				パンツ種類 = 15
			CASE 22						;;ローライズパンツ
				パンツ種類 = 5
			CASE 23						;;ハイレグパンツ
				パンツ種類 = 5
			CASE 24,25,26				;;レースのパンティ
				パンツ種類 = 10
			CASE 27,28					;;スパッツ
				パンツ種類 = 20
			CASE 29,30,45,46,47			;;シースルーショーツ
				パンツ種類 = 3
			CASE 31,32,33				;;Tバック
				パンツ種類 = 5
			CASE 34 TO 39				;;紐パン
				パンツ種類 = 5
			CASE 40,41					;;レザーパンツ
				パンツ種類 = 5
			CASE 42,43,44				;;オープンレースTバック
				パンツ種類 = 3
			CASE 48,49,50				;;小窓付きのレースショーツ
				パンツ種類 = 3
			CASE 101,102,103			;;男物
				パンツ種類 = 30
			CASEELSE					;;（上記以外は無いはず）
				パンツ種類 = 10
		ENDSELECT
	ENDIF
	;;------------------------------------------
;;服装関係
	;;------------------------------------------
	IF !EQUIP:MASTER:0		;;全裸
		服装_接触ボーナス = 15
		服装_観賞ボーナス = 15
		服装_接触ペナ = 0
		服装_観賞ペナ = 0
	ELSE
		IF !TEQUIP:MASTER:下半身着衣状況
			服装_接触ボーナス = 7
			服装_観賞ボーナス = 7
			服装_接触ペナ = 0
			服装_観賞ペナ = 0
		ELSE
			IF !EQUIP:MASTER:下半身上着１ && !EQUIP:MASTER:下半身上着２ && !EQUIP:MASTER:スカート
				服装_接触ボーナス = 5
				服装_観賞ボーナス = 5
				服装_接触ペナ = 0
				服装_観賞ペナ = 0
			ELSE
				SELECTCASE EQUIP:MASTER:下半身上着１
					CASE 1	;;ズボン
						服装_接触ペナ += 2
						服装_観賞ペナ += 2
					CASE 2	;;スパッツ
						服装_接触ボーナス += 1
					CASE 3	;;袴
						服装_接触ペナ += 2
						服装_観賞ペナ += 2
				ENDSELECT
				SELECTCASE EQUIP:MASTER:下半身上着２
					CASE 1	;;半ズボン
						服装_接触ペナ += 1
						服装_観賞ペナ += 1
					CASE 2	;;ブルマ
						服装_接触ボーナス += 1
						服装_観賞ボーナス += 1
				ENDSELECT
				SELECTCASE EQUIP:MASTER:スカート
					CASE 1	;;ミニスカート
						服装_接触ボーナス += 1
						服装_観賞ボーナス += 1
					CASE 3	;;ロングスカート
						服装_接触ペナ += 1
					CASE 4	;;タイトスカート
						服装_観賞ボーナス += 1
					CASE 5	;;裳
						服装_接触ペナ += 1
						服装_観賞ペナ += 1
				ENDSELECT
			ENDIF
			IF EQUIP:MASTER:下半身下着１
				SELECTCASE EQUIP:MASTER:下半身下着１
					CASE 2				;;ばんそうこう
						服装_接触ボーナス += 1
						服装_観賞ボーナス += 1
					CASE 100			;;貞操帯
						服装_接触ペナ += 1
				ENDSELECT
			ELSE
				SELECTCASE EQUIP:MASTER:下半身下着２
					CASE 0						;;ノーパン
						服装_接触ボーナス += 2
						服装_観賞ボーナス += 2
					CASE 2,42,43,44,48,49,50	;;ばんそうこう／オープンレースTバック／小窓付きのレースショーツ
						服装_接触ボーナス += 1
						服装_観賞ボーナス += 1
					CASE 3,4,5					;;ドロワーズ
						服装_接触ペナ += 1
						服装_観賞ペナ += 1
					CASE 6,7, 16 TO 21			;くまさんパンツ／ねこさんパンツ／フリルぱんつ／ローレグパンツ
						服装_観賞ペナ += 1
					CASE 22,23, 34 TO 39		;;ローライズパンツ／ハイレグパンツ／紐パン
						服装_観賞ボーナス += 1
					CASE 29,30,45,46,47			;;シースルーショーツ
						服装_観賞ボーナス += 2
					CASE 31,32,33				;;Tバック
						服装_接触ボーナス += 1
					CASE 100					;;貞操帯
						服装_接触ペナ += 1
					CASE 101,102,103			;;男物
						服装_接触ペナ += 2
						服装_観賞ペナ += 2
				ENDSELECT
			ENDIF
		ENDIF
		IF !TEQUIP:MASTER:上半身着衣状況
			服装_接触ボーナス = 7
			服装_観賞ボーナス = 7
			服装_接触ペナ = 0
			服装_観賞ペナ = 0
		ELSE
			IF !EQUIP:MASTER:上半身上着１ && !EQUIP:MASTER:上半身上着２
				服装_接触ボーナス = 5
				服装_観賞ボーナス = 5
				服装_接触ペナ = 0
				服装_観賞ペナ = 0
			ELSE
				IF EQUIP:MASTER:上半身上着１	;;上着
					服装_接触ペナ += 2
					服装_観賞ペナ += 2
				ENDIF
				SELECTCASE EQUIP:MASTER:上半身上着２
					CASE 2	;;セーラー服
						服装_観賞ボーナス += 1
					CASE 3	;;ブレザー
						服装_接触ペナ += 1
						服装_観賞ペナ += 1
					CASE 5	;;学ラン
						服装_接触ペナ += 2
						服装_観賞ペナ += 2
					CASE 6	;;白装束
						服装_接触ペナ += 1
					CASE 7	;;パジャマ
						服装_接触ボーナス += 1
					CASE 8	;;Ｔシャツ
						服装_接触ボーナス += 1
					CASE 9	;;タンクトップ
						服装_接触ボーナス += 1
						服装_観賞ボーナス += 1
				ENDSELECT
			ENDIF
			SELECTCASE EQUIP:MASTER:上半身下着２
				CASE 0	;;なし
					服装_接触ボーナス += 2
					服装_観賞ボーナス += 2
				CASE 4	;;トップレスブラ
					服装_接触ボーナス += 2
					服装_観賞ボーナス += 2
				CASE 5	;;サラシ
					服装_接触ペナ += 1
			ENDSELECT
		ENDIF
		SELECTCASE EQUIP:MASTER:ボディースーツ
			CASE 1	;;ボディースーツ
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 2
		ENDSELECT
		SELECTCASE EQUIP:MASTER:ワンピース
			CASE 3	;;ネグリジェ
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
			CASE 4	;;ベビードール
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
		ENDSELECT
		SELECTCASE EQUIP:MASTER:着物
			CASE 2	;;寝巻
				服装_接触ボーナス += 1
		ENDSELECT
		SELECTCASE EQUIP:MASTER:レオタード
			CASE 1	;;スクール水着
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
			CASE 2	;;レオタード
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
		ENDSELECT
		SELECTCASE EQUIP:MASTER:靴下
			CASE 0	;;なし
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
			CASE 4	;;ストッキング
				服装_接触ボーナス += 1
				服装_観賞ボーナス += 1
		ENDSELECT
	ENDIF
	;;------------------------------------------
;;オトコだったらペナルティ
	;;------------------------------------------
	;;ホモっ気でペナ増減
	;;ゲイ中毒でペナ増減
	;;体型でペナ増減
	;;ズボン系履いてるとペナ増加
	;;　　ただし、半ズボンはペナ緩め
	;;　　スパッツとブルマはペナ無し
	;;スカートだとペナ減少
	;;ワンピース（チャイナ服は含まず）だとペナ減少
	;;セーラー服だとペナ減少
	;;女物下着だとペナ減少
	;;エロ下着だとさらにペナ減少
	;;男物下着だと増加
	IF !HAS_VAGINA(MASTER)
		オトコペナ += 5
		SELECTCASE EQUIP:MASTER:下半身上着１
			CASE 2	;;スパッツ
				;;変化無し
			CASEELSE	;;ズボン／袴
				オトコペナ += 5
		ENDSELECT
		SELECTCASE EQUIP:MASTER:下半身上着２
			CASE 1	;;半ズボン
				オトコペナ += 2
			CASE 2	;;ブルマ
				;;変化無し
		ENDSELECT
		SELECTCASE EQUIP:MASTER:下半身下着２
			CASE 3,4,5								;;ドロワーズ
				オトコペナ -= 1
			CASE 6 TO 15 , 24,25,26					;;くまさんパンツ／ねこさんパンツ／水玉ぱんつ／しましまぱんつ／レースのパンティ
				オトコペナ -= 3
			CASE 16 TO 21 , 40,41					;;フリルぱんつ／ローレグパンツ／レザーパンツ
				オトコペナ -= 2
			CASE 22,23 , 29 TO 39 , 42 TO 50		;;ローライズパンツ／ハイレグパンツ／シースルーショーツ／Tバック／紐パン／オープンレースTバック／小窓付きのレースショーツ
				オトコペナ -= 4
			CASE 101,102,103						;;男物
				オトコペナ += 5
			CASEELSE								;;ノーパン／ばんそうこう／スパッツ／貞操帯
				;;変化無し
		ENDSELECT
		SELECTCASE EQUIP:MASTER:スカート
			CASE 1	;;ミニスカート
				オトコペナ -= 5
			CASE 2	;;スカート
				オトコペナ -= 3
			CASE 3	;;ロングスカート
				オトコペナ -= 2
			CASE 4	;;タイトスカート
				オトコペナ -= 5
			CASE 5	;;裳
				オトコペナ -= 1
		ENDSELECT
		;;セーラー服
		SIF EQUIP:MASTER:上半身上着２ == 2
			オトコペナ -= 1
		;;ワンピース
		SIF EQUIP:MASTER:ワンピース == 1
			オトコペナ -= 3
		オトコペナ += TALENT:MASTER:体型
		オトコペナ -= ABL:MASTER:ホモっ気 + ABL:MASTER:ゲイ中毒
		SIF オトコペナ < 0
			オトコペナ = 0
	ENDIF
	;;------------------------------------------
;;セクハラの内容決定
	;;------------------------------------------
	;;分類・ライト
	LOCAL:11 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:従順								;;触られる
	LOCAL:12 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:露出癖								;;ぱんつ見られ
	LOCAL:13 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:舌									;;飲尿
	LOCAL:14 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:マゾっ気 + ABL:MASTER:サドっ気		;;小便シャワー
	LOCAL:15 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:欲望								;;キス
	;;------------------------------------------
	;;分類・ソフト
	LOCAL:20 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:露出癖 + ABL:MASTER:マゾっ気 + パンツ種類									;;ぱんつ脱がす
	LOCAL:21 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:従順 + ABL:MASTER:欲望 + 10 - GETPALAMLV(PALAM:MASTER:快Ｂ,10)				;;Ｂ愛撫
	LOCAL:22 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:従順 + ABL:MASTER:欲望 + 10 - GETPALAMLV(PALAM:MASTER:快Ｃ,10)				;;Ｃ愛撫
	LOCAL:23 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:従順 + ABL:MASTER:欲望 + 10 - GETPALAMLV(PALAM:MASTER:快Ｖ,10)				;;Ｖ愛撫
	LOCAL:24 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:従順 + ABL:MASTER:欲望 + 10 - GETPALAMLV(PALAM:MASTER:快Ａ,10)				;;Ａ愛撫
	LOCAL:25 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:奉仕精神 + 10 - GETPALAMLV(PALAM:MASTER:習得,10) + ABL:MASTER:指			;;手扱き
	LOCAL:26 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:奉仕精神 + 10 - GETPALAMLV(PALAM:MASTER:習得,10) + ABL:MASTER:舌			;;口奉仕
	LOCAL:27 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:奉仕精神 + 10 - GETPALAMLV(PALAM:MASTER:習得,10) + ABL:MASTER:胸			;;パイズリ
	LOCAL:28 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:奉仕精神 + 10 - GETPALAMLV(PALAM:MASTER:習得,10) + ABL:MASTER:腰			;;素股
	LOCAL:29 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + CFLAG:MASTER:屈服度 / (10 * LIMIT(10 - CFLAG:MASTER:WC_訪問者受け入れ具合,1,10))		;;屈服レイプ
	;;------------------------------------------
	;;分類・ハード
	LOCAL:30 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:腰 + ABL:MASTER:膣			;;Ｖふぁっく
	LOCAL:31 = RAND:10 + 服装_接触ボーナス - 服装_接触ペナ + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:腰 + ABL:MASTER:アナル		;;Ａふぁっく
	LOCAL:32 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:従順 + 10 - GETPALAMLV(PALAM:MASTER:屈服,10) + ABL:MASTER:露出癖				;;前後ローター
;	LOCAL:33 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:従順 + 10 - GETPALAMLV(PALAM:MASTER:屈服,10) + ABL:MASTER:欲望					;;貞操帯
	LOCAL:34 = RAND:10 + 服装_観賞ボーナス - 服装_観賞ペナ + ABL:MASTER:従順 + 10 - GETPALAMLV(PALAM:MASTER:屈服,10) + MAXBASE:MASTER:勃起/100			;;ペニス用貞操帯
	;;------------------------------------------
	;;分類・特殊
	LOCAL:40 = RAND:10 + ABL:MASTER:親密 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:奉仕精神													;;お持ち帰り
	LOCAL:41 = RAND:10 + ABL:MASTER:従順 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:露出癖													;;人里・散歩
	LOCAL:42 = RAND:10 + ABL:MASTER:従順 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:舌														;;人里・口奉仕
	LOCAL:43 = RAND:10 + ABL:MASTER:従順 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:腰 + EXP:MASTER:売春経験									;;人里・売春
	LOCAL:44 = RAND:10 + ABL:MASTER:従順 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:膣 + ABL:MASTER:アナル									;;人里・肉便器
	LOCAL:45 = RAND:10 + ABL:MASTER:従順 + ABL:MASTER:欲望 + GETPALAMLV(PALAM:MASTER:欲情,10) + ABL:MASTER:奉仕精神 + ABL:MASTER:露出癖 + EXP:MASTER:売春経験		;;人里・お披露目プレイ
	;;------------------------------------------
	;;オトコペナ
	;;ふぁっくするくらいに状況が進むと、ペナにならないと考える
	FOR LOOP_CNT,1,30
		LOCAL:LOOP_CNT -= オトコペナ
	NEXT
	;;------------------------------------------
	;;さらにランダマイズ
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT = RAND:(MAX(LOCAL:LOOP_CNT,1))
	NEXT
	;;------------------------------------------
	;;ペニス用貞操帯を着けていたら、日数が経過しているほど発生しやすく
	SIF CFLAG:MASTER:ペニス用貞操帯着用
		LOCAL:34 *= TIME_PROGRESS(CFLAG:MASTER:ペニス用貞操帯着用)/1440
	;;------------------------------------------
	;;[13：飲尿]以上に、訪問者受け入れ具合を乗算
	;;　[=0]だと発生しなくなるし、受け入れ具合が進んでるほど発生しやすくなる
	;;　受け入れ具合の細かな条件は「ACTABLE」で
	FOR LOOP_CNT,13,50
		LOCAL:LOOP_CNT *= CFLAG:MASTER:WC_訪問者受け入れ具合
	NEXT
	;;------------------------------------------
	;;条件判定
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT *= WC_SexHara_ACTABLE(LOOP_CNT)
	NEXT
	;;------------------------------------------
	;;セクハラ内容決定
	LOCAL:300 = 0
	FOR LOOP_CNT,1,100
		IF LOCAL:LOOP_CNT > LOCAL:300
			LOCAL:300 = LOCAL:LOOP_CNT
			セクハラ内容 = LOOP_CNT
		ENDIF
	NEXT
	CFLAG:MASTER:WC_訪問者の今回セクハラ = セクハラ内容
RETURN

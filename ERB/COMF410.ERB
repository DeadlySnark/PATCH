;-------------------------------------------------
;掃除
;-------------------------------------------------
@COM410
SELECTCASE FLAG:(100 + CFLAG:MASTER:現在位置)
	CASE IS < 500
		TFLAG:194 = 0
	CASE IS < 2000
		TFLAG:194 = 1
	CASE IS < 5000
		TFLAG:194 = 2
	CASEELSE
		TFLAG:194 = 3
ENDSELECT
IF TARGET <= 0
	;掃除した場所が誰かの私室の場合その人物のぱんつをゲット（子悪魔どうしよう…）
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 場所_パチュリー私室
			LOCAL = 人物_パチュリー
		CASE 場所_小悪魔私室
			LOCAL = 人物_小悪魔
		CASE 場所_守衛小屋
			LOCAL = 人物_美鈴
		CASE 場所_咲夜私室
			LOCAL = 人物_咲夜
		CASE 場所_レミリア私室
			LOCAL = 人物_レミリア
		CASE 場所_地下室
			LOCAL = 人物_フラン
		CASE 場所_チルノの家
			LOCAL = 人物_チルノ
		CASE 場所_大妖精の家
			LOCAL = 人物_大妖精
		CASE 場所_魔理沙の家
			LOCAL = 人物_魔理沙
		CASE 場所_博麗神社
			LOCAL = 人物_霊夢
		CASE 場所_ルーミアの住処
			LOCAL = 人物_ルーミア
		CASE 場所_アリスの家
			LOCAL = 人物_アリス
		CASEELSE
			LOCAL = 0
	ENDSELECT
	;訪問者の方が影響力大
	IF CFLAG:LOCAL:屈服度 > CFLAG:LOCAL:好感度
		LOCAL:1 = 今日のぱんつADULT(LOCAL, 1)
	ELSE
		LOCAL:1 = 今日のぱんつ(LOCAL)
	ENDIF
	IF FLAG:(100 + CFLAG:MASTER:現在位置) > RAND:5000 && LOCAL && LOCAL:1
		;ぱんつGET!
		TSTR:3 = %CALLNAME:LOCAL%{LOCAL:1}/
;		CSTR:MASTER:12 += TSTR:2
		IF GETBIT(FLAG:匂いフェチ,1) 
			TSTR:2 = %IRUINAME_SMELL(LOCAL:1)%%PANTSNAME(LOCAL:1)%
		ELSE
			TSTR:2 = %PANTSNAME(LOCAL:1)%
		ENDIF
	ENDIF
ENDIF
IF TARGET && ABL:親密 * 500 + ABL:従順 * 500 + ABL:清掃技能 * 1000 > CFLAG:館内地位 - CFLAG:MASTER:館内地位
	TFLAG:コマンド成功度 = 1
ELSE
	TFLAG:コマンド成功度 = 0
ENDIF
IF TFLAG:コマンド成功度 && !CFLAG:睡眠
	FLAG:(100 + CFLAG:MASTER:現在位置) = FLAG:(100 + CFLAG:MASTER:現在位置) / (3 + ABL:MASTER:清掃技能 + ABL:清掃技能)
ELSE
	FLAG:(100 + CFLAG:MASTER:現在位置) = FLAG:(100 + CFLAG:MASTER:現在位置) / (2 + ABL:MASTER:清掃技能)
ENDIF

DOWNBASE:MASTER:体力 = 50
DOWNBASE:MASTER:気力 = 100

IF TARGET > 0 && !CFLAG:睡眠
	;固定で獲得するソース
	SOURCE:歓楽 = 400

	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:親密 <= 3
		SOURCE:歓楽 += 200 + (ABL:従順 * 60)
	ELSEIF ABL:親密 <= 5
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:親密 <= 8
		SOURCE:歓楽 += 750 + (ABL:従順 * 75)
	ELSEIF ABL:親密 <= 11
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
	ENDIF

	;好感度をみる
	IF CFLAG:好感度 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:好感度 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
	SOURCE:征服 = 300 + 100 * ABL:サドっ気
ENDIF

TIME += 30
IF TFLAG:コマンド成功度 && !CFLAG:睡眠
	EXP:清掃経験 ++
	EXP:奉仕快楽経験 ++
ENDIF
EXP:MASTER:清掃経験 ++
RETURN 1

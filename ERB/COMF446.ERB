;-------------------------------------------------
;撮影
;-------------------------------------------------
@COM446
	#DIM キャラ番号
	#DIM 部屋番号
	#DIM 撮影する部屋
	PRINTL Where do you want to peek in?
	FOR 部屋番号,0,MAXROOM()
		IF CAN_MOVE(CFLAG:MASTER:現在位置,部屋番号)
			FOR キャラ番号,1,CHARANUM
				SIF CFLAG:キャラ番号:現在位置 == 部屋番号
					SETCOLOR 色設定_マップ_他人
			NEXT
			PRINTFORML %"["%{部屋番号,2}%"] - "+GETPLACENAME(部屋番号)%
			RESETCOLOR
		ENDIF
	NEXT
	PRINTL [900] - Return
	$INPUT_LOOP
	INPUT
	撮影する部屋 = RESULT
	TFLAG:現在覗いている場所 = 0
	SIF 撮影する部屋 == 900
		RETURN 0
	IF CAN_MOVE(CFLAG:MASTER:現在位置,撮影する部屋)
		CALL PEEP(撮影する部屋)
		TIME += 3
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;撮影処理
@PEEP(部屋番号)
	#DIM キャラ番号
	#DIM 部屋番号
	#DIM 撮影枚数
	#DIM 写真内容
	#DIM 攻め側
	#DIM 受け側

	撮影枚数 = 0
	IF FLAG:(300+部屋番号) && !HAS_DUPLICATE_KEY(部屋番号) && 部屋番号 != 場所_あなた私室
		;鍵がかかっていて、合鍵を持っていない場合
		PRINTL The door was locked...
		RETURN 0
	ENDIF
	TFLAG:現在覗いている場所 = 部屋番号
	;住人同士の交流は先に判定する
	IF ITEM:フィルム > 1
		FOR キャラ番号,1,CHARANUM
			IF CFLAG:キャラ番号:現在位置 == 部屋番号
				IF 0 != CFLAG:キャラ番号:住人同士イベント内容 
					;攻め・受けを判定
					IF CFLAG:キャラ番号:住人同士イベント内容 % 10 == 0
						受け側 = キャラ番号
						攻め側 = CFLAG:キャラ番号:住人同士イベント相手
					ELSE
						受け側 = CFLAG:キャラ番号:住人同士イベント相手
						攻め側 = キャラ番号
					ENDIF
					撮影枚数++
					写真内容 = CHOOSE_PHOTO2(攻め側,受け側,部屋番号)
					IF 写真内容 <= 写真_住人同士_キス
						PRINTFORML You took the photography of %CALLNAME:攻め側% and %CALLNAME:受け側%%PHOTO_NAME_TAKING(写真内容,1)%.
						TSTR:3 = %CALLNAME:攻め側%と%CALLNAME:受け側%{写真内容}PH/
					ELSE
						PRINTFORML You took the photography of %CALLNAME:攻め側% %PHOTO_NAME_TAKING(写真内容)% %CALLNAME:受け側%.
						TSTR:3 = %CALLNAME:攻め側%が%CALLNAME:受け側%{写真内容}PH/
					ENDIF
					;PRINTFORML %CALLNAME:攻め側%と%CALLNAME:受け側%の{写真内容}を撮影した
					CFLAG:MASTER:収集品死守戦フラグ ++
					CSTR:MASTER:収集品 += TSTR:3
					ITEM:フィルム--
					BREAK
				ENDIF
			ENDIF
		NEXT
	ENDIF
	;住人同士の交流で撮影できなければ通常の処理
	IF 0 == 撮影枚数
		FOR キャラ番号,1,CHARANUM
			IF ITEM:フィルム < 1
				;フィルムが無ければ終了
				RETURN 0
			ELSEIF CFLAG:キャラ番号:現在位置 == 部屋番号
				撮影枚数++
				写真内容 = CHOOSE_PHOTO(キャラ番号,部屋番号)
				PRINTFORML You took the photography of %CALLNAME:キャラ番号%%PHOTO_NAME_TAKING(写真内容)%.
				CFLAG:MASTER:収集品死守戦フラグ ++
				TSTR:3 = %CALLNAME:キャラ番号%{写真内容}PH/
				CSTR:MASTER:収集品 += TSTR:3
				ITEM:フィルム--
			ENDIF
		NEXT
	ENDIF
	IF !撮影枚数
		PRINTL Nobody was there...
		RETURN 0
	ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;写真の名称を取得する
@PHOTO_NAME(ARG)
	#FUNCTIONS
	SELECTCASE ARG
		CASE 写真_普段
			RETURNF "Normal Photo"
		CASE 写真_着替え
			RETURNF "Changing The Clothes"
		CASE 写真_入浴
			RETURNF "Taking A Bath"
		CASE 写真_寝顔
			RETURNF "Sleeping Face"
		CASE 写真_料理
			RETURNF "Cooking"
		CASE 写真_居眠り
			RETURNF "Dozing Off"
		CASE 写真_読書
			RETURNF "Reading A Book"
		CASE 写真_トイレ
			RETURNF "Sitting On The Toilet"
		CASE 写真_NTR_キス
			RETURNF "Kissing Visitor"
		CASE 写真_NTR_愛撫
			RETURNF "Caressed By Visitor"
		CASE 写真_NTR_胸愛撫
			RETURNF "Chest Caress By Visitor"
		CASE 写真_NTR_フェラチオ
			RETURNF "Sucking Off Visitor's Dick"
		CASE 写真_NTR_シックスナイン
			RETURNF "69 With Visitor"
		CASE 写真_NTR_素股
			RETURNF "Thighjob With Visitor"
		CASE 写真_NTR_Ｖセックス
			RETURNF "Having Sex With Visitor"
		CASE 写真_NTR_Ａセックス
			RETURNF "Having Anal Sex With Visitor"
		CASE 写真_自慰
			RETURNF "Masturbating"
		CASE 写真_NTR_接触
			RETURNF "Skinship With Visitor"
		CASE 写真_NTR_抱擁
			RETURNF "Hugging Visitor"
		CASE 写真_住人同士_会話
			RETURNF "Having Conversation"
		CASE 写真_住人同士_お茶
			RETURNF "Drinking Tea"
		CASE 写真_住人同士_接触
			RETURNF "Skinshipping"
		CASE 写真_住人同士_抱擁
			RETURNF "Hugging"
		CASE 写真_住人同士_キス
			RETURNF "Kissing"
		CASE 写真_住人同士_愛撫
			RETURNF "Crotch Caressing"
		CASE 写真_住人同士_Ａ愛撫
			RETURNF "Anus Caressing"
		CASE 写真_住人同士_胸愛撫
			RETURNF "Breast Caressing"
		CASE 写真_住人同士_Ｖ性交
			RETURNF "Having Sex"
		CASE 写真_住人同士_Ａ性交
			RETURNF "Having Anal Sex"
	ENDSELECT


;When taking photo
;ARG:1 if there's multiple people (though probably not needed)
@PHOTO_NAME_TAKING(ARG,ARG:1)
	#FUNCTIONS
	SELECTCASE ARG
		CASE 写真_普段
			RETURNF " who " + \@ARG:1 ?were looking# was looking\@ + " pretty normal" 
		CASE 写真_着替え
			RETURNF " who " + \@ARG:1 ?were changing# was changing\@ + " the clothes"
		CASE 写真_入浴
			RETURNF " who " + \@ARG:1 ?were taking# was taking\@ + " a bath"
		CASE 写真_寝顔
			RETURNF "'s sleeping face" + \@ARG:1 ?s#\@
		CASE 写真_料理
			RETURNF " in the middle of cooking"
		CASE 写真_居眠り
			RETURNF " dozing off"
		CASE 写真_読書
			RETURNF " reading a book"
		CASE 写真_トイレ
			RETURNF " sitting on the toilet"
		CASE 写真_NTR_キス
			RETURNF " kissing a Visitor"
		CASE 写真_NTR_愛撫
			RETURNF " being caressed by a Visitor"
		CASE 写真_NTR_胸愛撫
			RETURNF " having their chest caressed by a Visitor"
		CASE 写真_NTR_フェラチオ
			RETURNF " sucking off Visitor's dick"
		CASE 写真_NTR_シックスナイン
			RETURNF " in 69 position together with Visitor"
		CASE 写真_NTR_素股
			RETURNF " and Visitor performing a thighjob on them"
		CASE 写真_NTR_Ｖセックス
			RETURNF " having sex with Visitor"
		CASE 写真_NTR_Ａセックス
			RETURNF " having anal sex with Visitor"
		CASE 写真_自慰
			RETURNF " in the middle of masturbation"
		CASE 写真_NTR_接触
			RETURNF " having an intimate contact with Visitor"
		CASE 写真_NTR_抱擁
			RETURNF " hugging and embracing Visitor"
		CASE 写真_住人同士_会話
			RETURNF "speaking with"
		CASE 写真_住人同士_お茶
			RETURNF "drinking tea with"
		CASE 写真_住人同士_接触
			RETURNF "having an intimate contact with"
		CASE 写真_住人同士_抱擁
			RETURNF "hugging and embracing"
		CASE 写真_住人同士_キス
			RETURNF "kissing with"
		CASE 写真_住人同士_愛撫
			RETURNF "caressing crotch of"
		CASE 写真_住人同士_Ａ愛撫
			RETURNF "caressing anus of"
		CASE 写真_住人同士_胸愛撫
			RETURNF "caressing breasts of"
		CASE 写真_住人同士_Ｖ性交
			RETURNF "having sex with"
		CASE 写真_住人同士_Ａ性交
			RETURNF "having anal sex with"
	ENDSELECT

;-------------------------------------------------------------------------------
;撮影した写真の内容取得
;@return 写真内容コード
@CHOOSE_PHOTO(キャラ番号,部屋番号)
	#FUNCTION
	#DIM キャラ番号
	#DIM 部屋番号
	;訪問者と一緒で、何かしらの行為中であれば寝取られ姿の撮影
	IF CFLAG:キャラ番号:現在位置 == FLAG:訪問者の現在位置 && FLAG:訪問者との行為 != 0
		SELECTCASE FLAG:訪問者との行為
			CASE NTR_行為_キス
				RETURNF 写真_NTR_キス
			CASE NTR_行為_愛撫
				RETURNF 写真_NTR_愛撫
			CASE NTR_行為_胸愛撫
				RETURNF 写真_NTR_胸愛撫
			CASE NTR_行為_フェラチオ
				RETURNF 写真_NTR_フェラチオ
			CASE NTR_行為_シックスナイン
				RETURNF 写真_NTR_シックスナイン
			CASE NTR_行為_素股
				RETURNF 写真_NTR_素股
			CASE NTR_行為_Ｖ正常位 TO NTR_行為_Ｖ背面立位
				RETURNF 写真_NTR_Ｖセックス
			CASE NTR_行為_Ａ正常位 TO NTR_行為_Ａ背面立位
				RETURNF 写真_NTR_Ａセックス
		ENDSELECT
	ENDIF
	;訪問者と一緒で、住人からのカウンターセクハラの場合
	IF CFLAG:キャラ番号:現在位置 == FLAG:訪問者の現在位置 && FLAG:訪問者との行為 == 0
		SELECTCASE CFLAG:キャラ番号:NTRカウンターセクハラ
			CASE NTR_CNT_キス, NTR_CNT_ディープキス
				RETURNF 写真_NTR_キス
			CASE NTR_CNT_胸を揉ませる
				RETURNF 写真_NTR_胸愛撫
			CASE NTR_CNT_身体摺り寄せ, NTR_CNT_耳元に息, NTR_CNT_股間をまさぐる
				RETURNF 写真_NTR_接触
			CASE NTR_CNT_背面抱擁, NTR_CNT_対面抱擁
				RETURNF 写真_NTR_抱擁
		ENDSELECT
	ENDIF
	;訪問者との行為中でなければ通常の撮影
	SELECTCASE 部屋番号
		CASE GET_PRIVATE_ROOM(キャラ番号)
			IF CFLAG:(キャラ番号):睡眠
				RETURNF 写真_寝顔
			ELSEIF CFLAG:キャラ番号:自慰中
				RETURNF 写真_自慰
			ELSEIF RAND(2) == 1
				RETURNF 写真_着替え
			ENDIF
		CASE 場所_正門
			IF CSVNAME(NO:キャラ番号) == "紅美鈴" && RAND(3) == 1
				RETURNF 写真_居眠り
			ENDIF
		CASE 場所_図書室
			IF RAND(2) == 1
				RETURNF 写真_読書
			ENDIF
		CASE 場所_厨房
			RETURNF 写真_料理
		CASE 場所_大浴場
			RETURNF 写真_入浴
		CASE 場所_屋外トイレ,場所_1Fトイレ,場所_2Fトイレ
			RETURNF 写真_トイレ
		CASEELSE
			RETURNF 写真_普段
	ENDSELECT
RETURNF 写真_普段

;-------------------------------------------------------------------------------
;撮影した写真の内容取得
;@return 写真内容コード
@CHOOSE_PHOTO2(キャラ番号,キャラ番号2,部屋番号)
	#FUNCTION
	#DIM キャラ番号
	#DIM キャラ番号2
	#DIM 部屋番号
	#DIM 行為

	行為 = CFLAG:キャラ番号:住人同士イベント内容 / 10
	SELECTCASE 行為
		CASE 住人同士行為_会話
			RETURNF 写真_住人同士_会話
		CASE 住人同士行為_お茶
			RETURNF 写真_住人同士_お茶
		CASE 住人同士行為_スキンシップ,住人同士行為_尻を撫でる
			RETURNF 写真_住人同士_接触
		CASE 住人同士行為_抱きつく
			RETURNF 写真_住人同士_抱擁
		CASE 住人同士行為_キスする
			RETURNF 写真_住人同士_キス
		CASE 住人同士行為_クリ愛撫,住人同士行為_指挿れ
			RETURNF 写真_住人同士_愛撫
		CASE 住人同士行為_アナル愛撫
			RETURNF 写真_住人同士_Ａ愛撫
		CASE 住人同士行為_胸愛撫
			RETURNF 写真_住人同士_胸愛撫
		CASE 住人同士行為_Ｖ性交Ｎ,住人同士行為_Ｖ性交Ｃ,住人同士行為_Ｖ後戯Ｎ,住人同士行為_Ｖ後戯Ｃ
			RETURNF 写真_住人同士_Ｖ性交
		CASE 住人同士行為_Ａ性交Ｎ,住人同士行為_Ａ性交Ｃ,住人同士行為_Ａ後戯Ｎ,住人同士行為_Ａ後戯Ｃ
			RETURNF 写真_住人同士_Ａ性交
	ENDSELECT
;フェイルセーフ
RETURNF 写真_住人同士_会話

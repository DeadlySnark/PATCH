;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 売春について 実行判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM300_13_ENABLE
	#DIM 奴隷

	奴隷 = TARGET:0
	RESULTS:0 = %"[13]-"+CALLNAME:奴隷+"の売春について"%
	IF TCVAR:奴隷:会話不能
		RETURN 0
	ELSEIF TALENT:奴隷:公衆便所 == 0
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 売春について話す
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_13
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	調教者 = PLAYER:0
	;売春人数で分岐
	PRINTFORM %CALLNAME:(TARGET:0)%はこれまでに
	SELECTCASE EXP:(TARGET:0):浮気人数-1
		CASE IS <= 5
			PRINTFORML 片手で数えられるくらいの数の相手と寝たと言った。
		CASE IS <= 10
			PRINTFORML 両手で数えられるくらいの数の相手と寝たと言った。
		CASE IS <= 20
			PRINTFORML 両手でも数えられないくらいの数の相手と寝たと言った。
		CASEELSE
			PRINTFORML 寝た相手の数はもう覚えていないと言った。
	ENDSELECT
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_13_01(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_13_01(奴隷, 調教者)
	ENDFUNC
	;前回の売春時に寝た人数
	IF EXP:奴隷:前回浮気人数 > 0
		PRINTL
		PRINTFORML この前は{EXP:奴隷:前回浮気人数}人と寝たと言った。
		TRYCALLLIST
			FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_13_01_3(奴隷, 調教者)
			FUNC KOJO_MESSAGE_COM_KU_300_13_01_3(奴隷, 調教者)
		ENDFUNC
	ENDIF
	PRINTL
	;恋慕、鍵の有無で分岐
	IF TALENT:奴隷:恋慕
		IF FLAG:貞操帯鍵購入フラグ == 奴隷
			PRINTFORML %CALLNAME:奴隷%は%MSG_LOVER(奴隷)%である%CALLNAME:調教者%が自分を買ってくれて嬉しいと言った。
		ELSE
			PRINTFORML %CALLNAME:奴隷%はたとえ%MSG_LOVER(奴隷)%でも、鍵を買わないと自分とはセックスできないと言った。
		ENDIF
	ELSE
		IF FLAG:貞操帯鍵購入フラグ == 奴隷
			PRINTFORML %CALLNAME:奴隷%は%CALLNAME:調教者%が自分を買ってくれて嬉しいと言った。
		ELSE
			PRINTFORML %CALLNAME:奴隷%は鍵を買わないと自分とはセックスできないと言った。
		ENDIF
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_13_02(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_13_02(奴隷, 調教者)
	ENDFUNC
RETURN 1
;RETURN 0

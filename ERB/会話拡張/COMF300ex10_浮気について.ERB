;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 浮気について話す 実行判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM300_10_ENABLE
	#DIM 奴隷

	奴隷 = TARGET:0
	RESULTS:0 = %"[10]-"+CALLNAME:奴隷+"の浮気について"%
	IF TCVAR:奴隷:会話不能
		RETURN 0
	ELSEIF TALENT:奴隷:浮気公認 > 0
		RETURN 1
	ELSEIF STRCOUNT(SAVESTR:10,@"%CALLNAME:奴隷+"15PH/"%") + STRCOUNT(SAVESTR:10,@"%CALLNAME:奴隷+"16PH/"%")
		;浮気の証拠がある
		RETURN 1
	ELSEIF NTR_CHK_FAVORABLY(奴隷, FAV_主人より高い) == 0 && NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう) == 0 && NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前) == 0
		RETURN 0
	ELSEIF TALENT:奴隷:恋慕 == 0
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 浮気について話す
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
;SOURCEは合計300ぐらいの変化でいいかな
@COM300_10
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	調教者 = PLAYER:0
	;会話累積値を増やしたりの処理は共通、時間は少し多めに
	LOSEBASE:気力 += 100
	DOWNBASE:調教者:気力 += 100
	DOWNBASE:奴隷:気力 += 100
	TCVAR:会話累積値 += 200 / (3 + ABL:MASTER:話術技能)
	TIME += 15
	;-----------------------------------------------------------------------------
	;会話不能があると失敗
	IF TCVAR:奴隷:会話不能
		PRINTFORML %CALLNAME:奴隷+"に話しかけようとしたが言葉が見つからない"%
		PRINTFORML %"あたりに気まずい空気が流れた……"%
		TFLAG:コマンド成功度 = -1
		RETURN 0
	ELSE
		;特殊会話すると会話不能が増える
		TCVAR:奴隷:会話不能 ++
	ENDIF
	;-----------------------------------------------------------------------------
	EXP:MASTER:会話経験 ++
	TFLAG:コマンド成功度 = 0
	IF TALENT:奴隷:浮気公認 > 0
		;すでに浮気公認してるなら
		CALL COM300_10_02
	ELSE
		;まだ浮気公認していないなら
		CALL COM300_10_01
	ENDIF
	; どちらの場合もここでフラグを消す。
    CFLAG:奴隷:NTR処女喪失 = 0
RETURN RESULT:0
RETURN 0

;-------------------------------------------------------------------------------
;浮気について問い詰める
@COM300_10_01
	#DIM 奴隷
	#DIM 調教者
	#DIM 浮気の証拠がある = 0

	奴隷 = TARGET:0
	調教者 = PLAYER:0
	浮気の証拠がある = STRCOUNT(SAVESTR:10,@"%CALLNAME:奴隷+"15PH/"%") + STRCOUNT(SAVESTR:10,@"%CALLNAME:奴隷+"16PH/"%")
	PRINTL
	PRINTFORM %CALLNAME:調教者+"が"+NTR_NAME(0)+"との関係を指摘すると"%
	IF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前)
		IF CFLAG:汚れ発覚 == 汚れ発覚_あり
			;精液汚れを見つけられた場合
			PRINTFORML %CALLNAME:奴隷+"はあっさりと浮気を認め、何か問題があるのかと問い返した。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:挑発 += 200
			SOURCE:奴隷:逸脱 += 100
		ELSEIF (CFLAG:覗き発覚回数 > 0 && CFLAG:覗き発覚時行為 > 0) || (CFLAG:同席目撃回数 > 0 && CFLAG:同席目撃時行為 > 0)
			;覗きバレしていた場合
			PRINTFORML %CALLNAME:奴隷+"はあっさりと浮気を認め、助けもせずに覗き続けていたのは"+MSG_RE_LOVER(奴隷)+"が犯される様を見て興奮したからなのかと問い返した。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:挑発 += 200
			SOURCE:奴隷:逸脱 += 100
		ELSE
			;寝取られ寸前なら、証拠が無くても隠そうとしない
			PRINTFORML %CALLNAME:奴隷+"はあっさりと浮気を認め、何か問題があるのかと問い返した。"%
			SOURCE:奴隷:逸脱 += 300
		ENDIF
	ELSEIF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう)
		;寝取られそうなら嘘を吐こうとする
		IF CFLAG:汚れ発覚 == 汚れ発覚_あり
			;精液汚れを見つけられた場合、逆ギレで開き直る
			PRINTFORML %CALLNAME:奴隷+"はあっさりと浮気を認め、何か問題があるのかと問い返した。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:反感 += 300
		ELSEIF (CFLAG:覗き発覚回数 > 0 && CFLAG:覗き発覚時行為 > 0) || (CFLAG:同席目撃回数 > 0 && CFLAG:同席目撃時行為 > 0)
			;覗きバレしていた場合
			PRINTFORML %CALLNAME:奴隷+"は自分の浮気を棚に上げ、どうして助けもせずに覗き続けていたのか、"+MSG_RE_LOVER(奴隷)+"が犯される様を見て興奮するのかと問い返した。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:反感 += 300
		ELSEIF 浮気の証拠がある > 0
			;浮気の証拠がある場合、嘘を暴かれてしまう
			PRINTFORML %CALLNAME:奴隷+"は浮気などしていないと嘘を吐いたが、証拠の写真を見せつけられると慌てて言い訳を始めた。"%
			SOURCE:奴隷:反感 += 100
			SOURCE:奴隷:恐怖 += 200
		ELSE
			;浮気の証拠がない場合、嘘と責任転嫁で切り抜ける
			PRINTFORML %CALLNAME:奴隷+"は浮気などしていないと怒りだし、"+MSG_RE_LOVER(奴隷)+"を疑った事を激しく非難した。"%
			SOURCE:奴隷:反感 += 150
			SOURCE:奴隷:恐怖 += 100
			SOURCE:奴隷:鬱屈 += 50
			TFLAG:コマンド成功度 = -1
			;会話はここで終わる
			RETURN 0
		ENDIF
	ELSE
		;それ以外の場合
		IF CFLAG:汚れ発覚 == 汚れ発覚_あり
			;精液汚れを見つけられた場合、強姦だったということにして切り抜ける
			PRINTFORML %CALLNAME:奴隷+"は強姦されたのだと主張した。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:反感 += 150
			SOURCE:奴隷:鬱屈 += 150
			TFLAG:コマンド成功度 = -1
			;会話はここで終わる
			RETURN 1
		ELSEIF (CFLAG:覗き発覚回数 > 0 && CFLAG:覗き発覚時行為 > 0) || (CFLAG:同席目撃回数 > 0 && CFLAG:同席目撃時行為 > 0)
			;覗きバレしていた場合、強姦だったということにして切り抜ける
			PRINTFORML %CALLNAME:奴隷+"はどうして強姦されるのを見ていながら助けてくれなかったのか、自分は助ける価値もないのか、それでも"+MSG_LOVER(奴隷)+"なのかと罵った。"%
			CFLAG:覗き発覚回数 = 0
			CFLAG:覗き発覚時行為 = 0
			CFLAG:同席目撃回数 = 0
			CFLAG:同席目撃時行為 = 0
			SOURCE:奴隷:反感 += 150
			SOURCE:奴隷:鬱屈 += 150
			TFLAG:コマンド成功度 = -1
			;会話はここで終わる
			RETURN 1
		ELSEIF 浮気の証拠がある > 0
			;浮気の証拠がある場合、嘘を暴かれてしまう
			PRINTFORML %CALLNAME:奴隷+"は浮気などしていないと嘘を吐いたが、証拠の写真を見せつけられると何も言えなくなってしまったようだ。"%
			SOURCE:奴隷:恐怖 += 300
		ELSE
			;浮気の証拠がない場合、嘘と責任転嫁で切り抜ける
			PRINTFORML %CALLNAME:奴隷+"は浮気などしていない、自分を信じていないのかと非難した。"%
			SOURCE:奴隷:反感 += 100
			SOURCE:奴隷:恐怖 += 100
			SOURCE:奴隷:鬱屈 += 100
			TFLAG:コマンド成功度 = -1
			;会話はここで終わる
			RETURN 0
		ENDIF
	ENDIF
	;ここで表示する口上は「それで、どうしたいの？」とかそんな感じ？
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_01(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_01(奴隷, 調教者)
	ENDFUNC
	PRINTL
	PRINTFORML %"[1]-浮気をするのは許せない"%
	PRINTFORML %"[2]-浮気をしても別にいい"%
	PRINTFORML %"[3]-むしろ積極的に浮気して欲しい"%
	$COM300_10_01_INPUT_LOOP
	INPUT
	TRYCCALLFORM COM300_10_01_%TOSTR(RESULT:0,"00")%
		RETURN RESULT:0
	CATCH
		CLEARLINE 1
		GOTO COM300_10_01_INPUT_LOOP
	ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;浮気について話す - 許せない
@COM300_10_01_01
	#DIM 屈服度減少分
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTFORM %CALLNAME:調教者+"が浮気をするのは許せないと言うと、"%
	IF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前)
		;寝取られ寸前ならもう手遅れ
		PRINTFORML %CALLNAME:奴隷+"は浮気をやめるつもりは無いから、自分と別れてくれても構わないと言った。"%
	ELSEIF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう)
		;寝取られそうなぐらいなら、止めさせるきっかけになる程度まで屈服度が下がる
		;屈服度が1000以上あなたの好感度以下まで減少
		PRINTFORML %CALLNAME:奴隷+"は、渋々と頭を下げて、もうしないと約束した。"%
		屈服度減少分 = MIN(CFLAG:奴隷:屈服度-1000,CFLAG:奴隷:屈服度-CFLAG:奴隷:好感度)
	ELSE
		;それ以外なら身体を許さない程度まで屈服度が下がる
		;屈服度が500まで減少
		PRINTFORML %CALLNAME:奴隷+"は、もう二度としないから許してほしい、と泣き出した。"%
		屈服度減少分 = MAX(0,CFLAG:奴隷:屈服度-500)
	ENDIF
	;口上表示は屈服度再計算前
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_01_01(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_01_01(奴隷, 調教者)
	ENDFUNC
	IF 屈服度減少分 > 0
		PRINTFORML %"(屈服度 -"%{屈服度減少分}%")"%
		CFLAG:奴隷:屈服度 -= 屈服度減少分
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;浮気について話す - 許せる
@COM300_10_01_02
	#DIM 屈服度減少分
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTFORM %CALLNAME:調教者+"が浮気をしても別にいいと言うと、"%
	IF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前)
		;寝取られ寸前
		PRINTFORML %CALLNAME:奴隷+"は、浮気をされる側じゃなく、する側になってもかまわないか、と訊ねてきた……。"%
	ELSEIF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう)
		;寝取られそうなら、別れ話と思い少し不安を感じる
		;屈服度が１割下がる
		PRINTFORML %CALLNAME:奴隷+"は、自分と別れるつもりなのかと訝しんでいるものの、一応は納得したようだ……。"%
		屈服度減少分 = MAX(0,CFLAG:奴隷:屈服度/10)
	ELSE
		;それ以外なら自分のしたことを悔いる
		;屈服度が1000まで減少
		PRINTFORML %CALLNAME:奴隷+"は、そうではないのだと必死に言い訳していたが、やがて不承不承ながら聞き入れた。"%
		屈服度減少分 = MAX(0,CFLAG:奴隷:屈服度-1000)
	ENDIF
	;浮気公認レベルが最低1に
	TALENT:奴隷:浮気公認 = MAX(1,TALENT:奴隷:浮気公認)
	;口上表示は屈服度再計算前
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_01_02(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_01_02(奴隷, 調教者)
	ENDFUNC
	IF 屈服度減少分 > 0
		PRINTFORML %"(屈服度 -"%{屈服度減少分}%")"%
		CFLAG:奴隷:屈服度 -= 屈服度減少分
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;浮気について話す - 寝取られたい
@COM300_10_01_03
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTFORM %CALLNAME:調教者+"がむしろ積極的に浮気して欲しいと言うと、"%
	IF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前)
		;寝取られ寸前なら
		PRINTFORML %CALLNAME:奴隷+"はやっぱり"+MSG_RE_LOVER(奴隷)+"を寝取られるのに興奮する変態だったのね、と笑った。"%
	ELSEIF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう)
		;寝取られそうなら
		PRINTFORML %CALLNAME:奴隷+"は理解はできないけれど、そう言うのなら遠慮はしないことにする、と言った。"%
	ELSE
		;それ以外なら
		PRINTFORML %CALLNAME:奴隷+"は何を言っているのかわからないと狼狽えていたが、やがて不承不承ながら聞き入れた。"%
	ENDIF
	;浮気公認レベルが最低1に
	TALENT:奴隷:浮気公認 = MAX(1,TALENT:奴隷:浮気公認)
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_01_03(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_01_03(奴隷, 調教者)
	ENDFUNC
RETURN 0

;-------------------------------------------------------------------------------
;浮気公認について
@COM300_10_02
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTL
	PRINTFORM %CALLNAME:調教者+"が"+NTR_NAME(0)+"との関係を訊ねると、"%
	IF TALENT:奴隷:妊娠
		PRINTFORML %"子供が産まれるまではお預けなのだと答えた。"%
	ELSEIF (CFLAG:奴隷:NTR訪問者と最後にセックスした日時/(24*60)) == (DATETIME()/(24*60))
		;同じ日にセックスしてたら
		SELECTCASE CFLAG:奴隷:NTR訪問者と最後にセックスした日時
			CASE IS > (DATETIME() - 1*01*60) ;１時間以内
				PRINTFORML %"今セックスしたばかりなのだと答えた。"%
			CASE IS > (DATETIME() - 1*04*60) ;４時間以内
				PRINTFORML %"先程セックスしたところなのだと答えた。"%
			CASEELSE
				PRINTFORML %"今日もセックスしたのだと答えた。"%
		ENDSELECT
	ELSE
		SELECTCASE CFLAG:奴隷:NTR訪問者と最後にセックスした日時
			CASE IS > (DATETIME() - 1*01*60) ;１時間以内
				PRINTFORML %"今セックスしたばかりなのだと答えた。"%
			CASE IS > (DATETIME() - 1*08*60) ;８時間以内
				PRINTFORML %"先程セックスしたところなのだと答えた。"%
			CASE IS > (DATETIME() - 1*24*60) ;１日以内
				PRINTFORML %"昨日もセックスしたのだと答えた。"%
			CASE IS > (DATETIME() - 3*24*60) ;２日以内
				PRINTFORML %"昨日はセックスして貰えなかったのだと答えた。"%
			CASEELSE
				PRINTFORML %"もう随分とセックスしていないのだと答えた。"%
		ENDSELECT
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_02(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_02(奴隷, 調教者)
	ENDFUNC
	PRINTL
	PRINTFORML %"[1]-やっぱり浮気はされたくない"%
	PRINTFORML %"[2]-いまのままでいい"%
	PRINTFORML %"[3]-もっと積極的に浮気して欲しい"%
	$COM300_10_02_INPUT_LOOP
	INPUT
	TRYCCALLFORM COM300_10_02_%TOSTR(RESULT:0,"00")%
		RETURN RESULT:0
	CATCH
		CLEARLINE 1
		GOTO COM300_10_02_INPUT_LOOP
	ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;浮気公認について - やっぱりやだ
@COM300_10_02_01
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTFORM %CALLNAME:調教者+"がやっぱり浮気はされたくないと言うと、"%
	IF TALENT:奴隷:公衆便所 && TALENT:奴隷:親愛
		;親愛つき公衆便所は０にはならない
		TALENT:奴隷:浮気公認 = 1
		PRINTFORML %CALLNAME:奴隷+"に自分もこんなことを続けたくないが、脅迫されているので止めることができない、と言われてしまった。"%
	ELSEIF TALENT:奴隷:公衆便所 && TALENT:奴隷:浮気公認 >= 4
		;公衆便所の場合4以下にはならない
		TALENT:奴隷:浮気公認 = 4
		PRINTFORML %CALLNAME:奴隷+"にそんなことを言われても、"+NTR_NAME(0)+"との関係は止められないし、売春もやめない、と言われてしまった。"%
	ELSEIF CHK_NTR_SATISFACTORY(奴隷) 
		;快楽を覚えてしまっている場合０にはならない
		TALENT:奴隷:浮気公認 = 1
		PRINTFORML %CALLNAME:奴隷+"にそんなことを言われても、もう"+CALLNAME:調教者+"とのセックスでは満足できないし、"+NTR_NAME(0)+"との関係は止められそうにない、と言われてしまった。"%
	ELSE
		;快楽を覚えていない場合０になる
		TALENT:奴隷:浮気公認 = 0
		PRINTFORML %CALLNAME:奴隷+"は自分から誘ったりはしないけれど、"+NTR_NAME(0)+"に誘われたら断れないかも知れない、と困った顔を見せた。"%
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_02_01(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_02_01(奴隷, 調教者)
	ENDFUNC
RETURN 1
RETURN 0

;-------------------------------------------------------------------------------
;浮気公認について - いまのままで
@COM300_10_02_02
	#DIM 奴隷
	#DIM 調教者

	奴隷 = TARGET:0
	PRINTFORM %CALLNAME:調教者+"がいまのままでいいと言うと、"%
	IF TALENT:奴隷:公衆便所 && TALENT:奴隷:親愛
		PRINTFORML %CALLNAME:奴隷+"は本当にいいのか、と不安そうだった。"%
	ELSEIF CHK_NTR_SATISFACTORY(奴隷) 
		IF TALENT:奴隷:浮気公認 < MARK:奴隷:浮気快楽刻印
			PRINTFORML %CALLNAME:奴隷+"はもっとしたいのに、と残念そうだった。"%
		ELSE
			PRINTFORML %CALLNAME:奴隷+"は本当にいいのか、と嬉しそうだった。"%
		ENDIF
	ELSE
		PRINTFORML %CALLNAME:奴隷+"は本当にいいのか、と不安そうだった。"%
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_02_02(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_02_02(奴隷, 調教者)
	ENDFUNC
RETURN 1
RETURN 0

;-------------------------------------------------------------------------------
;浮気公認について - もっと寝取られたい
@COM300_10_02_03
	#DIM 奴隷
	#DIM 調教者
	#DIMS MSG0
	
	奴隷 = TARGET:0
    調教者 = PLAYER:0
	MSG0 = %"笑った。"%
	IF TALENT:奴隷:公衆便所 && TALENT:奴隷:親愛
		MSG0 = %"言った。"%
	ENDIF
	PRINTFORM %CALLNAME:調教者+"がもっと積極的に浮気して欲しいと言うと、"%
	;現在の所最大で４まで。将来的に５まで拡張したい
	TALENT:奴隷:浮気公認 = MIN(4,TALENT:奴隷:浮気公認+1)
	SELECTCASE TALENT:奴隷:浮気公認
		CASE 1 ;最低１からスタートなので発生しないはず
			PRINTFORML %CALLNAME:奴隷+"はそれなら期待に応えられるようにする、と"+MSG0%
		CASE 2
			PRINTFORML %CALLNAME:奴隷+"はそれなら"+CALLNAME:調教者+"の見てる前で"+NTR_NAME(0)+"を誘惑してみる、と"+MSG0%
		CASE 3
			PRINTFORML %CALLNAME:奴隷+"はそれなら"+NTR_NAME(0)+"に犯される所を"+CALLNAME:調教者+"に見せてあげる、と"+MSG0%
		CASE 4
			PRINTFORML %CALLNAME:奴隷+"はそれなら"+NTR_NAME(0)+"の家に時々泊まりに行くようにする、と"+MSG0%
		CASEELSE ;５以上
			PRINTFORML %CALLNAME:奴隷+"はそれなら"+NTR_NAME(0)+"の妻として向こうに住み、"+CALLNAME:調教者+"にも時々逢いに来てあげる、と"+MSG0%
	ENDSELECT
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{奴隷}_300_10_02_03(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_10_02_03(奴隷, 調教者)
	ENDFUNC
RETURN 1
RETURN 0

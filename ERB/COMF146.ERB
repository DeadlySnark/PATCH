;-------------------------------------------------
;浣腸器＋プラグ
;ハードな調教コマンド、レベル4
;-------------------------------------------------
@COM146

	TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_左尻)
	SIF RESULT
		RETURN 0

	;着脱の処理
	TEQUIP:浣腸 = !TEQUIP:浣腸
	IF !TEQUIP:浣腸
		IF !GETBIT(CFLAG:3,7)
			EXP:異常経験 += 1
			SETBIT CFLAG:異常経験,7
		ENDIF
	ENDIF
	;-------------------------------------------------
	;COM別FLAGの設定
	;-------------------------------------------------
	;行為者
	TCVAR:116 = PLAYER
	;-------------------------------------------------
	;ソース追加処理
	;-------------------------------------------------
	;消費体力、気力
	DOWNBASE:体力 += 60
	DOWNBASE:気力 += 150

	SOURCE:快Ａ = 300
	SOURCE:苦痛 = 500
	SOURCE:露出 = 300
	SOURCE:屈従 = 200
	SOURCE:不潔 = 500
	SOURCE:逸脱 = 500
	SOURCE:反感 = 500

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 500

	;Aへの苦痛は先に処理しておく
	CALL PAIN_CHECK_A(TARGET)
	;-------------------------------------------------
	;汚れの処理
	;-------------------------------------------------
	STAIN:腸内 |= STAIN:Ａ
	STAIN:Ａ |= STAIN:腸内
	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;Ａ経験
	EXP:Ａ経験 += 4
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM146(奴隷)
	#DIM 奴隷
	#DIM 苦痛増分
	#DIM 反感増分
	PRINTL ＜Enema＋Butt Plug Attached＞

	DOWNBASE:奴隷:体力 += 60
	DOWNBASE:奴隷:気力 += 150

	SOURCE:奴隷:快Ａ += 150
	SOURCE:奴隷:屈従 += 100
	SOURCE:奴隷:不潔 += 250
	SOURCE:奴隷:逸脱 += 250

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:奴隷:自己愛 < 0 || TALENT:奴隷:抵抗
		SOURCE:奴隷:鬱屈 += 250

	;Aへの苦痛は先に処理しておく
	;EXP:Ａ経験をみる
	苦痛増分 = 250
	反感増分 = 250
	IF EXP:奴隷:Ａ経験 < EXPLV:1
		TIMES 苦痛増分 , 3.00
		TIMES 反感増分 , 3.00
	ELSEIF EXP:奴隷:Ａ経験 < EXPLV:2
		TIMES 苦痛増分 , 1.20
		TIMES 反感増分 , 2.00
	ELSEIF EXP:奴隷:Ａ経験 < EXPLV:3
		TIMES 苦痛増分 , 0.80
		TIMES 反感増分 , 1.50
	ELSEIF EXP:奴隷:Ａ経験 < EXPLV:4
		TIMES 苦痛増分 , 0.60
		TIMES 反感増分 , 1.20
	ELSEIF EXP:奴隷:Ａ経験 < EXPLV:5
		TIMES 苦痛増分 , 0.40
	ELSEIF EXP:奴隷:Ａ経験 >= EXPLV:5
		TIMES 苦痛増分 , 0.20
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:奴隷:潤滑 < PALAMLV:1
		TIMES 苦痛増分 , 1.20
		TIMES 反感増分 , 2.00
	ELSEIF PALAM:奴隷:潤滑 < PALAMLV:2
		TIMES 苦痛増分 , 1.00
	TIMES 反感増分 , 1.50
	ELSEIF PALAM:奴隷:潤滑 < PALAMLV:3
		TIMES 苦痛増分 , 0.60
	ELSEIF PALAM:奴隷:潤滑 < PALAMLV:4
		TIMES 苦痛増分 , 0.30
	ELSEIF PALAM:奴隷:潤滑 >= PALAMLV:4
		TIMES 苦痛増分 , 0.20
	ENDIF
	SOURCE:奴隷:苦痛 += 苦痛増分
	SOURCE:奴隷:反感 += 反感増分

	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;Ａ経験
	EXP:奴隷:Ａ経験 += 1


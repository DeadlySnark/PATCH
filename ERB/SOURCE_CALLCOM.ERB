@CALL_COM(ARG)
;シックスナイン
IF (TEQUIP:ARG:口 == 1 || TEQUIP:ARG:口 == 2) && (TEQUIP:ARG:ペニス == 4 || TEQUIP:ARG:クリトリス == 4) && (SELECTCOM == 1 || SELECTCOM == 81)
	TFLAG:50 = 1
;岩清水
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 1
	TFLAG:50 = 2
;Gスポット刺激 && PREVCOM == SELECTCOM && TFLAG:103 == ARG
ELSEIF (SELECTCOM == 3 || SELECTCOM == 316 || SELECTCOM == 603) && TFLAG:103 == ARG 
	RESULT = 0
	CALLFORM COM_ABLE503
	IF !FLAG:FingeringCheck && RESULT
		PRINTL How would you like to finger?
		PRINTL [0]Gently
		PRINTFORML [1]All the way in \@TALENT:ARG:0 ? (This will take %CALLNAME:ARG%'s %VIRGINITY(ARG)%)# \@
		PRINTL [2]Never ask again and use default behavior (Fingering -> G Spot Stimulation)
		$INPUT_LOOP
		INPUT
		IF RESULT == 0
		ELSEIF RESULT == 1
			TFLAG:50 = 3
		ELSEIF RESULT == 2
			SIF PREVCOM == SELECTCOM
				TFLAG:50 = 3
			FLAG:FingeringCheck = 1
			PRINTW You can enable this back in game options.
		ELSE
			CLEARLINE 1
			REUSELASTLINE
			GOTO INPUT_LOOP
		ENDIF
		
	ELSE
		SIF PREVCOM == SELECTCOM
			TFLAG:50 = 3
	ENDIF
;乱れ牡丹
ELSEIF TEQUIP:ARG:羞恥プレイ && SELECTCOM == 68
	TFLAG:50 = 4
;手淫フェラ
ELSEIF (TEQUIP:ARG:指 == 1 && TEQUIP:ARG:口２ == 1) || (TEQUIP:ARG:口 == 1 && TEQUIP:ARG:指２ == 1)
	;フェラ-手淫
	IF TEQUIP:ARG:指 == 1
		TFLAG:194 = 0
	;手淫-フェラ
	ELSE
		TFLAG:194 = 1
	ENDIF
	TFLAG:50 = 5
;母乳飲み
ELSEIF SELECTCOM == 10 && TALENT:ARG:母乳体質
	IF TEQUIP:ARG:指２ == 1
	TFLAG:50 = 16
	ELSE
	TFLAG:50 = 14
	ENDIF
;挿入Ｇスポット責め、子宮口責め、交互挿入
ELSEIF (SELECTCOM == 60 || SELECTCOM == 61 || SELECTCOM == 65 || SELECTCOM == 67 || SELECTCOM == 68) 
	IF TEQUIP:ARG:体位 == TEQUIP:ARG:体位２ && TFLAG:103 == TARGET && TEQUIP:ARG:膣２ == 1
		IF SELECTCOM == 65 || RAND:2
			TFLAG:50 = 7
		ELSE
			TFLAG:50 = 6
		ENDIF
	ENDIF
	;交互挿入
	IF TEQUIP:ARG:膣 == 1 && (MASTER_POSE(6,1,1) && MASTER_POSE(6,1,1) != ARG)
		TFLAG:50 = 13
	ENDIF
;シックスナインパイズリ
ELSEIF TEQUIP:ARG:胸 == 1 && (TEQUIP:ARG:ペニス == 4 || TEQUIP:ARG:クリトリス == 4) && (SELECTCOM == 1 || SELECTCOM == 82)
	TFLAG:50 = 8
;ダブルフェラ
ELSEIF TEQUIP:ARG:口 == 1 && (MASTER_POSE(4,1,1) && MASTER_POSE(4,1,1) != ARG )
	TFLAG:50 = 9
;ダブル素股
ELSEIF TEQUIP:ARG:クリトリス == 1 && (MASTER_POSE(2,1,1) && MASTER_POSE(2,1,1) != ARG )
	TFLAG:50 = 10
;ダブルパイズリ
ELSEIF TEQUIP:ARG:胸 == 1 && (MASTER_POSE(5,1,1) && MASTER_POSE(5,1,1) != ARG )
	TFLAG:50 = 11
;パイズリフェラ
ELSEIF (TEQUIP:ARG:胸 == 1 && TEQUIP:ARG:口２ == 1) || (TEQUIP:ARG:口 == 1 && TEQUIP:ARG:胸２ == 1)
	TFLAG:50 = 12
;授乳する
ELSEIF SELECTCOM == 11 && TALENT:MASTER:母乳体質
	TFLAG:50 = 15
;授乳手コキ
ELSEIF TEQUIP:ARG:指２ == 1 && SELECTCOM == 10
	TFLAG:50 = 16
ENDIF

RESULT = 0
IF TFLAG:50
	CALLFORM COM_ABLE{500 + TFLAG:50}
	IF RESULT
		TRYCALLFORM CAN_SCOM{TFLAG:50}(1)
		IF RESULT
			TRYCCALLFORM CAN_SCOM{TFLAG:50}
			CATCH
				PRINTFORML %STR:(1500 + TFLAG:50)%
			ENDCATCH
			STR:0 = %STR:(1500 + TFLAG:50)%
			TRYCALLFORM SCOM{TFLAG:50}
		ENDIF
	ENDIF
ENDIF
SIF RESULT
	RETURN RESULT
TFLAG:50 = 0
LOCALS = %STR:(1000 + SELECTCOM)%

TRYCCALLFORM CAN_COM{SELECTCOM}
CATCH
	PRINTFORML %LOCALS%
ENDCATCH
STR:0 = %LOCALS%
TRYCALLFORM COM{SELECTCOM}
RETURN RESULT

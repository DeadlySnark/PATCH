
@NTR陥落イベント(奴隷, イベント種別)
	#DIM 奴隷
	#DIM イベント種別	;0=NTR取得 1=公衆便所取得

	IF イベント種別 == 0
		CALL NTR陥落イベント_NTR口上(奴隷)
	ENDIF

	CFLAG:奴隷:NTR陥落イベントフラグ = 2
RETURN


@NTR陥落イベント_NTR口上(奴隷)
	#DIM 奴隷
	#DIM 写真番号0
	#DIM 写真番号1
	#DIM 写真番号2
	
	PRINTFORMW %CALLNAME:MASTER%が寝支度をしていると、メイド妖精が%CALLNAME:MASTER%のもとに封筒を届けに来た。
	PRINTFORMW %CALLNAME:MASTER%は封筒を調べた、…どうやら差出人は%CALLNAME:奴隷%のようだ
	PRINTW 開けてみると中には何枚かの写真が束ねられて入っていた。
	PRINTL
	PRINTFORMW %CALLNAME:MASTER%はその写真の束の一番上の写真を見ると
	PRINTW そこには最近よく館に訪れている男と
	PRINT 何故か
	SELECTCASE RAND:3
		CASE 0
			PRINT 下着姿
		CASE 1
			PRINT 上半身裸
		CASE 2
			PRINT 全裸
	ENDSELECT
	PRINTFORMW の%CALLNAME:奴隷%が写っていた。

	PRINTFORMW %CALLNAME:MASTER%は何か嫌な予感がしつつも次の写真に目を通すと
	PRINTW そこには
	写真番号0 = RAND:9
	WHILE 11
		写真番号1 = RAND:13
		SIF 写真番号1 != 写真番号0
			BREAK
	WEND
	WHILE 11
		写真番号2 = RAND(9, 16)
		SIF 写真番号2 != 写真番号0 && 写真番号2 != 写真番号1
			BREAK
	WEND
	PRINTFORMW %NTR陥落イベント_写真(写真番号0, 奴隷)%%CALLNAME:奴隷%の姿があった。
	PRINTL
	PRINTL 他にも
	PRINTFORMW %NTR陥落イベント_写真(写真番号1, 奴隷)%写真や
	PRINTFORML %NTR陥落イベント_写真(写真番号2, 奴隷)%写真など
	PRINTFORMW 計{10 + RAND:20}枚の写真が入っていた。
	PRINTL
	PRINTFORMW %CALLNAME:MASTER%はそれらの写真を見て%CALLNAME:奴隷%が寝取られた事に気付かされた。
	PRINTL

	;イベント的にいけるとおもったら[SKIP○○]を消してくだしあ
[SKIPSTART]
	IF NTR陥落イベント_自慰条件(MASTER)
		PRINTFORML すると%CALLNAME:MASTER%は溜まりに溜まった性欲と
		PRINTFORML 愛する人をとられた事へのショックでおかしくなってしまったのか
		PRINTFORMW その送られてきた写真をおかずにして自慰を始めた
		PRINTL
	
		;経験処理
		VARSET LOCAL
		IF HAS_PENIS(MASTER)
			LOCAL:1 += CFLAG:MASTER:性欲 / 10
			LOCAL = LOCAL:1
		ENDIF
		IF HAS_VAGINA(MASTER)
			LOCAL:2 = 1 + RAND:(CFLAG:MASTER:性欲 / 10)
			LOCAL:3 = MAX(LOCAL:2 / (10 - ABL:MASTER:Ｃ感覚), 1)
			LOCAL:4 = 1 + RAND:(CFLAG:MASTER:性欲 / 10)
			SIF ABL:MASTER:Ｂ感覚
				LOCAL:5 = MAX(LOCAL:4 / (10 - ABL:MASTER:Ｂ感覚), 1)
			IF ABL:MASTER:Ｖ感覚
				LOCAL:6 = 1 + RAND:(CFLAG:MASTER:性欲 / 10)
				LOCAL:7 = MAX(LOCAL:6 / (10 - ABL:MASTER:Ｖ感覚), 1)
			ENDIF
			IF ABL:MASTER:Ａ感覚
				LOCAL:8 = 1 + RAND:(CFLAG:MASTER:性欲 / 10)
				LOCAL:9 = MAX(LOCAL:8 / (10 - ABL:MASTER:Ａ感覚), 1)
			ENDIF
			LOCAL = LOCAL:3 + LOCAL:5 + LOCAL:7 + LOCAL:9
		ENDIF
	
		PRINTFORML 絶頂経験（%CALLNAME:MASTER%） +{LOCAL}
		SIF LOCAL:1
			PRINTFORML 射精経験（%CALLNAME:MASTER%） +{LOCAL:1}
		SIF LOCAL:2
			PRINTFORML Ｃ経験（%CALLNAME:MASTER%） +{LOCAL:2}
		SIF LOCAL:6
			PRINTFORML Ｖ経験（%CALLNAME:MASTER%） +{LOCAL:6}
		SIF LOCAL:8
			PRINTFORML Ａ経験（%CALLNAME:MASTER%） +{LOCAL:8}
		SIF LOCAL:4
			PRINTFORML Ｂ経験（%CALLNAME:MASTER%） +{LOCAL:4}
		SIF LOCAL:3
			PRINTFORML Ｃ絶頂経験（%CALLNAME:MASTER%） +{LOCAL:3}
		SIF LOCAL:7
			PRINTFORML Ｖ絶頂経験（%CALLNAME:MASTER%） +{LOCAL:7}
		SIF LOCAL:9
			PRINTFORML Ａ絶頂経験（%CALLNAME:MASTER%） +{LOCAL:9}
		SIF LOCAL:5
			PRINTFORML Ｂ絶頂経験（%CALLNAME:MASTER%） +{LOCAL:5}
		PRINTW
		CFLAG:MASTER:性欲 = 0
		EXP:MASTER:絶頂経験 += LOCAL
		EXP:MASTER:射精経験 += LOCAL:1
		EXP:MASTER:Ｃ経験 += LOCAL:2
		EXP:MASTER:Ｃ絶頂経験 += LOCAL:3
		EXP:MASTER:Ｂ経験 += LOCAL:4
		EXP:MASTER:Ｂ絶頂経験 += LOCAL:5
		EXP:MASTER:Ｖ経験 += LOCAL:6
		EXP:MASTER:Ｖ絶頂経験 += LOCAL:7
		EXP:MASTER:Ａ経験 += LOCAL:8
		EXP:MASTER:Ａ絶頂経験 += LOCAL:9
		PRINTL 
		PRINTW やがて最後の絶頂に達すると、電池が切れたかの様にベッドに突っ伏してそのまま眠りについてしまった。
		PRINTL
		PRINTL
	ENDIF
[SKIPEND]
RETURN

@NTR陥落イベント_写真(写真番号, 奴隷)
	#FUNCTIONS
	#DIM 写真番号
	#DIM 奴隷

	SELECTCASE 写真番号
		;写真１、２
		CASE 0
			RETURNF "後ろから突かれて、快楽に惚けてだらしない顔をしている"
		CASE 1
			RETURNF "男と座位で交じり合い、男の体に手足を絡めている"
		CASE 2
			RETURNF "両足を持たれ、背後から突かれながらもカメラに向かって笑顔でピースをする"
		CASE 3
			RETURNF "男の前に跪いて、いきり立つペニスを頬張る"
		CASE 4
			RETURNF "男と舌を絡めあっている"
		CASE 5
			RETURNF "胸を揉まれてトロンとした表情をうかべている"
		CASE 6
			RETURNF "クリトリスを摘まれて、強い刺激に顔を赤く染めながらも歯を食いしばっている"
		CASE 7
			RETURNF "乳首を摘まれ、強い刺激に口をあんぐりと開いている"
		CASE 8
			RETURNF "両手で尻たぶをひらいてペニスをねだっている"
		;写真２、３
		CASE 9
			IF !TALENT:奴隷:処女
				RETURNF "精液を注がれた膣を男に指で開かれて、恥ずかしくも嬉しそうに笑っている"
			ELSE
				RETURNF "精液を注がれた尻穴を男に指で開かれて、恥ずかしくも嬉しそうに笑っている"
			ENDIF
		CASE 10
			RETURNF "男とのキスをアップで撮影した"
		CASE 11
			IF !TALENT:奴隷:処女
				RETURNF "男と繋がった局部のみをアップで撮影した"
			ELSE
				RETURNF "男と繋がった菊門のみをアップで撮影した"
			ENDIF
		CASE 12
			IF !TALENT:奴隷:処女
				RETURNF "割れ目を自らの指で開いて、漏れていく精液をローアングルから写した"
			ELSE
				RETURNF "アヌスを自らの指で開いて、漏れていく精液をローアングルから写した"
			ENDIF
		;写真３
		CASE 13
			RETURNF "色んな液体の染みついたベッドに座り、お腹に手を触れて幸せそうな表情をみせている"
		CASE 14
			RETURNF "ベッドに横たわり、男と汗ばんだ体をくっつけて満面の笑顔を見せている"
		CASE 15
			RETURNF "皺だらけのベッドに横たわり、余韻に浸った甘い表情でカメラを見つめている"
		;CASE 
		;	RETURNF ""
	ENDSELECT


@NTR陥落イベント_自慰条件(ARG)
	#FUNCTION
	IF TALENT:ARG:マゾ
		RETURNF 1
	ELSEIF ABL:ARG:マゾっ気 >= 5 && CFLAG:ARG:性欲 >= 30
		RETURNF 1
	ELSEIF ABL:ARG:マゾっ気 == 4 && CFLAG:ARG:性欲 >= 60
		RETURNF 1
	ELSEIF ABL:ARG:マゾっ気 == 3 && CFLAG:ARG:性欲 >= 90
		RETURNF 1
	ELSEIF ABL:ARG:マゾっ気 == 2 && CFLAG:ARG:性欲 >= 120
			RETURNF 1
	ELSEIF ABL:ARG:マゾっ気 == 1 && CFLAG:ARG:性欲 >= 150
		RETURNF 1
	ENDIF

	RETURNF 0



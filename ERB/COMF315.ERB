;クリ愛撫
@COM315

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_恥丘)
SIF RESULT
	RETURN 0

TRYCALL NTR_UNLOCK_CHASTITY_BELT(TARGET)
IF !RESULT
	DRAWLINE
	PRINTFORML %CALLNAME:MASTER% reached %HIS_HER(MASTER)% hands, trying to caress %CALLNAME:TARGET%'s clitoris,
	PRINTFORML However %CALLNAME:MASTER% couldn't do so since %HE_SHE(TARGET)% wore a chastity belt.
	CFLAG:TARGET:ぱんつ確認 = 1

	IF CFLAG:TARGET:好感度 > 0
		PRINTFORML Looks like %CALLNAME:TARGET% is being dissatisfied with the fact that you were unable to touch %HIM_HER(TARGET)%...
		;欲情していると反感が伸びる
		PALAM:TARGET:反感 += MIN( PALAM:TARGET:欲情/MAX(CFLAG:TARGET:好感度,1),300)
		;欲情していると好感度も下がる
		CFLAG:TARGET:好感度 = MAX( CFLAG:TARGET:好感度 - MIN(PALAM:TARGET:欲情 ,30),0)
	ELSE
		PRINTFORML %CALLNAME:TARGET% looked at %CALLNAME:MASTER%, seemingly being completely uninterested...
	ENDIF
	RETURN 0
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:歓楽 = 100
SOURCE:情愛 = 150
SOURCE:欲情 = 150
SOURCE:露出 = 100
SOURCE:反感 = 370
SOURCE:快Ｃ = 100
SOURCE:受動 = 145 + 270 * ABL:従順

;手を突っ込めない衣装
IF TEQUIP:0 & 16
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:欲情 , 0.80
	TIMES SOURCE:露出 , 0.80
;スカート以外の何かを履いてる
ELSEIF TEQUIP > 1
	TIMES SOURCE:快Ｃ , 0.90
	TIMES SOURCE:欲情 , 0.90
	TIMES SOURCE:露出 , 0.90
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:Ｖ |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:Ｖ

TIME += 10

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF

RETURN 1

;多生児出産(ryパッチ======================================
;新規追加フラグ
;FLAG:53 多生児出産と(ryパッチVerUP用フラグ
;FLAG:54 訪問者の子供の数

;CFLAG:85 子供の人数
;CFLAG:86 父親の何人目の子供(子供用フラグ)
;CFLAG:87 母親の何人目の子供(子供用フラグ)
;CFLAG:88 里子に出した数

;CFLAG:353 何人出産するか(出産後は育児中の人数となる)
;CFLAG:354 多生児の兄弟用フラグ


;====================================================
;SHOP.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜



;PREGNACY_S.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜

;出産直前に処理---------------------------------
@生まれる人数, ARG
#DIM 最大出産可能人数

最大出産可能人数 = 人物最大 - CHARANUM
;産まれる人数確認
CFLAG:ARG:出産数 = 1
;獣系(素質[猫舌][動物耳][狐][妖狐]のいずれかを持つ)は多生児を産みやすい
IF TALENT:ARG:猫舌 || TALENT:ARG:動物耳 || TALENT:ARG:狐 || TALENT:ARG:妖狐
	SIF RAND:多生児出産強度(ARG, 80) == 0
		CFLAG:ARG:出産数 = 2
	SIF RAND:多生児出産強度(ARG, 120) == 0
		CFLAG:ARG:出産数 = 3
	SIF RAND:多生児出産強度(ARG, 160) == 0
		CFLAG:ARG:出産数 = 4
	SIF RAND:多生児出産強度(ARG, 260) == 0
		CFLAG:ARG:出産数 = 5
	SIF RAND:多生児出産強度(ARG, 500) == 0
		CFLAG:ARG:出産数 = 6
	SIF RAND:多生児出産強度(ARG, 650) == 0
		CFLAG:ARG:出産数 = 7
	SIF RAND:多生児出産強度(ARG, 800) == 0
		CFLAG:ARG:出産数 = 8
ELSE
	SIF RAND:多生児出産強度(ARG, 100) == 0
		CFLAG:ARG:出産数 = 2
	SIF RAND:多生児出産強度(ARG, 250) == 0
		CFLAG:ARG:出産数 = 3
	SIF RAND:多生児出産強度(ARG, 500) == 0
		CFLAG:ARG:出産数 = 4
	SIF RAND:多生児出産強度(ARG, 750) == 0
		CFLAG:ARG:出産数 = 5
	SIF RAND:多生児出産強度(ARG, 1000) == 0
		CFLAG:ARG:出産数 = 6
	SIF RAND:多生児出産強度(ARG, 2000) == 0
		CFLAG:ARG:出産数 = 7
	SIF RAND:多生児出産強度(ARG, 3000) == 0
		CFLAG:ARG:出産数 = 8
ENDIF
;最大人数のチェック
IF (最大出産可能人数 < CFLAG:ARG:出産数)
	CFLAG:ARG:出産数 = 最大出産可能人数 
ENDIF


SIF FLAG:生理機能追加パッチ等debug
	TRYCALL 生まれる人数DEBUG, ARG

@親子関係追加取得, ARG, ARG:1, ARG:2
;ARG   子供
;ARG:1 ARGの父
;ARG:2 ARGの母

;父母の子供の人数フラグ設定
;父親が訪問者なら訪問者用の人数フラグ処理
IF ARG:1 >= CHARANUM
	FLAG:訪問者の子供の数 ++
ELSE
	CFLAG:(ARG:1):85 ++
ENDIF
CFLAG:(ARG:2):85 ++

;子供が父母の何人目の子供かを設定
IF ARG:1 >= CHARANUM
	CFLAG:ARG:父親の子番号 = FLAG:訪問者の子供の数
ELSE
	CFLAG:ARG:父親の子番号 = CFLAG:(ARG:1):85
ENDIF
CFLAG:ARG:母親の子番号 = CFLAG:(ARG:2):85

;母親及び父親がMASTERではない時の出産口上---------------------
@両親がMASTER以外の出産口上, ARG, ARG:1
;ARG   母親の番号
;ARG:1 子供の番号

PRINTFORMW %CALLNAME:ARG% gave birth to %N_T_R(CFLAG:(ARG:1):73, 1)%'s \@ CFLAG:ARG:出産数 != 1 ?{CFLAG:ARG:出産数} children# child\@.



;生まれた人数に応じて口上変化---------------------
@多生児出産口上, ARG, ARG:1
;設定上八つ子まで
SIF ARG:1 == 0
	PRINT It's a 
SELECTCASE ARG
	CASE 2
		PRINT twin
	CASE 3
		PRINT triplet
	CASE 4
		PRINT quadruplet
	CASE 5
		PRINT quintuplet
	CASE 6
		PRINT sextuplet
	CASE 7
		PRINT septuplet
	CASE 8
		PRINT octuplet
ENDSELECT
PRINT s
SIF ARG:1 == 0
	PRINTW !
;SIF ARG:1 == 1
;	PRINT の子

@多生児フラグ処理, ARG, ARG:1
;ARG 生まれる子供
;ARG:1 現在の繰り返し番号

;一番目の子
IF ARG:1 == 0
	LOCAL:1 = 0
	;他の子供をチェックし、CFLAG:354の最大数を取得
	FOR LOCAL, 0, CHARANUM
		SIF NO:LOCAL != 148 && NO:LOCAL != 149
			CONTINUE
		SIF LOCAL:1 < CFLAG:LOCAL:多生児兄弟フラグ
			LOCAL:1 = CFLAG:LOCAL:多生児兄弟フラグ
	NEXT

	CFLAG:ARG:多生児兄弟フラグ = (LOCAL:1 + 1)
ELSE
	CFLAG:ARG:多生児兄弟フラグ = CFLAG:(CHARANUM-2):多生児兄弟フラグ
	
ENDIF


@里子, ARG
;生まれる子供の数だけ追加
SIF (CFLAG:ARG:にんっしんっ - 1) < CHARANUM
	CFLAG:(CFLAG:ARG:にんっしんっ - 1):88 += CFLAG:ARG:出産数
CFLAG:ARG:里子人数 += CFLAG:ARG:出産数

SIF (CFLAG:ARG:にんっしんっ - 1) < CHARANUM
	CFLAG:(CFLAG:ARG:にんっしんっ - 1):85 += CFLAG:ARG:出産数
CFLAG:ARG:子供の人数 += CFLAG:ARG:出産数




@子供相性設定, ARG
;ARG 相性を設定するキャラ

;父親が登録外のキャラの場合の処理
IF CFLAG:ARG:父親 >= CHARANUM
	FOR LOCAL, 0, CHARANUM
		IF RELATION:(CFLAG:ARG:母親):LOCAL == 0 || RELATION:(CFLAG:ARG:母親):LOCAL == 100
			RELATION:ARG:LOCAL = 100
		ELSE
			;受け継ぐ相性が100超なら ((受け継ぐ相性-100)/2)+100
			;受け継ぐ相性が100未満なら((100-受け継ぐ相性)/2)+受け継ぐ相性
			IF RELATION:(CFLAG:ARG:母親):LOCAL > 100
				RELATION:ARG:LOCAL = ((RELATION:(CFLAG:ARG:母親):LOCAL - 100) / 2) + 100
			ELSEIF RELATION:(CFLAG:ARG:母親):LOCAL < 100
				RELATION:ARG:LOCAL = ((100 - RELATION:(CFLAG:ARG:母親):LOCAL) / 2) + RELATION:(CFLAG:ARG:母親):LOCAL
			ENDIF
		ENDIF
	NEXT
	RELATION:ARG:(CFLAG:ARG:母親) = 300
ELSE
	FOR LOCAL, 0, CHARANUM
		IF (RELATION:(CFLAG:ARG:母親):LOCAL == 0 || RELATION:(CFLAG:ARG:母親):LOCAL == 100) && (RELATION:(CFLAG:ARG:父親):LOCAL == 0 || RELATION:(CFLAG:ARG:父親):LOCAL == 100)
			RELATION:ARG:LOCAL = 100
		ELSE
			;対象LOCALに対する相性が両親共に同じか
			IF RELATION:(CFLAG:ARG:母親):LOCAL == RELATION:(CFLAG:ARG:父親):LOCAL
				;受け継ぐ相性が100超なら ((受け継ぐ相性-100)/2)+100
				;受け継ぐ相性が100未満なら((100-受け継ぐ相性)/2)+受け継ぐ相性
				IF RELATION:(CFLAG:ARG:母親):LOCAL > 100
					RELATION:ARG:LOCAL = (((RELATION:(CFLAG:ARG:母親):LOCAL - 100) / 2) + 100)
				ELSEIF RELATION:(CFLAG:ARG:母親):LOCAL < 100
					RELATION:ARG:LOCAL = (((100 - RELATION:(CFLAG:ARG:母親):LOCAL) / 2) + RELATION:(CFLAG:ARG:母親):LOCAL)
				ENDIF
			ELSE
				;両親の平均値を取得
				RELATION:ARG:LOCAL = ((RELATION:(CFLAG:ARG:母親):LOCAL + RELATION:(CFLAG:ARG:父親):LOCAL) / 2)
			ENDIF
		ENDIF
	NEXT
	RELATION:ARG:(CFLAG:ARG:父親) = 300
	RELATION:ARG:(CFLAG:ARG:母親) = 300
ENDIF


;PRINT_STATE.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
;-------------------------------------------------
;親の名前表示
;-------------------------------------------------
;親の名前をステータスに表示します
;使用変数一覧
;CFLAG:73 父親
;CFLAG:74 母親
;CFLAG:86 父親の第×子
;CFLAG:87 母親の第×子
@PRINT_PARENT(ARG)
;息子または娘でないとスキップ
SIF NO:ARG != 148 && NO:ARG != 149
	RETURN 0
VARSET LOCAL

IF CFLAG:ARG:父親 >= 人物_客
	PRINTFORM Father: Unknown 
ELSEIF CFLAG:ARG:父親 == 人物_村人
	PRINTFORM Father: Unknown 
ELSEIF CFLAG:ARG:父親 == 人物_訪問者
	PRINTFORM Father: %NTR_NAME(0)% 
	PRINTFORM ({CFLAG:ARG:父親の子番号}%CALENDAR_COUNT(CFLAG:ARG:父親の子番号)% child) 
ELSE
	PRINTFORM Father: %CALLNAME:(CFLAG:ARG:73)% 
	PRINTFORM ({CFLAG:ARG:父親の子番号}%CALENDAR_COUNT(CFLAG:ARG:父親の子番号)% child) 
ENDIF
;前二つはいらないけど一応つけておく
IF CFLAG:ARG:母親 >= 人物_客
	PRINTFORM Mother: Unknown 
ELSEIF CFLAG:ARG:母親 == 人物_村人
	PRINTFORM Mother: Unknown 
ELSEIF CFLAG:ARG:母親 == 人物_訪問者
	PRINTFORM Mother: %NTR_NAME(0)% 
	PRINTFORM ({CFLAG:ARG:母親の子番号}%CALENDAR_COUNT(CFLAG:ARG:母親の子番号)% child)
ELSE
	PRINTFORM Mother: %CALLNAME:(CFLAG:ARG:74)% 
	PRINTFORM ({CFLAG:ARG:母親の子番号}%CALENDAR_COUNT(CFLAG:ARG:母親の子番号)% child)
ENDIF

;-------------------------------------------------
;子の名前表示
;-------------------------------------------------
;子の名前をステータスに表示します
;使用変数一覧
;LOCAL:1  父母判定
;CFLAG:73 父親
;CFLAG:74 母親
@PRINT_PARENT_2(ARG)
;一人もいないとスキップ
SIF CFLAG:ARG:子供の人数 == 0 && !TALENT:ARG:妊娠
	RETURN 0
VARSET LOCAL
;ARGの子供の名前を出す
IF CFLAG:ARG:子供の人数
	PRINTL As a father of children (Between the () is the mother's name)
	FOR LOCAL:1, 0, CHARANUM
		IF (NO:(LOCAL:1) == 148 || NO:(LOCAL:1) == 149) && CFLAG:(LOCAL:1):73 == ARG
			PRINTFORM  %N_T_R(LOCAL:1)%(%N_T_R(CFLAG:(LOCAL:1):74)%)
			LOCAL += 1
			IF (LOCAL%3) == 0
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
		ENDIF
	NEXT
	LOCAL = 0
	PRINTL 
	PRINTL 
	PRINTL As a mother of children (Between the () is the father's name)
	FOR LOCAL:1, 0, CHARANUM
		IF (NO:(LOCAL:1) == 148 || NO:(LOCAL:1) == 149) && CFLAG:(LOCAL:1):74 == ARG
			PRINTFORM  %N_T_R(LOCAL:1)%(%N_T_R(CFLAG:(LOCAL:1):73)%)
			LOCAL += 1
			IF (LOCAL%3) == 0
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
		ENDIF
	NEXT
	PRINTL
	PRINTL 
	PRINTFORML  Amount of children given to the orphanage: {CFLAG:ARG:里子人数} \@CFLAG:ARG:里子人数 == 1 ? child# children\@
	PRINTFORML                                      Total: {CFLAG:ARG:子供の人数} \@CFLAG:ARG:子供の人数 == 1 ? child# children\@
	PRINTL
ENDIF

SIF TALENT:ARG:妊娠
	PRINTFORML Currently %HE_SHE(ARG)% is pregnant with %N_T_R(CFLAG:ARG:にんっしんっ - 1, 1)%'s child.


;-------------------------------------------------
;兄弟の名前表示
;-------------------------------------------------
;兄弟の名前をステータスに表示します
;里子に出された兄弟は表示されません
;使用変数一覧
;LOCAL:1  兄弟判定
;LOCAL:2  兄弟数及び兄弟の番号
;LOCAL:3  多生児の場合の人数
;LOCAL:4  多生児の兄弟判定
;CFLAG:73 父親
;CFLAG:74 母親
;CFLAG:354 多生児判定
@PRINT_PARENT_3(ARG)
#DIM 対象
#DIM 多生児関係
VARSET LOCAL

;初期キャラの兄弟関係
IF ARG == 人物_レミリア
	SETCOLOR 色設定_兄弟_自分
	PRINTFORM  1_%CALLNAME:人物_レミリア%
	RESETCOLOR
	PRINTFORM  2_%CALLNAME:人物_フラン%
ELSEIF ARG == 人物_フラン
	PRINTFORM  1_%CALLNAME:人物_レミリア%
	SETCOLOR 色設定_兄弟_自分
	PRINTFORM  2_%CALLNAME:人物_フラン%
	RESETCOLOR
ENDIF
;息子または娘でないとスキップ
SIF NO:ARG != 148 && NO:ARG != 149
	RETURN 0
多生児関係 = 0
FOR 対象, 0, CHARANUM
	;父親、母親が同じ
	IF (NO:対象 == 148 || NO:対象 == 149) && (CFLAG:対象:父親 == CFLAG:ARG:父親 || CFLAG:対象:母親 == CFLAG:ARG:母親)
		LOCAL:2 ++
		SIF 対象 == ARG
			SETCOLOR 色設定_兄弟_自分
		PRINTFORM  {LOCAL:2}_%CALLNAME:対象%
		RESETCOLOR
		;対象とARGが多生児の関係である場合
		IF CFLAG:ARG:多生児兄弟フラグ > 0 && CFLAG:対象:多生児兄弟フラグ == CFLAG:ARG:多生児兄弟フラグ
			IF 多生児関係 == 0
				FOR LOCAL:4, 0, CHARANUM
					SIF (NO:(LOCAL:4) == 148 || NO:(LOCAL:4) == 149) && CFLAG:ARG:多生児兄弟フラグ == CFLAG:(LOCAL:4):多生児兄弟フラグ
						多生児関係 ++
				NEXT
			ENDIF
			;多生児表記は対象だけ
			PRINT (
			CALL 多生児出産口上, 多生児関係, 2
			;多生児だけど父親が違う場合
			SIF CFLAG:対象:父親 != CFLAG:ARG:父親
				PRINT /Different Father
			PRINT )
		;父親が違う場合
		ELSEIF CFLAG:対象:父親 != CFLAG:ARG:父親
			PRINT (Different Father)
		;母親が違う場合
		ELSEIF CFLAG:対象:母親 != CFLAG:ARG:母親
			PRINT (Different Mother)
		ENDIF

		LOCAL += 1
		IF (LOCAL%3) == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	ENDIF
NEXT
PRINTL


;-------------------------------------------------
;出産表
;-------------------------------------------------
;子の名前をステータスに表示します
;使用変数一覧
;LOCAL   父親
;LOCAL:1 スペース
;LOCAL:2 父親(名前用)
@PRINT_PARENT_4(ARG)
;一人もいないとスキップ
SIF CFLAG:ARG:子供の人数 == 0
	RETURN 0

VARSET LOCAL

LOCALS = 　　　　　　　　　　　　　　
STRLENS LOCALS
LOCAL:1 = RESULT

PRINTL 　Father　　　　　　　　　　　　Number of births
PRINTL
FOR LOCAL, 0, 300
	SIF LOCAL == ARG
		CONTINUE
	
	IF LOCAL == CHARANUM
		PRINTL
		LOCAL = 298
	ENDIF
		
	IF LOCAL == 298
		LOCAL:2 = 人物_訪問者
	ELSEIF LOCAL == 299
		LOCAL:2 = 1000
	ELSE
		LOCAL:2 = LOCAL
	ENDIF
	
	STRLENS N_T_R(LOCAL:2, 1)
	LOCALS:1 = 
	LOCALS:1 += SUBSTRING(LOCALS, 0, (LOCAL:1 - RESULT))
	PRINTFORML 　\@ LOCAL:2 == 1000 ?Unknown#%N_T_R(LOCAL:2, 1)%\@ %LOCALS:1% {親別出産数:ARG:LOCAL}
	
	
NEXT


;-------------------------------------------------
;キャラ同士の相性表示処理
;-------------------------------------------------
@PRINT_RELATION(ARG)
PRINTL
PRINTL □ Affinity □---------------------------------------------------------------------------
SIF FLAG:生理機能追加パッチ等debug
	TRYCALL 現在位置_開始位置DEBUG, ARG
PRINTFORML
LOCAL = 0
FOR LOCAL:1,0,CHARANUM
	IF RELATION:ARG:(LOCAL:1) != 0 && RELATION:ARG:(LOCAL:1) != 100
		PRINTFORM %CALLNAME:(LOCAL:1), 14, RIGHT% 
		PRINT × 
		;色の選択
		IF RELATION:ARG:(LOCAL:1) < 60
			SETCOLOR C_RED
		ELSEIF RELATION:ARG:(LOCAL:1) < 100
			SETCOLOR 0xE9967A
		ELSEIF RELATION:ARG:(LOCAL:1) == 100
			;SETCOLOR C_YELLOW
			CONTINUE
		ELSEIF RELATION:ARG:(LOCAL:1) < 130
			SETCOLOR C_P_GREEN
		ELSEIF RELATION:ARG:(LOCAL:1) < 200
			SETCOLOR C_GREEN
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		PRINTFORM {RELATION:ARG:(LOCAL:1), 4 ,RIGHT}\%
		RESETCOLOR
		LOCAL ++
		SIF LOCAL % 3 == 0
			PRINTL 
	ENDIF
NEXT
IF LOCAL == 0
	PRINTL 　　Nobody in particular.
ELSEIF LOCAL%3 != 0
	PRINTL 
ENDIF


;その他いろいろ〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
@N_T_R(ARG, ARG:1)
#FUNCTIONS
;ARG キャラ番号ないし訪問者(998)番号

;訪問者による第１引数に引っかかる時とかにいちいちIF文とか使わないでいけるようにするもの
;%N_T_R(LOCAL, 0)%

SELECTCASE ARG:1
	CASE 0
		SELECTCASE ARG
			CASE 0 TO CHARANUM-1
				RETURNF CALLNAME:ARG
			CASE 人物_訪問者
				;RETURNF "訪問者"
				RETURNF NTR_NAME(0)
			CASE 人物_客
				RETURNF "Customer"
			CASE 人物_村人
				RETURNF "Villager"
			CASEELSE
				RETURNF "Something else whose name was not defined"
		ENDSELECT
	CASE 1
		SELECTCASE ARG
			CASE 0 TO CHARANUM-1
				RETURNF CALLNAME:ARG
			CASE 人物_訪問者
				;RETURNF "訪問者"
				RETURNF NTR_NAME(0)
			CASE 人物_客, 人物_村人
				RETURNF "Someone unknown"
			CASE IS > 人物_客
				RETURNF "Some person"
			CASEELSE
				RETURNF "Something else whose name was not defined"
		ENDSELECT
ENDSELECT


;多生児出産確率------------------
@多生児出産強度(ARG, ARG:1)
#FUNCTION

SELECTCASE CFLAG:ARG:355
	CASE 1
		;8つ子の確立、獣系でRAND:640 それ以外でRAND:2400となる
		TIMES ARG:1, 0.80
	CASE 2
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.50
	CASE 3
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.30
	CASE 4
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.15
	CASE 5
		;8つ子の確立、獣系でRAND:40 それ以外でRAND:150となる
		TIMES ARG:1, 0.05
	CASE 6
		;8つ子の確立、獣系でRAND:8 それ以外でRAND:30となる
		TIMES ARG:1, 0.01
ENDSELECT

IF ARG:1 == 1
	ARG:1 = 2
ELSEIF ARG:1 == 0
	ARG:1 = 1
ENDIF

RETURNF ARG:1


